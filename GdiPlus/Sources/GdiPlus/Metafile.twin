' A part of the GDI+ Package for TwinBasic  https://github.com/kubao/GdiPlus
' Licensed under the MIT license. (c) 2026 Sunderland Ober Consulting.
' "Windows" is a trademark of the Microsoft Corporation.
' Modeled after code and descriptions (c) Microsoft, taken from SDK headers and official documentation.
' Certain flat API function and type declarations taken from WinDevLib by fafalone, with thanks.

[COMCreatable(False)]
Class Metafile
    Inherits Image
    
    ' Playback a metafile from a HMETAFILE
    ' If deleteWmf is TRUE, then when the metafile is deleted,
    ' the hWmf will also be deleted.  Otherwise, it won't be.
    Sub New(ByVal hWmf As LongPtr, wmfPlaceableFileHeader As WmfPlaceableFileHeader, ByVal deleteWmf As Boolean = False)
        SetStatus(Native.CreateFromWmfInto(hWmf, deleteWmf, wmfPlaceableFileHeader, im))
    End Sub
    
    ' Playback a metafile from a HENHMETAFILE
    ' If deleteEmf is TRUE, then when the metafile is deleted,
    ' the hEmf will also be deleted.  Otherwise, it won't be.
    Sub New(ByVal hEmf As LongPtr, ByVal deleteWmf As Boolean = False)
        SetStatus(Native.CreateFromEmfInto(hEmf, deleteWmf, im))
    End Sub
    
    Sub New(ByVal filename$)
        SetStatus(Native.CreateFromFileInto(filename, im))
    End Sub
    
    ' Playback a WMF metafile from a file.
    Sub New(ByVal filename$, wmfPlaceableFileHeader As WmfPlaceableFileHeader)
        SetStatus(Native.CreateFromWmfFileInto(filename, wmfPlaceableFileHeader, im))
    End Sub
    
    Sub New(stream As GDIP_IStream)
        SetStatus(Native.CreateFromStreamInto(stream, im))
    End Sub
    
    ' Record a metafile to memory.
    Sub New(ByVal referenceHdc As LongPtr, ByVal type As GpEmfType = EmfTypeEmfPlusDual, Optional ByVal description$)
        SetStatus(Native.RecordInto(referenceHdc, type, ByVal vbNullPtr, MetafileFrameUnitGdi, description, im))
    End Sub

    ' Record a metafile to memory.
    Sub New(ByVal referenceHdc As LongPtr, frameRect As GpRectF, ByVal frameUnit As GpMetafileFrameUnit = MetaFileFrameUnitGdi, _
            ByVal type As GpEmfType = EmfTypeEmfPlusDual, Optional ByVal description$)
        SetStatus(Native.RecordInto(referenceHdc, type, frameRect, MetafileFrameUnitGdi, description, im))
    End Sub

    ' Record a metafile to memory.
    Sub New(ByVal referenceHdc As LongPtr, frameRect As GpRect, ByVal frameUnit As GpMetafileFrameUnit = MetaFileFrameUnitGdi, _
            ByVal type As GpEmfType = EmfTypeEmfPlusDual, Optional ByVal description$)
        SetStatus(Native.RecordIntoI(referenceHdc, type, frameRect, frameUnit, description, im))
    End Sub
    
    Sub New(ByVal fileName$, ByVal referenceHdc As LongPtr, _
            ByVal type As GpEmfType = EmfTypeEmfPlusDual, Optional ByVal description$)
        SetStatus(Native.RecordFileNameInto(fileName, referenceHdc, type, ByVal vbNullPtr, MetafileFrameUnitGdi, description, im))
    End Sub
    
    Sub New(ByVal fileName$, ByVal referenceHdc As LongPtr, _
            frameRect As GpRectF, ByVal frameUnit As GpMetafileFrameUnit = MetaFileFrameUnitGdi, _
            ByVal type As GpEmfType = EmfTypeEmfPlusDual, Optional ByVal description$)
        SetStatus(Native.RecordFileNameInto(fileName, referenceHdc, type, frameRect, frameUnit, description, im))
    End Sub
    
    Sub New(ByVal fileName$, ByVal referenceHdc As LongPtr, _
            frameRect As GpRect, ByVal frameUnit As GpMetafileFrameUnit = MetaFileFrameUnitGdi, _
            ByVal type As GpEmfType = EmfTypeEmfPlusDual, Optional ByVal description$)
        SetStatus(Native.RecordFileNameIntoI(fileName, referenceHdc, type, frameRect, frameUnit, description, im))
    End Sub
    
    Sub New(stream As GDIP_IStream, ByVal referenceHdc As LongPtr, _
            ByVal type As GpEmfType = EmfTypeEmfPlusDual, Optional ByVal description$)
        SetStatus(Native.RecordStreamInto(stream, referenceHdc, type, ByVal vbNullPtr, MetafileFrameUnitGdi, description, im))
    End Sub
    
    Sub New(stream As GDIP_IStream, ByVal referenceHdc As LongPtr, _
            frameRect As GpRectF, ByVal frameUnit As GpMetafileFrameUnit = MetaFileFrameUnitGdi, _
            ByVal type As GpEmfType = EmfTypeEmfPlusDual, Optional ByVal description$)
        SetStatus(Native.RecordStreamInto(stream, referenceHdc, type, frameRect, frameUnit, description, im))
    End Sub
    
    Sub New(stream As GDIP_IStream, ByVal referenceHdc As LongPtr, _
            frameRect As GpRect, ByVal frameUnit As GpMetafileFrameUnit = MetaFileFrameUnitGdi, _
            ByVal type As GpEmfType = EmfTypeEmfPlusDual, Optional ByVal description$)
        SetStatus(Native.RecordStreamIntoI(stream, referenceHdc, type, frameRect, frameUnit, description, im))
    End Sub
    
    Function Clone() As Metafile
        Return CloneImpl2(Of Metafile, GpMetafile)(im)
    End Function

    Property Get MetafileHeader() As MetafileHeader
        SetStatus(Native.GetHeaderFromMetafile(MetafileHeader))
    End Property
    
    ' Once this method is called, the Metafile object is in an invalid state
    ' and can no longer be used.  It is the responsiblity of the caller to
    ' invoke DeleteEnhMetaFile to delete this hEmf.
    Function GetHENHMETAFILE() As LongPtr
        SetStatus(Native.GetHemfFromMetafile(GetHENHMETAFILE))
    End Function
    
    ' Used in conjuction with Graphics::EnumerateMetafile to play an EMF+
    ' The data must be DWORD aligned if it's an EMF or EMF+.  It must be
    ' WORD aligned if it's a WMF.
    Function PlayRecord(ByVal recordType As GpEmfPlusRecordType, ByVal flags As UINT, ByVal dataSize As UINT, data As Byte) As GpStatus
        Return SetStatus(Native.PlayRecord(recordType, flags, dataSize, data))
    End Function
    
    ' If you're using a printer HDC for the metafile, but you want the
    ' metafile rasterized at screen resolution, then use this API to set
    ' the rasterization dpi of the metafile to the screen resolution,
    ' e.g. 96 dpi or 120 dpi.
    Property Let DownLevelRasterizationLimit(ByVal metafileRasterizationLimitDpi As UINT)
        SetStatus(Native.SetDownLevelRasterizationLimit(metafileRasterizationLimitDpi))
    End Property
    
    Property Get DownLevelRasterizationLimit() As UINT
        SetStatus(Native.GetDownLevelRasterizationLimit(DownLevelRasterizationLimit))
    End Property
    
'#if (GDIPVER >= 0x0110)
    Function ConvertToEmfPlus(refGraphics As Graphics, _
        Optional conversionFailureFlag&, ByVal emfType As GpEmfType = EmfTypeEmfPlusOnly, Optional ByVal description$) As GpStatus
        Dim metafile As GpMetafile
        Dim status As Any = Native.ConvertToEmfPlus(GetNative(refGraphics), Native, conversionFailureFlag, emfType, description, metafile)
        ReplaceNative(metafile, status)
        Return status
    End Function

    Function ConvertToEmfPlus(refGraphics As Graphics, ByVal filename$, _
        Optional conversionFailureFlag&, ByVal emfType As GpEmfType = EmfTypeEmfPlusOnly, Optional ByVal description$) As GpStatus
        Dim metafile As GpMetafile
        Dim status As Any = Native.ConvertToEmfPlusToFile(GetNative(refGraphics), Native, conversionFailureFlag, StrPtr(filename), emfType, StrPtr(description), metafile)
        ReplaceNative(metafile, status)
        Return status
    End Function
    
    Function ConvertToEmfPlus(refGraphics As Graphics, stream As GDIP_IStream, _
        Optional conversionFailureFlag&, ByVal emfType As GpEmfType = EmfTypeEmfPlusOnly, Optional ByVal description$) As GpStatus
        Dim metafile As GpMetafile
        Dim status As Any = Native.ConvertToEmfPlusToStream(GetNative(refGraphics), Native, conversionFailureFlag, stream, emfType, StrPtr(description), metafile)
        ReplaceNative(metafile, status)
        Return status
    End Function
    
    Protected Sub ReplaceNative(ByVal metafile As GpMetafile, ByVal status As GpStatus)
        If metafile.Native <> 0 Then
            If status = Ok Then
                im.Dispose
                im.Native = metafile.Native
            Else
                metafile.Dispose
            End If
        End If
    End Sub
'#endif // (GDIPVER >= 0x0110)
    
    Protected Property Get Native() As GpMetafile
        Native.Native = im.Native
    End Property
        
    Sub New(ByVal nativeMetafile As GpMetafile, ByVal status As GpStatus = ok)
        SetStatus(status)
        im.Native = nativeMetafile.Native
    End Sub
End Class

Module GdipMetafile
    Function Metafile(ByVal hWmf As LongPtr, wmfPlaceableFileHeader As WmfPlaceableFileHeader, ByVal deleteWmf As Boolean = False) As Metafile
        Return New Metafile(hWmf, wmfPlaceableFileHeader, deleteWmf)
    End Function

    Function Metafile(ByVal hEmf As LongPtr, ByVal deleteWmf As Boolean = False) As Metafile
        Return New Metafile(hEmf, deleteWmf)
    End Function

    Function Metafile(ByVal filename$) As Metafile
        Return New Metafile(filename)
    End Function

    Function Metafile(ByVal filename$, wmfPlaceableFileHeader As WmfPlaceableFileHeader) As Metafile
        Return New Metafile(filename, wmfPlaceableFileHeader)
    End Function

    Function Metafile(stream As GDIP_IStream) As Metafile
        Return New Metafile(stream)
    End Function

    Function Metafile(ByVal referenceHdc As LongPtr, _
            ByVal Type As GpEmfType = EmfTypeEmfPlusDual, Optional ByVal description$) As Metafile
        Return New Metafile(referenceHdc, Type, description)
    End Function

    Function Metafile(ByVal referenceHdc As LongPtr, frameRect As GpRectF, _
            ByVal frameUnit As GpMetafileFrameUnit = MetaFileFrameUnitGdi, _
            ByVal type As GpEmfType = EmfTypeEmfPlusDual, Optional ByVal description$) As Metafile
        Return New Metafile(referenceHdc, frameRect, frameUnit, type, description)
    End Function

    Function Metafile(ByVal referenceHdc As LongPtr, frameRect As GpRect, _
            ByVal frameUnit As GpMetafileFrameUnit = MetaFileFrameUnitGdi, _
            ByVal type As GpEmfType = EmfTypeEmfPlusDual, Optional ByVal description$) As Metafile
        Return New Metafile(referenceHdc, frameRect, frameUnit, type, description)
    End Function

    Function Metafile(ByVal filename$, ByVal referenceHdc As LongPtr, _
                ByVal type As GpEmfType = EmfTypeEmfPlusDual, Optional ByVal description$) As Metafile
        Return New Metafile(filename, referenceHdc, type, description)
    End Function

    Function Metafile(ByVal filename$, ByVal referenceHdc As LongPtr, _
                frameRect As GpRectF, ByVal frameUnit As GpMetafileFrameUnit = MetaFileFrameUnitGdi, _
                ByVal type As GpEmfType = EmfTypeEmfPlusDual, Optional ByVal description$) As Metafile
        Return New Metafile(filename, referenceHdc, frameRect, frameUnit, type, description)
    End Function

    Function Metafile(ByVal filename$, ByVal referenceHdc As LongPtr, _
                frameRect As GpRect, ByVal frameUnit As GpMetafileFrameUnit = MetaFileFrameUnitGdi, _
                ByVal type As GpEmfType = EmfTypeEmfPlusDual, Optional ByVal description$) As Metafile
        Return New Metafile(filename, referenceHdc, frameRect, frameUnit, type, description)
    End Function

    Function Metafile(stream As GDIP_IStream, ByVal referenceHdc As LongPtr, _
                ByVal type As GpEmfType = EmfTypeEmfPlusDual, Optional ByVal description$) As Metafile
        Return New Metafile(stream, referenceHdc, type, description)
    End Function

    Function Metafile(stream As GDIP_IStream, ByVal referenceHdc As LongPtr, _
                frameRect As GpRectF, ByVal frameUnit As GpMetafileFrameUnit = MetaFileFrameUnitGdi, _
                ByVal type As GpEmfType = EmfTypeEmfPlusDual, Optional ByVal description$) As Metafile
        Return New Metafile(stream, referenceHdc, frameRect, frameUnit, type, description)
    End Function

    Function Metafile(stream As GDIP_IStream, ByVal referenceHdc As LongPtr, _
                frameRect As GpRect, ByVal frameUnit As GpMetafileFrameUnit = MetaFileFrameUnitGdi, _
                ByVal type As GpEmfType = EmfTypeEmfPlusDual, Optional ByVal description$) As Metafile
        Return New Metafile(stream, referenceHdc, frameRect, frameUnit, type, description)
    End Function
    
    Type GpMetafile
        ' Inherits GpImage
        Native As LongPtr
        
        [UseGetLastError(False)]
        Public Declare PtrSafe Function GetHemfFromMetafile Lib "gdiplus" Alias "GdipGetHemfFromMetafile" (ByVal Me As GpMetafile, hemf As LongPtr) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function SetDownLevelRasterizationLimit Lib "gdiplus" Alias "GdipSetMetafileDownLevelRasterizationLimit" (ByVal Me As GpMetafile, ByVal metafileRasterizationLimitDpi As Long) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function GetDownLevelRasterizationLimit Lib "gdiplus" Alias "GdipGetMetafileDownLevelRasterizationLimit" (ByVal Me As GpMetafile, metafileRasterizationLimitDpi As Long) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function GetHeaderFromMetafile Lib "gdiplus" Alias "GdipGetMetafileHeaderFromMetafile" (ByVal Me As GpMetafile, header As MetafileHeader) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function CreateFromWmfInto Lib "gdiplus" Alias "GdipCreateMetafileFromWmf" (ByVal hWnf As LongPtr, ByVal deleteWmf As GDIP_BOOL, wmfPlaceableFileHeader As WmfPlaceableFileHeader, metafile As GpMetafile) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function CreateFromEmfInto Lib "gdiplus" Alias "GdipCreateMetafileFromEmf" (ByVal hEnf As LongPtr, ByVal deleteEmf As GDIP_BOOL, metafile As GpMetafile) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function CreateFromFileInto Lib "gdiplus" Alias "GdipCreateMetafileFromFile" (ByVal filename As LongPtr, metafile As GpMetafile) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function CreateFromWmfFileInto Lib "gdiplus" Alias "GdipCreateMetafileFromWmfFile" (ByVal filename As LongPtr, wmfPlaceableFileHeader As WmfPlaceableFileHeader, metafile As GpMetafile) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function CreateFromStreamInto Lib "gdiplus" Alias "GdipCreateMetafileFromStream" (ByVal stream As GDIP_IStream, metafile As GpMetafile) As GpStatus
    
        [UseGetLastError(False)]
        Public Declare PtrSafe Function RecordInto Lib "gdiplus" Alias "GdipRecordMetafile" (ByVal referenceHdc As LongPtr, ByVal type As GpEmfType, frameRect As GpRectF, ByVal frameUnit As GpMetafileFrameUnit, ByVal Description As LongPtr, metafile As GpMetafile) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function RecordIntoI Lib "gdiplus" Alias "GdipRecordMetafileI" (ByVal referenceHdc As LongPtr, ByVal type As GpEmfType, frameRect As GpRect, ByVal frameUnit As GpMetafileFrameUnit, ByVal Description As LongPtr, metafile As GpMetafile) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function RecordFileNameInto Lib "gdiplus" Alias "GdipRecordMetafileFileName" (ByVal FileName As LongPtr, ByVal referenceHdc As LongPtr, ByVal type As GpEmfType, frameRect As GpRectF, ByVal frameUnit As GpMetafileFrameUnit, ByVal Description As LongPtr, metafile As GpMetafile) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function RecordFileNameIntoI Lib "gdiplus" Alias "GdipRecordMetafileFileNameI" (ByVal FileName As LongPtr, ByVal referenceHdc As LongPtr, ByVal type As GpEmfType, frameRect As GpRect, ByVal frameUnit As GpMetafileFrameUnit, ByVal Description As LongPtr, metafile As GpMetafile) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function RecordStreamInto Lib "gdiplus" Alias "GdipRecordMetafileStream" (ByVal stream As GDIP_IStream, ByVal referenceHdc As LongPtr, ByVal type As GpEmfType, frameRect As GpRectF, ByVal frameUnit As GpMetafileFrameUnit, ByVal Description As LongPtr, metafile As GpMetafile) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function RecordStreamIntoI Lib "gdiplus" Alias "GdipRecordMetafileStreamI" (ByVal stream As GDIP_IStream, ByVal referenceHdc As LongPtr, ByVal type As GpEmfType, frameRect As GpRect, ByVal frameUnit As GpMetafileFrameUnit, ByVal Description As LongPtr, metafile As GpMetafile) As GpStatus
    
        [UseGetLastError(False)]
        Public Declare PtrSafe Function PlayRecord Lib "gdiplus" Alias "GdipPlayMetafileRecord" (ByVal Me As GpMetafile, ByVal recordType As GpEmfPlusRecordType, ByVal flags As Long, ByVal dataSize As Long, data As Byte) As GpStatus
    
        [UseGetLastError(False)]
        Public Declare PtrSafe Function ConvertToEmfPlusToStream Lib "gdiplus" Alias "GdipConvertToEmfPlusToStream" (ByVal refGraphics As GpGraphics, ByVal metafile As GpMetafile, conversionFailureFlag As Long, ByVal stream As GDIP_IStream, ByVal emfType As GpEmfType, ByVal description As LongPtr, out_metafile As GpMetafile) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function ConvertToEmfPlusToFile Lib "gdiplus" Alias "GdipConvertToEmfPlusToFile" (ByVal refGraphics As GpGraphics, ByVal metafile As GpMetafile, conversionFailureFlag As Long, ByVal filename As LongPtr, ByVal emfType As GpEmfType, ByVal description As LongPtr, out_metafile As GpMetafile) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function ConvertToEmfPlus Lib "gdiplus" Alias "GdipConvertToEmfPlus" (ByVal refGraphics As GpGraphics, ByVal metafile As GpMetafile, conversionFailureFlag As Long, ByVal emfType As GpEmfType, ByVal description As LongPtr, out_metafile As GpMetafile) As GpStatus
        
        ' Inherited from GpImage
        
        [UseGetLastError(False)]
        DeclareWide PtrSafe Function LoadFromFileInto Lib "gdiplus" Alias "GdipLoadImageFromFile" (ByVal FileName$, ByRef Image As GpMetafile) As GpStatus
        [UseGetLastError(False)]
        DeclareWide PtrSafe Function LoadFromFileICMInto Lib "gdiplus" Alias "GdipLoadImageFromFileICM" (ByVal FileName$, ByRef Image As GpMetafile) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function LoadFromStreamInto Lib "gdiplus" Alias "GdipLoadImageFromStream" (ByVal stream As GDIP_IStream, ByRef Image As GpMetafile) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function LoadFromStreamICMInto Lib "gdiplus" Alias "GdipLoadImageFromStreamICM" (ByVal stream As GDIP_IStream, ByRef Image As GpMetafile) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function Clone Lib "gdiplus" Alias "GdipCloneImage" (ByVal Me As GpMetafile, cloneImage As GpMetafile) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function Dispose Lib "gdiplus" Alias "GdipDisposeImage" (ByVal Me As GpMetafile) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function Delete Lib "gdiplus" Alias "GdipDisposeImage" (ByVal Me As GpMetafile) As GpStatus

        [Description("There should be a dummy encoderParams(0) that's not filled in. It's used internally.")]
        Function SaveToFile(ByVal FileName$, ByVal clsidEncoder As GDIP_UUID, encoderParams() As GpEncoderParameter) As GpStatus
            Return SaveToFileImpl(Me.Native, FileName, clsidEncoder, encoderParams)
        End Function
        
        [Description("There should be a dummy encoderParams(0) that's not filled in. It's used internally.")]
        Function SaveToStream(ByVal stream As GDIP_IStream, ByVal clsidEncoder As GDIP_UUID, encoderParams() As GpEncoderParameter) As GpStatus
            Return SaveToStreamImpl(Me.Native, stream, clsidEncoder, encoderParams)
        End Function
        
        [Description("There should be a dummy encoderParams(0) that's not filled in. It's used internally.")]
        Function SaveAdd(encoderParams() As GpEncoderParameter) As GpStatus
            Return SaveAddImpl(Me.Native, encoderParams)
        End Function

        [Description("There should be a dummy encoderParams(0) that's not filled in. It's used internally.")]
        Function SaveAddImage(ByVal newImage As GpMetafile, encoderParams() As GpEncoderParameter) As GpStatus
            Return SaveAddImageImpl(Me.Native, newImage.Native, encoderParams)
        End Function
        
        [UseGetLastError(False)]
        Declare PtrSafe Function GetType Lib "gdiplus" Alias "GdipGetImageType" (ByVal Me As GpMetafile, type As GpImageType) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function GetDimension Lib "gdiplus" Alias "GdipGetImageDimension" (ByVal Me As GpMetafile, ByRef Width!, ByRef Height!) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function GetBounds Lib "gdiplus" Alias "GdipGetImageBounds" (ByVal Me As GpMetafile, srcRect As GpRectF, srcUnit As GpUnit) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function GetHeight Lib "gdiplus" Alias "GdipGetImageHeight" (ByVal Me As GpMetafile, Height As Long) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function GetWidth Lib "gdiplus" Alias "GdipGetImageWidth" (ByVal Me As GpMetafile, Width As Long) As GpStatus
        
        Function GetSize(size As GpSize) As GpStatus
            Return GetSizeImpl(Me, size)
        End Function
    
        [UseGetLastError(False)]
        Declare PtrSafe Function GetHorizontalResolution Lib "gdiplus" Alias "GdipGetImageHorizontalResolution" (ByVal Me As GpMetafile, resolution!) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function GetVerticalResolution Lib "gdiplus" Alias "GdipGetImageVerticalResolution" (ByVal Me As GpMetafile, resolution!) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function GetFlags Lib "gdiplus" Alias "GdipGetImageFlags" (ByVal Me As GpMetafile, flags&) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function GetRawFormat Lib "gdiplus" Alias "GdipGetImageRawFormat" (ByVal Me As GpMetafile, Format As GDIP_UUID) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function GetPixelFormat Lib "gdiplus" Alias "GdipGetImagePixelFormat" (ByVal Me As GpMetafile, format As PixelFormat) As GpStatus
        
        [UseGetLastError(False)]
        Declare PtrSafe Function GetPaletteSize Lib "gdiplus" Alias "GdipGetImagePaletteSize" (ByVal Me As GpMetafile, size As Long) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function GetPalette Lib "gdiplus" Alias "GdipGetImagePalette" (ByVal Me As GpMetafile, palette As ColorPalette, ByVal size&) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function SetPalette Lib "gdiplus" Alias "GdipSetImagePalette" (ByVal Me As GpMetafile, palette As ColorPalette) As GpStatus
        
        Function GetThumbnail(ByVal thumbSize As GpSize, thumbImage As GpMetafile, ByVal callback As LongPtr, callbackData As LongPtr) As GpStatus
            Return Me.GetThumbnail2(thumbSize.Width, thumbSize.Height, thumbImage, callback, callbackData)
        End Function
        [UseGetLastError(False)]
        Declare PtrSafe Function GetThumbnail2 Lib "gdiplus" Alias "GdipGetImageThumbnail" (ByVal Me As GpMetafile, ByVal thumbWidth&, ByVal thumbHeight&, thumbImage As GpImage, ByVal callback As LongPtr, callbackData As Any) As GpStatus
        
        Function GetFrameDimensionsList(dims As GDIP_UUID()) As GpStatus
            Return GetFrameDimensionsListImpl(Me.Native, dims)
        End Function
        [UseGetLastError(False)]
        Declare PtrSafe Function GetFrameDimensionsCount Lib "gdiplus" Alias "GdipImageGetFrameDimensionsCount" (ByVal Me As GpMetafile, count&) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function GetFrameCount Lib "gdiplus" Alias "GdipImageGetFrameCount" (ByVal Me As GpMetafile, dimensionID As GDIP_UUID, count&) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function SelectActiveFrame Lib "gdiplus" Alias "GdipImageSelectActiveFrame" (ByVal Me As GpMetafile, ByRef dimensionID As GDIP_UUID, ByVal frameIndex&) As GpStatus

        [UseGetLastError(False)]
        Declare PtrSafe Function RotateFlip Lib "gdiplus" Alias "GdipImageRotateFlip" (ByVal Me As GpMetafile, ByVal rfType As RotateFlipType) As GpStatus
    
        Function GetPropertyIdList(propIDs() As PROPID) As GpStatus
            Return GetPropertyIdListImpl(Me.Native, propIDs)
        End Function
        [UseGetLastError(False)]
        Declare PtrSafe Function GetPropertyCount Lib "gdiplus" Alias "GdipGetPropertyCount" (ByVal Me As GpMetafile, numOfProperty&) As GpStatus
        
        ' The simplest way to return a ref-counted item is to wrap it in an array.
        ' That forgoes the need for a class that represents the item.
        Function GetPropertyItem(ByVal propID As PROPID, item() As GpPropertyItem) As GpStatus
            Return GetPropertyItemImpl(Me.Native, propID, item)
        End Function
        [UseGetLastError(False)]
        Declare PtrSafe Function SetPropertyItem Lib "gdiplus" Alias "GdipSetPropertyItem" (ByVal Me As GpMetafile, item As GpPropertyItem) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function GetPropertyItemSize Lib "gdiplus" Alias "GdipGetPropertyItemSize" (ByVal Me As GpMetafile, ByVal propId&, ByRef Size&) As GpStatus
        
        Function GetAllPropertyItems(items() As GpPropertyItem) As GpStatus
            Return GetAllPropertyItemsImpl(Me.Native, items)
        End Function
        [UseGetLastError(False)]
        Declare PtrSafe Function GetPropertySize Lib "gdiplus" Alias "GdipGetPropertySize" (ByVal Me As GpMetafile, totalBufferSize&, numProperties&) As GpStatus
        
        [UseGetLastError(False)]
        Declare PtrSafe Function RemovePropertyItem Lib "gdiplus" Alias "GdipRemovePropertyItem" (ByVal Me As GpMetafile, ByVal propId As PROPID) As GpStatus

        [Description("Returns an array with parameters. The array starts at index 1 even though LBound(params)=0")]
        Function GetEncoderParameterList(ByVal clsidencoder As GDIP_UUID, params() As GpEncoderParameter) As GpStatus
            Return GetEncoderParameterListImpl(Me.Native, clsidencoder, params)
        End Function
        [UseGetLastError(False)]
        Declare PtrSafe Function GetEncoderParameterListSize Lib "gdiplus" Alias "GdipGetEncoderParameterListSize" (ByVal Me As GpMetafile, clsidEncoder As GDIP_UUID, size&) As GpStatus
    
        Function FindFirstItem(item() As GpImageItemData) As GpStatus
            Return FindFirstItemImpl(Me.Native, item)
        End Function
        
        Function FindNextItem(item() As GpImageItemData) As GpStatus
            Return FindNextItemImpl(Me.Native, item)
        End Function
        
        Function GetItemData(item() As GpImageItemData) As GpStatus
            Return GetItemDataImpl(Me.Native, item)
        End Function
                
        [UseGetLastError(False)]
        Declare PtrSafe Function GetGraphicsContext Lib "gdiplus" Alias "GdipGetImageGraphicsContext" (ByVal Me As GpMetafile, ByVal Graphics As GpGraphics) As GpStatus
        [UseGetLastError(False)]
        [Description("⛔This API is not implemented in GdiPlus 1.0 nor 1.1." & vbCrLf _
            & "⛔NOTE: This API is a placeholder until tB supports no-vtable interfaces")]
        Declare PtrSafe Function SetAbort Lib "gdiplus" Alias "GdipImageSetAbort" (ByVal Me As GpMetafile, pIAbort As Any /*GdiplusAbort*/) As GpStatus
    End Type
    
    Function GpMetafile() As GpMetafile
        GpMetafile.Native = 0
    End Function
    
    Function GetMetafileHeaderFromWmf(ByVal hWmf As LongPtr, ByVal wmfPlaceableFileHeader As WmfPlaceableFileHeader, header As MetafileHeader) As GpStatus
        Return GdipGetMetafileHeaderFromWmf(hWmf, wmfPlaceableFileHeader, header)
    End Function

    Function GetMetafileHeaderFromEmf(ByVal hEmf As LongPtr, header As MetafileHeader) As GpStatus
        Return GdipGetMetafileHeaderFromEmf(hEmf, header)
    End Function
    
    Function GetMetafileHeader(ByVal filename$, header As MetafileHeader) As GpStatus
        Return GdipGetMetafileHeaderFromFile(StrPtr(filename), header)
    End Function

    Function GetMetafileHeader(stream As GDIP_IStream, header As MetafileHeader) As GpStatus
        Return GdipGetMetafileHeaderFromStream(stream, header)
    End Function

    Function EmfToWmfBits(ByVal hEmf As LongPtr, ByVal cbData16 As UINT, pData16 As Byte, _
            ByVal iMapMode As GDIP_MappingModes = GDIP_MM_ANISOTROPIC, _
            ByVal eFlags As GpEmfToWmfBitsFlags = EmfToWmfBitsFlagsDefault) As UINT
        Return GdipEmfToWmfBits(hEmf, cbData16, pData16, iMapMode, eFlags)
    End Function
    
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipGetMetafileHeaderFromWmf Lib "gdiplus" (ByVal hWmf As LongPtr, wmfPlaceableFileHeader As WmfPlaceableFileHeader, header As MetafileHeader) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipGetMetafileHeaderFromEmf Lib "gdiplus" (ByVal hEmf As LongPtr, header As MetafileHeader) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipGetMetafileHeaderFromFile Lib "gdiplus" (ByVal filename As LongPtr, header As MetafileHeader) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipGetMetafileHeaderFromStream Lib "gdiplus" (ByVal stream As GDIP_IStream, header As MetafileHeader) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipEmfToWmfBits Lib "gdiplus" (ByVal hemf As LongPtr, ByVal cbData16 As Long, pData16 As Byte, ByVal iMapMode As Long, ByVal eFlags As Long) As Long
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipCreateStreamOnFile Lib "gdiplus" (ByVal filename As LongPtr, ByVal access As Long, stream As GDIP_IStream) As GpStatus
End Module
