' A part of the GDI+ Package for TwinBasic  https://github.com/kubao/GdiPlus
' Licensed under the MIT license. (c) 2026 Sunderland Ober Consulting.
' "Windows" is a trademark of the Microsoft Corporation.
' Modeled after code and descriptions (c) Microsoft, taken from SDK headers and official documentation.
' Certain flat API function and type declarations taken from WinDevLib by fafalone, with thanks.

[COMCreatable(False)]
Class Bitmap
    Inherits Image
    
    Sub New(ByVal filename As String, ByVal useEmbeddedColorManagement As Boolean = False)
        Dim nativeBitmap As GpBitmap
        If useEmbeddedColorManagement Then
            LastStatus = GdipCreateBitmapFromFileICM(filename, nativeBitmap)
        Else
            LastStatus = GdipCreateBitmapFromFile(filename, nativeBitmap)
        End If
        SetNativeBitmap(nativeBitmap)
    End Sub
    
    Sub New(stream As IStream, ByVal useEmbeddedColorManagement As Boolean = False)
        Dim nativeBitmap As GpBitmap
        If useEmbeddedColorManagement Then
            LastStatus = GdipCreateBitmapFromStreamICM(stream, nativeBitmap)
        Else
            LastStatus = GdipCreateBitmapFromStream(stream, nativeBitmap)
        End If
        SetNativeBitmap(nativeBitmap)
    End Sub
    
    Sub New(ByVal size As GpSizeF, ByVal stride&, ByVal format As PixelFormat, scan0 As Byte)
        NewImpl2(size.Width, size.Height, stride, format, scan0)
    End Sub
    
    Sub New(ByVal width!, ByVal height!, ByVal stride&, ByVal format As PixelFormat, scan0 As Byte)
        NewImpl2(width, height, stride, format, scan0)
    End Sub
    
    Sub New(ByVal size As GpSizeF, ByVal format As PixelFormat = PixelFormat32bppARGB)
        NewImpl2(size.Width, size.Height, 0, format, ByVal 0)
    End Sub
    
    Sub New(ByVal width!, ByVal height!, ByVal format As PixelFormat = PixelFormat32bppARGB)
        NewImpl2(width, height, 0, format, ByVal 0)
    End Sub
    
    Sub New(ByVal size As GpSizeF, target As Graphics)
        NewImpl1(size.Width, size.Height, target)
    End Sub
    
    Sub New(ByVal width!, ByVal height!, target As Graphics)
        NewImpl1(width, height, target)
    End Sub
    
    Private Sub NewImpl1(ByVal width!, ByVal height!, target As Graphics)
        Dim nativeBitmap As GpBitmap
        LastStatus = GdipCreateBitmapFromGraphics(width, height, GetNative(target), nativeBitmap)
        SetNativeBitmap(nativeBitmap)
    End Sub
    
    Private Sub NewImpl2(ByVal width!, ByVal height!, ByVal stride&, ByVal format As PixelFormat, scan0 As Byte)
        Dim nativeBitmap As GpBitmap
        LastStatus = GdipCreateBitmapFromScan0(width, height, stride, format, scan0, nativeBitmap)
        SetNativeBitmap(nativeBitmap)
    End Sub
    
    Function Clone(rect As GpRect, ByVal format As PixelFormat) As Bitmap
        Return Clone(rect.X, rect.Y, rect.Width, rect.Height, format)
    End Function
    
    Function CloneI(ByVal x&, ByVal y&, ByVal width&, ByVal height&, ByVal format As PixelFormat) As Bitmap
        Dim dstNative As GpBitmap
        SetStatus(GdipCloneBitmapAreaI(x, y, width, height, format, Native, dstNative))
        If LastResult = Ok Then Set CloneI = New Bitmap(dstNative)
    End Function
    
    Function Clone(rect As GpRectF, ByVal format As PixelFormat) As Bitmap
        Return Clone(rect.X, rect.Y, rect.Width, rect.Height, format)
    End Function
    
    Function Clone(ByVal x!, ByVal y!, ByVal width!, ByVal height!, ByVal format As PixelFormat) As Bitmap
        Dim dstNative As GpBitmap
        SetStatus(GdipCloneBitmapArea(x, y, width, height, format, Native, dstNative))
        If LastResult = Ok Then Set Clone = New Bitmap(dstNative)
    End Function
    
    Function LockBits(rect As GpRect, ByVal flags As UINT, ByVal format As PixelFormat, lockedBitmapData As BitmapData) As GpStatus
        Return SetStatus(GdipBitmapLockBits(Native, rect, flags, format, lockedBitmapData))
    End Function
    
    Function UnlockBits(lockedBitmapData As BitmapData) As GpStatus
        Return SetStatus(GdipBitmapUnlockBits(Native, lockedBitmapData))
    End Function
    
    [DefaultMember]
    Property Get Pixel(ByVal x&, ByVal y&) As Color
        SetStatus(GdipBitmapGetPixel(Native, x, y, Pixel))
    End Property
    
    [DefaultMember]
    Property Let Pixel(ByVal x&, ByVal y&, ByVal color As Color)
        SetStatus(GdipBitmapSetPixel(Native, x, y, color))
    End Property
    
    '#if (GDIPVER >= 0x0110)
    Function ConvertFormat(ByVal format As PixelFormat, ByVal ditherType As DitherType, ByVal paletteType As PaletteType, _
            palette As ColorPalette, ByVal alphaThresholdPercent!) As GpStatus
        Return SetStatus(GdipBitmapConvertFormat(Native, _
                format, ditherType, paletteType, palette, alphaThresholdPercent))
    End Function
    
    Function ApplyEffect(effect As Effect, roi As GpRect) As GpStatus
        Dim auxData As LongPtr
        Dim auxDataSize&
        If effect.AuxData <> 0 Then effect.SetAuxData(0, 0)
        SetStatus(GdipBitmapApplyEffect(Native, GetNative(effect), _
                roi.AsRECT(), effect.UseAuxData, auxData, auxDataSize))
        effect.SetAuxData(auxData, auxDataSize)
        Return LastStatus
    End Function
    
    Function GetHistogram(ByVal format As HistogramFormat) As Histogram
        GetHistogram(format, GetHistogram.channel0, GetHistogram.channel1, GetHistogram.channel2, GetHistogram.channel3)
    End Function
    
    Function GetHistogram(ByVal format As HistogramFormat, channel0() As UINT, channel1() As UINT, channel2() As UINT, channel3() As UINT) As GpStatus
        Dim size As UINT = HistogramSize(format)
        ReDim channel0(1, size)
        ReDim channel1(1, size)
        ReDim channel2(1, size)
        ReDim channel3(1, size)
        Return GetHistogram(format, size, channel0(1), channel1(1), channel2(1), channel3(1))
    End Function
    
    Function GetHistogram(ByVal format As HistogramFormat, ByVal numberOfEntries As UINT, _
            channel0 As UINT, channel1 As UINT, channel2 As UINT, channel3 As UINT) As GpStatus
        Return SetStatus(GdipBitmapGetHistogram(Native, format, numberOfEntries, channel0, channel1, channel2, channel3))
    End Function
    
    Property Get HistogramSize(ByVal format As HistogramFormat) As UINT
        SetStatus(GdipBitmapGetHistogramSize(format, HistogramSize))
    End Property
    '#endif //(GDIPVER >= 0x0110)
    
    Function SetResolution(ByVal xdpi&, ByVal ydpi&) As GpStatus
        Return SetStatus(GdipBitmapSetResolution(Native, xdpi, ydpi))
    End Function
    
    Sub New(surface As IDirectDrawSurface7)
        Dim nativeBitmap As GpBitmap
        LastStatus = GdipCreateBitmapFromDirectDrawSurface(surface, nativeBitmap)
        SetNativeBitmap(nativeBitmap)
    End Sub
    
    Sub New(gdiBitmapInfo As BITMAPINFO, gdiBitmapData As Byte)
        Dim nativeBitmap As GpBitmap
        LastStatus = GdipCreateBitmapFromGdiDib(gdiBitmapInfo, gdiBitmapData, nativeBitmap)
        SetNativeBitmap(nativeBitmap)
    End Sub
    
    Sub New(ByVal hbm As LongPtr, ByVal hpal As LongPtr)
        Dim nativeBitmap As GpBitmap
        LastStatus = GdipCreateBitmapFromHBITMAP(hbm, hpal, nativeBitmap)
        SetNativeBitmap(nativeBitmap)
    End Sub
    
    Sub New(ByVal hicon As LongPtr)
        Dim nativeBitmap As GpBitmap
        LastStatus = GdipCreateBitmapFromHICON(hicon, nativeBitmap)
        SetNativeBitmap(nativeBitmap)
    End Sub
    
    Sub New(ByVal hInstance As LongPtr, ByVal bitmapName As String)
        Dim nativeBitmap As GpBitmap
        LastStatus = GdipCreateBitmapFromResource(hInstance, StrPtr(bitmapName), nativeBitmap)
        SetNativeBitmap(nativeBitmap)
    End Sub
    
    Function GetHBitmap(ByVal colorBackground As Color, hbmReturn As LongPtr) As GpStatus
        Return SetStatus(GdipCreateHBITMAPFromBitmap(Native, hbmReturn, colorBackground))
    End Function
    
    Function GetHIcon(hIconReturn As LongPtr) As GpStatus
        Return SetStatus(GdipCreateHICONFromBitmap(Native, hIconReturn))
    End Function
    
    Protected Property Get Native() As GpBitmap
        Native.Native = im.Native
    End Property

    ' Used by freestanding ApplyEffect
    Public Sub New(ByVal nativeBitmap As GpBitmap)
        LastStatus = Ok
        SetNativeBitmap(nativeBitmap)
    End Sub

    Protected Sub SetNativeBitmap(ByVal nativeBitmap As GpBitmap)
        im.Native = nativeBitmap.Native
    End Sub
End Class

Module GdipBitmap
    Function Bitmap(ByVal filename As String, ByVal useEmbeddedColorManagement As Boolean = False) As Bitmap
        Return New Bitmap(filename, useEmbeddedColorManagement)
    End Function
    
    Function Bitmap(stream As IStream, ByVal useEmbeddedColorManagement As Boolean = False) As Bitmap
        Return New Bitmap(stream, useEmbeddedColorManagement)
    End Function
    
    Function Bitmap(ByVal size As GpSizeF, ByVal stride&, ByVal format As PixelFormat, scan0 As Byte) As Bitmap
        Return New Bitmap(size, stride, format, scan0)
    End Function
    
    Function Bitmap(ByVal width!, ByVal height!, ByVal stride&, ByVal format As PixelFormat, scan0 As Byte) As Bitmap
        Return New Bitmap(width, height, stride, format, scan0)
    End Function
    
    Function Bitmap(ByVal size As GpSizeF, ByVal format As PixelFormat = PixelFormat32bppARGB) As Bitmap
        Return New Bitmap(size, format)
    End Function
    
    Function Bitmap(ByVal width!, ByVal height!, ByVal format As PixelFormat = PixelFormat32bppARGB) As Bitmap
        Return New Bitmap(width, height, format)
    End Function
    
    Function Bitmap(ByVal size As GpSizeF, target As Graphics) As Bitmap
        Return New Bitmap(size, target)
    End Function
    
    Function Bitmap(ByVal width!, ByVal height!, target As Graphics) As Bitmap
        Return New Bitmap(width, height, target)
    End Function
    
    Function Bitmap(surface As IDirectDrawSurface7) As Bitmap
        Return New Bitmap(surface)
    End Function
    
    Function Bitmap(gdiBitmapInfo As BITMAPINFO, gdiBitmapData As Byte) As Bitmap
        Return New Bitmap(gdiBitmapInfo, gdiBitmapData)
    End Function
    
    Function Bitmap(hbm As LongPtr, hpal As LongPtr) As Bitmap
        Return New Bitmap(hbm, hpal)
    End Function
    
    Function Bitmap(hicon As LongPtr) As Bitmap
        Return New Bitmap(hicon)
    End Function
    
    Function Bitmap(hInstance As LongPtr, ByVal bitmapName As String) As Bitmap
        Return New Bitmap(hInstance, bitmapName)
    End Function
    
    Type Histogram
        channel0() As UINT
        channel1() As UINT
        channel2() As UINT
        channel3() As UINT
    End Type
    
    Type GpBitmap
        ' Inherits GpImage
        Native As LongPtr
    End Type
    
    Function GpBitmap() As GpBitmap
        GpBitmap.Native = 0
    End Function
    
    '#if (GDIPVER >= 0x0110)
    ' The palette must be allocated and count must be set to the number of
    ' entries in the palette. If there are not enough, the API will fail.
    Function InitializePalette( _
            palette As ColorPalette, /* Palette to initialize. */ _
            ByVal paletteTypee As PaletteType, _
            ByVal optimalColors!, _
            ByVal useTransparentColor As Boolean, /* add a transparent color to the palette? */ _
            Optional bitmap As Bitmap /* optional bitmap for median cut */ _
            ) As GpStatus
        Return GdipInitializePalette(palette, PaletteType, optimalColors, useTransparentColor, GetNative(bitmap))
    End Function
    
    Function ApplyEffect( _
            inputs() As Bitmap, effect As Effect, _
            output As Bitmap _
            ) As GpStatus
        Return ApplyEffect(inputs, effect, ByVal CLngPtr(0), ByVal CLngPtr(0), output)
    End Function
    
    Function ApplyEffect( _
            inputs() As Bitmap, effect As Effect, _
            ROI As RECT, /* optional */ _
            outputRect As RECT, /* optional */ _
            output As Bitmap _
            ) As GpStatus
        Dim outputNative As GpBitmap
        Dim nativeInputs() As GpBitmap
        ReDim nativeInputs(1 To ArrayLen(inputs))
        Dim j As Integer = 1
        For i As Integer = LBound(inputs) To UBound(inputs)
            nativeInputs(j) = GetNative(inputs(i))
            j += 1
        Next
        
        If effect.AuxData <> 0 Then effect.SetAuxData(0, 0)
        
        Dim auxData As LongPtr
        Dim auxDataSize&
        Dim status As GpStatus = GdipBitmapCreateApplyEffect( _
                nativeInputs(1), ArrayLen(nativeInputs), GetNative(effect), _
                ROI, _
                outputRect, _
                outputNative, _
                effect.UseAuxData, _
                auxData, auxDataSize _
                )
        
        If status = Ok And outputNative.Native <> 0 Then
            effect.SetAuxData(auxData, auxDataSize)
            Set output = New Bitmap(outputNative)
        Else
            If auxData <> 0 And auxDataSize > 0 Then GdipFree(auxData)
            Set output = Nothing
        End If
        
        Return status
    End Function
    
    [UseGetLastError(False)]
    Public DeclareWide PtrSafe Function GdipCreateBitmapFromFile Lib "gdiplus" (ByVal FileName As String, ByRef bitmap As GpBitmap) As GpStatus
    [UseGetLastError(False)]
    Public DeclareWide PtrSafe Function GdipCreateBitmapFromFileICM Lib "gdiplus" (ByVal FileName As String, ByRef bitmap As GpBitmap) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipCreateBitmapFromStream Lib "gdiplus" (ByVal stream As IStream, ByRef bitmap As GpBitmap) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipCreateBitmapFromStreamICM Lib "gdiplus" (ByVal stream As IStream, ByRef bitmap As GpBitmap) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipCreateBitmapFromResource Lib "gdiplus" (ByVal hInstance As LongPtr, ByVal lpBitmapName As LongPtr, ByRef bitmap As GpBitmap) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipCreateBitmapFromScan0 Lib "gdiplus" (ByVal Width As Long, ByVal Height As Long, ByVal stride As Long, ByVal format As PixelFormat, scan0 As Any, bitmap As GpBitmap) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipCreateBitmapFromGraphics Lib "gdiplus" (ByVal Width As Long, ByVal Height As Long, ByVal Graphics As GpGraphics, bitmap As GpBitmap) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipCreateBitmapFromDirectDrawSurface Lib "gdiplus" (surface As Any, bitmap As GpBitmap) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipCreateBitmapFromGdiDib Lib "gdiplus" (gdiBitmapInfo As BITMAPINFO, ByVal gdiBitmapData As LongPtr, bitmap As GpBitmap) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipCreateBitmapFromHBITMAP Lib "gdiplus" (ByVal hbm As LongPtr, ByVal hpal As LongPtr, bitmap As GpBitmap) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipCreateBitmapFromHICON Lib "gdiplus" (ByVal hicon As LongPtr, bitmap As GpBitmap) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipCreateHBITMAPFromBitmap Lib "gdiplus" (ByVal bitmap As GpBitmap, ByRef hbmReturn As LongPtr, ByVal Background As Color) As GpStatus
    
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipInitializePalette Lib "gdiplus" (palette As ColorPalette, ByVal paletteType As PaletteType, ByVal optimalColors As Long, ByVal useTransparentColor As BOOL, ByVal bitmap As GpBitmap) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipBitmapApplyEffect Lib "gdiplus" (ByVal Me As GpBitmap, ByVal effect As GpEffect, roi As RECT, ByVal useAuxData As BOOL, auxData As Any, auxDataSize As Long) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipBitmapCreateApplyEffect Lib "gdiplus" (inputBitmaps As Any, ByVal numInputs As Long, ByVal effect As GpEffect, roi As RECT, outputRect As RECT, outputBitmap As GpBitmap, ByVal useAuxData As BOOL, auxData As Any, auxDataSize As Long) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipCloneBitmapArea Lib "gdiplus" (ByVal x As Single, ByVal y As Single, ByVal width As Single, ByVal height As Single, ByVal format As PixelFormat, ByVal srcBitmap As GpBitmap, dstBitmap As GpBitmap) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipCloneBitmapAreaI Lib "gdiplus" (ByVal x As Long, ByVal y As Long, ByVal width As Long, ByVal height As Long, ByVal format As PixelFormat, ByVal srcBitmap As GpBitmap, dstBitmap As GpBitmap) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipBitmapLockBits Lib "gdiplus" (ByVal Me As GpBitmap, Rect As GpRect, ByVal flags As Long, ByVal format As PixelFormat, lockedBitmapData As BitmapData) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipBitmapUnlockBits Lib "gdiplus" (ByVal Me As GpBitmap, lockedBitmapData As BitmapData) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipBitmapGetPixel Lib "gdiplus" (ByVal Me As GpBitmap, ByVal X As Long, ByVal Y As Long, Color As Color) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipBitmapSetPixel Lib "gdiplus" (ByVal Me As GpBitmap, ByVal X As Long, ByVal Y As Long, ByVal Color As Color) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipBitmapConvertFormat Lib "gdiplus" (ByVal inputBitmap As GpBitmap, ByVal format As PixelFormat, ByVal dithertype As DitherType, ByVal palettetype As PaletteType, palette As ColorPalette, ByVal alphaThresholdPercent As Single) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipBitmapGetHistogram Lib "gdiplus" (ByVal Me As GpBitmap, ByVal format As HistogramFormat, ByVal NumberOfEntries As Long, channel0 As Long, channel1 As Long, channel2 As Long, channel3 As Long) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipBitmapGetHistogramSize Lib "gdiplus" (ByVal format As HistogramFormat, NumberOfEntries As Long) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipBitmapSetResolution Lib "gdiplus" (ByVal Me As GpBitmap, ByVal xdpi As Single, ByVal ydpi As Single) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipCreateHICONFromBitmap Lib "gdiplus" (ByVal Me As GpBitmap, hbmReturn As LongPtr) As GpStatus
End Module
