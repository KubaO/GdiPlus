' A part of the GDI+ Package for TwinBasic  https://github.com/kubao/GdiPlus
' Licensed under the MIT license. (c) 2026 Sunderland Ober Consulting.
' "Windows" is a trademark of the Microsoft Corporation.
' Modeled after code and descriptions (c) Microsoft, taken from SDK headers and official documentation.
' Certain flat API function and type declarations taken from WinDevLib by fafalone, with thanks.

[COMCreatable(False)]
Class Bitmap
    Inherits Image
    
    Sub New(ByVal filename As String, ByVal useEmbeddedColorManagement As Boolean = False)
        If useEmbeddedColorManagement Then
            SetStatus(Native.CreateFromFileICMInto(filename, im))
        Else
            SetStatus(Native.CreateFromFileInto(filename, im))
        End If
    End Sub
    
    Sub New(stream As GDIP_IStream, ByVal useEmbeddedColorManagement As Boolean = False)
        If useEmbeddedColorManagement Then
            SetStatus(Native.CreateFromStreamICMInto(stream, im))
        Else
            SetStatus(Native.CreateFromStreamInto(stream, im))
        End If
    End Sub

    Sub New(ByVal size As GpSizeF, ByVal stride&, ByVal format As PixelFormat, scan0 As Byte)
        NewImpl2(size.Width, size.Height, stride, format, scan0)
    End Sub
    
    Sub New(ByVal width!, ByVal height!, ByVal stride&, ByVal format As PixelFormat, scan0 As Byte)
        NewImpl2(width, height, stride, format, scan0)
    End Sub
    
    Sub New(ByVal size As GpSizeF, ByVal format As PixelFormat = PixelFormat32bppARGB)
        NewImpl2(size.Width, size.Height, 0, format, ByVal 0)
    End Sub
    
    Sub New(ByVal width!, ByVal height!, ByVal format As PixelFormat = PixelFormat32bppARGB)
        NewImpl2(width, height, 0, format, ByVal 0)
    End Sub
    
    Sub New(ByVal size As GpSizeF, target As Graphics)
        NewImpl1(size.Width, size.Height, target)
    End Sub
    
    Sub New(ByVal width!, ByVal height!, target As Graphics)
        NewImpl1(width, height, target)
    End Sub
    
    Private Sub NewImpl1(ByVal width!, ByVal height!, target As Graphics)
        SetStatus(Native.CreateFromGraphicsInto(width, height, GetNative(target), im))
    End Sub
    
    Private Sub NewImpl2(ByVal width!, ByVal height!, ByVal stride&, ByVal format As PixelFormat, scan0 As Byte)
        SetStatus(Native.CreateFromScan0Into(width, height, stride, format, scan0, im))
    End Sub
    
    Function Clone(rect As GpRect, ByVal format As PixelFormat) As Bitmap
        Return Clone(rect.X, rect.Y, rect.Width, rect.Height, format)
    End Function
    
    Function CloneI(ByVal x&, ByVal y&, ByVal width&, ByVal height&, ByVal format As PixelFormat) As Bitmap
        Dim dstNative As GpBitmap
        SetStatus(GdipCloneBitmapAreaI(x, y, width, height, format, Native, dstNative))
        If LastResult = Ok Then Set CloneI = New Bitmap(dstNative)
    End Function
    
    Function Clone(rect As GpRectF, ByVal format As PixelFormat) As Bitmap
        Return Clone(rect.X, rect.Y, rect.Width, rect.Height, format)
    End Function
    
    Function Clone(ByVal x!, ByVal y!, ByVal width!, ByVal height!, ByVal format As PixelFormat) As Bitmap
        Dim dstNative As GpBitmap
        SetStatus(GdipCloneBitmapArea(x, y, width, height, format, Native, dstNative))
        If LastResult = Ok Then Set Clone = New Bitmap(dstNative)
    End Function
    
    Function LockBits(rect As GpRect, ByVal flags As UINT, ByVal format As PixelFormat, lockedBitmapData As BitmapData) As GpStatus
        Return SetStatus(Native.LockBits(rect, flags, format, lockedBitmapData))
    End Function
    
    Function UnlockBits(lockedBitmapData As BitmapData) As GpStatus
        Return SetStatus(Native.UnlockBits(lockedBitmapData))
    End Function
    
    [DefaultMember]
    Property Get Pixel(ByVal x&, ByVal y&) As Color
        SetStatus(GetPixel(im, x, y, Pixel))
    End Property
    
    [DefaultMember]
    Property Let Pixel(ByVal x&, ByVal y&, ByVal color As Color)
        SetStatus(SetPixel(im, x, y, color))
    End Property
    
    '#if (GDIPVER >= 0x0110)
    Function ConvertFormat(ByVal format As PixelFormat, ByVal ditherType As DitherType, ByVal paletteType As PaletteType, _
            palette As ColorPalette, ByVal alphaThresholdPercent!) As GpStatus
        Return SetStatus(Native.ConvertFormat( _
                format, ditherType, paletteType, palette, alphaThresholdPercent))
    End Function
    
    Function ApplyEffect(effect As Effect, roi As GpRect) As GpStatus
        Dim auxData As LongPtr
        Dim auxDataSize&
        If effect.AuxData <> 0 Then effect.SetAuxData(0, 0)
        SetStatus(Native.ApplyEffect(GetNative(effect), _
                roi.AsRECT(), effect.UseAuxData, auxData, auxDataSize))
        effect.SetAuxData(auxData, auxDataSize)
        Return LastResult
    End Function
    
    Function GetHistogram(ByVal format As HistogramFormat) As Histogram
        GetHistogram(format, GetHistogram.channel0, GetHistogram.channel1, GetHistogram.channel2, GetHistogram.channel3)
    End Function
    
    Function GetHistogram(ByVal format As HistogramFormat, channel0() As UINT, channel1() As UINT, channel2() As UINT, channel3() As UINT) As GpStatus
        Dim size As UINT = HistogramSize(format)
        ReDim channel0(1, size)
        ReDim channel1(1, size)
        ReDim channel2(1, size)
        ReDim channel3(1, size)
        Return GetHistogram(format, size, channel0(1), channel1(1), channel2(1), channel3(1))
    End Function
    
    Function GetHistogram(ByVal format As HistogramFormat, ByVal numberOfEntries As UINT, _
            channel0 As UINT, channel1 As UINT, channel2 As UINT, channel3 As UINT) As GpStatus
        Return SetStatus(Native.GetHistogram(format, numberOfEntries, channel0, channel1, channel2, channel3))
    End Function
    
    Property Get HistogramSize(ByVal format As HistogramFormat) As UINT
        SetStatus(Native.GetHistogramSize(format, HistogramSize))
    End Property
    '#endif //(GDIPVER >= 0x0110)
    
    Function SetResolution(ByVal xdpi&, ByVal ydpi&) As GpStatus
        Return SetStatus(Native.SetResolution(xdpi, ydpi))
    End Function
    
    Sub New(surface As GDIP_IDirectDrawSurface7)
        SetStatus(Native.CreateFromDirectDrawSurfaceInto(surface, im))
    End Sub
    
    Sub New(gdiBitmapInfo As GDIP_BITMAPINFO, gdiBitmapData As Byte)
        SetStatus(Native.CreateFromGdiDibInto(gdiBitmapInfo, gdiBitmapData, im))
    End Sub

    Sub New(ByVal hbm As LongPtr, ByVal hpal As LongPtr)
        SetStatus(Native.CreateFromHBITMAPInto(hbm, hpal, im))
    End Sub
    
    Sub New(ByVal hicon As LongPtr)
        SetStatus(Native.CreateFromHICONInto(hicon, im))
    End Sub
    
    Sub New(ByVal hInstance As LongPtr, ByVal bitmapName As String)
        SetStatus(Native.CreateFromResourceInto(hInstance, StrPtr(bitmapName), im))
    End Sub
    
    Function GetHBitmap(ByVal colorBackground As Color, hbmReturn As LongPtr) As GpStatus
        Return SetStatus(Native.CreateHBITMAP(hbmReturn, colorBackground))
    End Function
    
    Function GetHIcon(hIconReturn As LongPtr) As GpStatus
        Return SetStatus(Native.CreateHICON(hIconReturn))
    End Function
    
    Protected Property Get Native() As GpBitmap
        Native.Native = im.Native
    End Property

    ' Used by freestanding ApplyEffect
    Public Sub New(ByVal nativeBitmap As GpBitmap)
        im.Native = nativeBitmap.Native
    End Sub

    Protected Sub SetNativeBitmap(ByVal nativeBitmap As GpBitmap)
        im.Native = nativeBitmap.Native
    End Sub
End Class

Module GdipBitmap
    Function Bitmap(ByVal filename As String, ByVal useEmbeddedColorManagement As Boolean = False) As Bitmap
        Return New Bitmap(filename, useEmbeddedColorManagement)
    End Function
    
    Function Bitmap(stream As GDIP_IStream, ByVal useEmbeddedColorManagement As Boolean = False) As Bitmap
        Return New Bitmap(stream, useEmbeddedColorManagement)
    End Function

    Function Bitmap(ByVal size As GpSizeF, ByVal stride&, ByVal format As PixelFormat, scan0 As Byte) As Bitmap
        Return New Bitmap(size, stride, format, scan0)
    End Function
    
    Function Bitmap(ByVal width!, ByVal height!, ByVal stride&, ByVal format As PixelFormat, scan0 As Byte) As Bitmap
        Return New Bitmap(width, height, stride, format, scan0)
    End Function
    
    Function Bitmap(ByVal size As GpSizeF, ByVal format As PixelFormat = PixelFormat32bppARGB) As Bitmap
        Return New Bitmap(size, format)
    End Function
    
    Function Bitmap(ByVal width!, ByVal height!, ByVal format As PixelFormat = PixelFormat32bppARGB) As Bitmap
        Return New Bitmap(width, height, format)
    End Function
    
    Function Bitmap(ByVal size As GpSizeF, target As Graphics) As Bitmap
        Return New Bitmap(size, target)
    End Function
    
    Function Bitmap(ByVal width!, ByVal height!, target As Graphics) As Bitmap
        Return New Bitmap(width, height, target)
    End Function
    
    Function Bitmap(surface As GDIP_IDirectDrawSurface7) As Bitmap
        Return New Bitmap(surface)
    End Function
    
    Function Bitmap(gdiBitmapInfo As GDIP_BITMAPINFO, gdiBitmapData As Byte) As Bitmap
        Return New Bitmap(gdiBitmapInfo, gdiBitmapData)
    End Function
    
    Function Bitmap(hbm As LongPtr, hpal As LongPtr) As Bitmap
        Return New Bitmap(hbm, hpal)
    End Function
    
    Function Bitmap(hicon As LongPtr) As Bitmap
        Return New Bitmap(hicon)
    End Function
    
    Function Bitmap(hInstance As LongPtr, ByVal bitmapName As String) As Bitmap
        Return New Bitmap(hInstance, bitmapName)
    End Function
    
    Type Histogram
        channel0() As UINT
        channel1() As UINT
        channel2() As UINT
        channel3() As UINT
    End Type
    
    Type GpBitmap
        ' Inherits GpImage
        Native As LongPtr
        
        [UseGetLastError(False)]
        DeclareWide PtrSafe Function CreateFromFileInto Lib "gdiplus" Alias "GdipCreateBitmapFromFile" (ByVal FileName As String, ByRef bitmap As GpBitmap) As GpStatus
        [UseGetLastError(False)]
        DeclareWide PtrSafe Function CreateFromFileICMInto Lib "gdiplus" Alias "GdipCreateBitmapFromFileICM" (ByVal FileName As String, ByRef bitmap As GpBitmap) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function CreateFromStreamInto Lib "gdiplus" Alias "GdipCreateBitmapFromStream" (ByVal stream As GDIP_IStream, ByRef bitmap As GpBitmap) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function CreateFromStreamICMInto Lib "gdiplus" Alias "GdipCreateBitmapFromStreamICM" (ByVal stream As GDIP_IStream, ByRef bitmap As GpBitmap) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function CreateFromResourceInto Lib "gdiplus" Alias "GdipCreateBitmapFromResource" (ByVal hInstance As LongPtr, ByVal lpBitmapName As LongPtr, ByRef bitmap As GpBitmap) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function CreateFromScan0Into Lib "gdiplus" Alias "GdipCreateBitmapFromScan0" (ByVal Width As Long, ByVal Height As Long, ByVal stride As Long, ByVal format As PixelFormat, scan0 As Any, bitmap As GpBitmap) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function CreateFromGraphicsInto Lib "gdiplus" Alias "GdipCreateBitmapFromGraphics" (ByVal Width As Long, ByVal Height As Long, ByVal Graphics As GpGraphics, bitmap As GpBitmap) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function CreateFromDirectDrawSurfaceInto Lib "gdiplus" Alias "GdipCreateBitmapFromDirectDrawSurface" (surface As Any, bitmap As GpBitmap) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function CreateFromGdiDibInto Lib "gdiplus" Alias "GdipCreateBitmapFromGdiDib" (gdiBitmapInfo As GDIP_BITMAPINFO, ByVal gdiBitmapData As LongPtr, bitmap As GpBitmap) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function CreateFromHBITMAPInto Lib "gdiplus" Alias "GdipCreateBitmapFromHBITMAP" (ByVal hbm As LongPtr, ByVal hpal As LongPtr, bitmap As GpBitmap) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function CreateFromHICONInto Lib "gdiplus" Alias "GdipCreateBitmapFromHICON" (ByVal hicon As LongPtr, bitmap As GpBitmap) As GpStatus
        
        [UseGetLastError(False)]
        Declare PtrSafe Function ApplyEffect Lib "gdiplus" Alias "GdipBitmapApplyEffect" (ByVal Me As GpBitmap, ByVal effect As GpEffect, roi As GDIP_RECT, ByVal useAuxData As GDIP_BOOL, auxData As Any, auxDataSize As Long) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function LockBits Lib "gdiplus" Alias "GdipBitmapLockBits" (ByVal Me As GpBitmap, Rect As GpRect, ByVal flags As Long, ByVal format As PixelFormat, lockedBitmapData As BitmapData) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function UnlockBits Lib "gdiplus" Alias "GdipBitmapUnlockBits" (ByVal Me As GpBitmap, lockedBitmapData As BitmapData) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function GetPixel Lib "gdiplus" Alias "GdipBitmapGetPixel" (ByVal Me As GpBitmap, ByVal X As Long, ByVal Y As Long, Color As Color) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function SetPixel Lib "gdiplus" Alias "GdipBitmapSetPixel" (ByVal Me As GpBitmap, ByVal X As Long, ByVal Y As Long, ByVal Color As Color) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function ConvertFormat Lib "gdiplus" Alias "GdipBitmapConvertFormat" (ByVal Me As GpBitmap, ByVal format As PixelFormat, ByVal dithertype As DitherType, ByVal palettetype As PaletteType, palette As ColorPalette, ByVal alphaThresholdPercent As Single) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function GetHistogram Lib "gdiplus" Alias "GdipBitmapGetHistogram" (ByVal Me As GpBitmap, ByVal format As HistogramFormat, ByVal NumberOfEntries As Long, channel0 As Long, channel1 As Long, channel2 As Long, channel3 As Long) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function GetHistogramSize Lib "gdiplus" Alias "GdipBitmapGetHistogramSize" (ByVal format As HistogramFormat, NumberOfEntries As Long) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function SetResolution Lib "gdiplus" Alias "GdipBitmapSetResolution" (ByVal Me As GpBitmap, ByVal xdpi As Single, ByVal ydpi As Single) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function CreateHICON Lib "gdiplus" Alias "GdipCreateHICONFromBitmap" (ByVal Me As GpBitmap, hbmReturn As LongPtr) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function CreateHBITMAP Lib "gdiplus" Alias "GdipCreateHBITMAPFromBitmap" (ByVal Me As GpBitmap, ByRef hbmReturn As LongPtr, ByVal Background As Color) As GpStatus
    End Type
    
    Function GpBitmap() As GpBitmap
        GpBitmap.Native = 0
    End Function
    
    '#if (GDIPVER >= 0x0110)
    ' The palette must be allocated and count must be set to the number of
    ' entries in the palette. If there are not enough, the API will fail.
    Function InitializePalette( _
            palette As ColorPalette, /* Palette to initialize. */ _
            ByVal paletteTypee As PaletteType, _
            ByVal optimalColors!, _
            ByVal useTransparentColor As Boolean, /* add a transparent color to the palette? */ _
            Optional bitmap As Bitmap /* optional bitmap for median cut */ _
            ) As GpStatus
        Return GdipInitializePalette(palette, PaletteType, optimalColors, useTransparentColor, GetNative(bitmap))
    End Function
    
    Function ApplyEffect( _
            inputs() As Bitmap, effect As Effect, _
            output As Bitmap _
            ) As GpStatus
        Return ApplyEffect(inputs, effect, ByVal CLngPtr(0), ByVal CLngPtr(0), output)
    End Function
    
    Function ApplyEffect( _
            inputs() As Bitmap, effect As Effect, _
            ROI As GDIP_RECT, /* optional */ _
            outputRect As GDIP_RECT, /* optional */ _
            output As Bitmap _
            ) As GpStatus
        Dim outputNative As GpBitmap
        Dim nativeInputs() As GpBitmap
        ReDim nativeInputs(1 To ArrayLen(inputs))
        Dim j As Integer = 1
        For i As Integer = LBound(inputs) To UBound(inputs)
            nativeInputs(j) = GetNative(inputs(i))
            j += 1
        Next
        
        If effect.AuxData <> 0 Then effect.SetAuxData(0, 0)
        
        Dim auxData As LongPtr
        Dim auxDataSize&
        Dim status As GpStatus = GdipBitmapCreateApplyEffect( _
                nativeInputs(1), ArrayLen(nativeInputs), GetNative(effect), _
                ROI, _
                outputRect, _
                outputNative, _
                effect.UseAuxData, _
                auxData, auxDataSize _
                )
        
        If status = Ok And outputNative.Native <> 0 Then
            effect.SetAuxData(auxData, auxDataSize)
            Set output = New Bitmap(outputNative)
        Else
            If auxData <> 0 And auxDataSize > 0 Then GdipFree(auxData)
            Set output = Nothing
        End If
        
        Return status
    End Function
    
    [UseGetLastError(False)]
    Declare PtrSafe Function GetPixel Lib "gdiplus" Alias "GdipBitmapGetPixel" (ByVal image As GpImage, ByVal X As Long, ByVal Y As Long, Color As Color) As GpStatus
    [UseGetLastError(False)]
    Declare PtrSafe Function SetPixel Lib "gdiplus" Alias "GdipBitmapSetPixel" (ByVal image As GpImage, ByVal X As Long, ByVal Y As Long, ByVal Color As Color) As GpStatus
    
    
    [UseGetLastError(False)]
    Declare PtrSafe Function GdipInitializePalette Lib "gdiplus" (palette As ColorPalette, ByVal paletteType As PaletteType, ByVal optimalColors As Long, ByVal useTransparentColor As GDIP_BOOL, ByVal bitmap As GpBitmap) As GpStatus
    [UseGetLastError(False)]
    Declare PtrSafe Function GdipBitmapCreateApplyEffect Lib "gdiplus" (inputBitmaps As Any, ByVal numInputs As Long, ByVal effect As GpEffect, roi As GDIP_RECT, outputRect As GDIP_RECT, outputBitmap As GpBitmap, ByVal useAuxData As GDIP_BOOL, auxData As Any, auxDataSize As Long) As GpStatus
    [UseGetLastError(False)]
    Declare PtrSafe Function GdipCloneBitmapArea Lib "gdiplus" (ByVal x As Single, ByVal y As Single, ByVal width As Single, ByVal height As Single, ByVal format As PixelFormat, ByVal srcBitmap As GpBitmap, dstBitmap As GpBitmap) As GpStatus
    [UseGetLastError(False)]
    Declare PtrSafe Function GdipCloneBitmapAreaI Lib "gdiplus" (ByVal x As Long, ByVal y As Long, ByVal width As Long, ByVal height As Long, ByVal format As PixelFormat, ByVal srcBitmap As GpBitmap, dstBitmap As GpBitmap) As GpStatus
End Module
