' A part of the GDI+ Package for TwinBasic  https://github.com/kubao/GdiPlus
' Licensed under the MIT license. (c) 2026 Sunderland Ober Consulting.
' "Windows" is a trademark of the Microsoft Corporation.
' Modeled after code and descriptions (c) Microsoft, taken from SDK headers and official documentation.
' Certain flat API function and type declarations taken from WinDevLib by fafalone, with thanks.

[COMCreatable(False)]
[Description("Solid Fill Brush Object")]
Class SolidBrush
    Inherits GdiPlusBase
    Implements IBrush
    Protected br As GpSolidFill
    
    Function Clone() As SolidBrush
        Dim newBrush As GpSolidFill
        SetStatus(br.Clone(newBrush))
        If LastResult = Ok Then Return New SolidBrush(newBrush, True)
    End Function
    
    Protected Sub New(ByVal native As GpSolidFill, ByVal tag As Boolean)
        SetNativeSolid(native.Release())
    End Sub
        
    Sub New(ByVal rgb&)
        SetStatus(br.CreateInto(FromCOLORREF(rgb), br))
    End Sub
    
    Sub New(ByVal color As Color)
        SetStatus(br.CreateInto(color, br))
    End Sub
    
    Property Get NativeBrush() As GpBrush Implements IBrush.NativeBrush
        NativeBrush.Native = br.Native
    End Property
    
    Private Sub Class_Terminate()
        br.Delete
    End Sub
    
    Function CloneBrush() As IBrush Implements IBrush.CloneBrush
        Return Clone()
    End Function
    
    Property Get Type() As GpBrushType Implements IBrush.Type
        Dim ty As GpBrushType
        SetStatus(br.GetType(ty))
        Debug.Assert ty = BrushTypeSolidColor OrElse LastResult <> Ok
        Return ty
    End Property
        
    Property Get Color() As Color
        SetStatus(br.GetColor(Color))
    End Property
    
    Property Let Color(ByVal color As Color)
        SetStatus(br.SetColor(color))
    End Property

    Property Let RGB(ByVal rgb&)
        SetStatus(br.SetRGB(rgb))
    End Property
    
    Property Get RGB() As Long
        SetStatus(br.GetRGB(RGB))
    End Property
    
    Sub New(ByVal nativeBrush As GpSolidFill, ByVal lastStatus As GpStatus)
        SetStatus(lastStatus)
        SetNativeSolid(nativeBrush)
    End Sub
    
    Protected Property Get Native() As GpSolidFill
        Native.Native = br.Native
    End Property
    
    Protected Sub SetNativeSolid(ByVal nativeSolid As GpSolidFill)
        br.Native = nativeSolid.Native
    End Sub
End Class

Module GdipSolidBrush
    Function SolidBrush(ByVal rgb&) As SolidBrush
        Return New SolidBrush(rgb)
    End Function

    Function SolidBrush(ByVal color As Color) As SolidBrush
        Return New SolidBrush(color)
    End Function
    
    Type GpSolidFill
        'Inherits GpBrush
        Native As LongPtr
        
        Function Create(ByVal argb As Color) As GpStatus
            Me.CreateInto(argb, Me)
        End Function
        
        Function Release() As GpSolidFill
            Release.Native = Me.Native
            Me.Native = 0
        End Function
        
        [UseGetLastError(False)]
        Declare PtrSafe Function CreateInto Lib "gdiplus" Alias "GdipCreateSolidFill" (ByVal argb As Color, brush As GpSolidFill) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function Delete Lib "gdiplus" Alias "GdipDeleteBrush" (ByVal Me As GpSolidFill) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function Clone Lib "gdiplus" Alias "GdipCloneBrush" (ByVal Me As GpSolidFill, cloneBrush As GpSolidFill) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function GetType Lib "gdiplus" Alias "GdipGetBrushType" (ByVal Me As GpSolidFill, type As GpBrushType) As GpStatus

        Function SetRGB(ByVal rgb&) As GpStatus
            Return Me.SetColor(FromCOLORREF(rgb))
        End Function
    
        Function GetRGB(rgb&) As GpStatus
            Dim clr As Color
            GetRGB = Me.GetColor(clr)
            rgb = clr.argb And &h00FF_FFFF
        End Function
        
        [UseGetLastError(False)]
        Declare PtrSafe Function SetColor Lib "gdiplus" Alias "GdipSetSolidFillColor" (ByVal Me As GpSolidFill, ByVal color As Color) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function GetColor Lib "gdiplus" Alias "GdipGetSolidFillColor" (ByVal Me As GpSolidFill, color As Color) As GpStatus
    End Type
    
    Function GpSolidFill() As GpSolidFill
        GpSolidFill.Native = 0
    End Function
    
    Function GpSolidFill(nativeBrush As GpBrush) As GpSolidFill
        GpSolidFill.Native = nativeBrush.Native
    End Function
End Module
