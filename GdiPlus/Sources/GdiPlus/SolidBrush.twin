' A part of the GDI+ Package for TwinBasic  https://github.com/kubao/GdiPlus
' Licensed under the MIT license. (c) 2026 Sunderland Ober Consulting.
' "Windows" is a trademark of the Microsoft Corporation.
' Modeled after code and descriptions (c) Microsoft, taken from SDK headers and official documentation.
' Certain flat API function and type declarations taken from WinDevLib by fafalone, with thanks.

[COMCreatable(False)]
[Description("Solid Fill Brush Object")]
Class SolidBrush
    Inherits Brush
    
    Sub New(ByVal rgb&)
        LastStatus = GdipSolidBrush.GdipCreateSolidFill(FromCOLORREF(rgb), br)
    End Sub
    
    Sub New(ByVal color As Color)
        LastStatus = GdipSolidBrush.GdipCreateSolidFill(color, br)
    End Sub
    
    Property Get Color() As Color
        SetStatus(GdipGetSolidFillColor(br, Color))
    End Property
    
    Property Let Color(ByVal color As Color)
        SetStatus(GdipSetSolidFillColor(br, color))
    End Property

    Property Let RGB(ByVal rgb&)
        SetStatus(GdipSetSolidFillColor(br, FromCOLORREF(rgb)))
    End Property
    
    Sub New(ByVal nativeBrush As GpSolidFill, ByVal lastStatus As GpStatus)
        lastStatus = lastStatus
        SetNativeSolid(nativeBrush)
    End Sub
    
    Protected Property Get Native() As GpSolidFill
        Native.Native = br.Native
    End Property
    
    Protected Sub SetNativeSolid(ByVal nativeSolid As GpSolidFill)
        br.Native = nativeSolid.Native
    End Sub
End Class

Module GdipSolidBrush
    Function SolidBrush(ByVal rgb&) As SolidBrush
        Return New SolidBrush(rgb)
    End Function

    Function SolidBrush(ByVal color As Color) As SolidBrush
        Return New SolidBrush(color)
    End Function
    
    Type GpSolidFill
        'Inherits GpBrush
        Native As LongPtr
        
        Function AsGpBrush() As GpBrush
            AsGpBrush.Native = Me.Native
        End Function
    End Type
    
    Function GpSolidFill() As GpSolidFill
        GpSolidFill.Native = 0
    End Function
    
    Function GpSolidFill(nativeBrush As GpBrush) As GpSolidFill
        GpSolidFill.Native = nativeBrush.Native
    End Function
    
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipCreateSolidFill Lib "gdiplus" (ByVal argb As Color, brush As GpSolidFill) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipSetSolidFillColor Lib "gdiplus" (ByVal brush As GpBrush, ByVal color As Color) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipGetSolidFillColor Lib "gdiplus" (ByVal brush As GpBrush, color As Color) As GpStatus
End Module
