' A part of the GDI+ Package for TwinBasic  https://github.com/kubao/GdiPlus
' Licensed under the MIT license. (c) 2026 Sunderland Ober Consulting.
' "Windows" is a trademark of the Microsoft Corporation.
' Modeled after code and descriptions (c) Microsoft, taken from SDK headers and official documentation.
' Certain flat API function and type declarations taken from WinDevLib by fafalone, with thanks.

[COMCreatable(False)]
Class Effect
    Inherits GdiPlusBase
    Protected fx As GpEffect
    Protected PropAuxDataSize&
    Protected PropAuxData As LongPtr
    Protected PropUseAuxData As Boolean
    
    Sub New()
        fx.Native = Null
        PropAuxDataSize = 0
        PropAuxData = Null
        PropUseAuxData = False
    End Sub
    
    Sub Class_Terminate()
        GdipFree(PropAuxData)
        GdipDeleteEffect(fx)
    End Sub
    
    Property Get AuxDataSize&()
        Return PropAuxDataSize
    End Property
        
    Property Get AuxData() As LongPtr
        Return PropAuxData
    End Property
    
    Sub SetAuxData(ByVal auxData As LongPtr, ByVal auxDataSize&)
        If PropAuxData <> 0 Then GdipFree(auxData)
        PropAuxData = auxData
        PropAuxDataSize = auxDataSize&
    End Sub
    
    Property Get UseAuxData() As Boolean
        Return PropUseAuxData
    End Property
    
    Property Let UseAuxData(ByVal useAuxDataFlag As Boolean)
        PropUseAuxData = useAuxDataFlag
    End Property
    
    Property Get ParameterSize() As UINT
        Return SetStatus(GdipGetEffectParameterSize(fx, ParameterSize))
    End Property
    
    Protected Property Get Native() As GpEffect
        Return fx
    End Property
        
    Protected Sub New(ByVal nativeEffect As GpEffect)
        SetNativeEffect(nativeEffect)
    End Sub
    
    Protected Sub SetNativeEffect(ByVal nativeEffect As GpEffect)
        fx = nativeEffect
    End Sub
End Class

[COMCreatable(False)]
Class Blur
    Inherits Effect
    
    Sub New()
        GdipCreateEffect(BlurEffectGuid, fx)
    End Sub
    
    Property Let Parameters(parameters As BlurParams)
        SetStatus(GdipSetEffectParameters(fx, parameters, LenB(Of BlurParams)))
    End Property
    
    Property Get Parameters(Optional size As UINT) As BlurParams
        SetStatus(GdipGetEffectParameters(fx, size, Parameters))
    End Property
End Class

[COMCreatable(False)]
Class Sharpen
    Inherits Effect
    
    Sub New()
        GdipCreateEffect(SharpenEffectGuid, fx)
    End Sub
    
    Property Let Parameters(parameters As SharpenParams)
        SetStatus(GdipSetEffectParameters(fx, parameters, LenB(Of SharpenParams)))
    End Property
    
    Property Get Parameters(Optional size As UINT) As SharpenParams
        SetStatus(GdipGetEffectParameters(fx, size, Parameters))
    End Property
End Class

[COMCreatable(False)]
Class RedEyeCorrection
    Inherits Effect
    
    Sub New()
        GdipCreateEffect(RedEyeCorrectionEffectGuid, fx)
    End Sub
    
    Property Let Parameters(parameters As RedEyeCorrectionParams)
        Dim size& = LenB(Of RedEyeCorrectionParams) + parameters.numberOfAreas * LenB(Of RECT)
        SetStatus(GdipSetEffectParameters(fx, parameters, size))
    End Property
    
    Property Get Parameters(Optional size As UINT) As RedEyeCorrectionParams
        SetStatus(GdipGetEffectParameters(fx, size, Parameters))
    End Property
End Class

[COMCreatable(False)]
Class BrightnessContrast
    Inherits Effect
    
    Sub New()
        GdipCreateEffect(BrightnessContrastEffectGuid, fx)
    End Sub
    
    Property Let Parameters(parameters As BrightnessContrastParams)
        SetStatus(GdipSetEffectParameters(fx, parameters, LenB(Of BrightnessContrastParams)))
    End Property
    
    Property Get Parameters(Optional size As UINT) As BrightnessContrastParams
        SetStatus(GdipGetEffectParameters(fx, size, Parameters))
    End Property
End Class

[COMCreatable(False)]
Class HueSaturationLightness
    Inherits Effect
    
    Sub New()
        GdipCreateEffect(HueSaturationLightnessEffectGuid, fx)
    End Sub
    
    Property Let Parameters(parameters As HueSaturationLightnessParams)
        SetStatus(GdipSetEffectParameters(fx, parameters, LenB(Of HueSaturationLightnessParams)))
    End Property
    
    Property Get Parameters(Optional size As UINT) As HueSaturationLightnessParams
        SetStatus(GdipGetEffectParameters(fx, size, Parameters))
    End Property
End Class

[COMCreatable(False)]
Class Levels
    Inherits Effect
    
    Sub New()
        GdipCreateEffect(LevelsEffectGuid, fx)
    End Sub
    
    Property Let Parameters(parameters As LevelsParams)
        SetStatus(GdipSetEffectParameters(fx, parameters, LenB(Of LevelsParams)))
    End Property
    
    Property Get Parameters(Optional size As UINT) As LevelsParams
        SetStatus(GdipGetEffectParameters(fx, size, Parameters))
    End Property
End Class

[COMCreatable(False)]
Class Tint
    Inherits Effect
    
    Sub New()
        GdipCreateEffect(TintEffectGuid, fx)
    End Sub
    
    Property Let Parameters(parameters As TintParams)
        SetStatus(GdipSetEffectParameters(fx, parameters, LenB(Of TintParams)))
    End Property
    
    Property Get Parameters(Optional size As UINT) As TintParams
        SetStatus(GdipGetEffectParameters(fx, size, Parameters))
    End Property
End Class

[COMCreatable(False)]
Class ColorBalance
    Inherits Effect
    
    Sub New()
        GdipCreateEffect(ColorBalanceEffectGuid, fx)
    End Sub
    
    Property Let Parameters(parameters As ColorBalanceParams)
        SetStatus(GdipSetEffectParameters(fx, parameters, LenB(Of ColorBalanceParams)))
    End Property
    
    Property Get Parameters(Optional size As UINT) As ColorBalanceParams
        SetStatus(GdipGetEffectParameters(fx, size, Parameters))
    End Property
End Class

[COMCreatable(False)]
Class ColorMatrixEffect
    Inherits Effect
    
    Sub New()
        GdipCreateEffect(ColorMatrixEffectGuid, fx)
    End Sub
    
    Property Let Parameters(parameters As ColorMatrix)
        SetStatus(GdipSetEffectParameters(fx, parameters, LenB(Of ColorMatrix)))
    End Property
    
    Property Get Parameters(Optional size As UINT) As ColorMatrix
        SetStatus(GdipGetEffectParameters(fx, size, Parameters))
    End Property
End Class

[COMCreatable(False)]
Class ColorLUT
    Inherits Effect
    
    Sub New()
        GdipCreateEffect(ColorLUTEffectGuid, fx)
    End Sub
    
    Property Let Parameters(parameters As ColorLUTParams)
        SetStatus(GdipSetEffectParameters(fx, parameters, LenB(Of ColorLUTParams)))
    End Property
    
    Property Get Parameters(Optional size As UINT) As ColorLUTParams
        SetStatus(GdipGetEffectParameters(fx, size, Parameters))
    End Property
End Class

[COMCreatable(False)]
Class ColorCurve
    Inherits Effect
    
    Sub New()
        GdipCreateEffect(ColorLUTEffectGuid, fx)
    End Sub
    
    Property Let Parameters(parameters As ColorCurveParams)
        SetStatus(GdipSetEffectParameters(fx, parameters, LenB(Of ColorCurveParams)))
    End Property
    
    Property Get Parameters(Optional size As UINT) As ColorCurveParams
        SetStatus(GdipGetEffectParameters(fx, size, Parameters))
    End Property
End Class

Module GdipEffects
    Function Blur() As Blur
        Return New Blur
    End Function
    
    Function Blur(params As BlurParams) As Blur
        Set Blur = New Blur
        Blur.Parameters = params
    End Function
    
    Function Blur(ByVal radius!, ByVal expandEdge As Boolean) As Blur
        Set Blur = New Blur
        Blur.Parameters = BlurParams(radius, expandEdge)
    End Function
    
    Function Sharpen() As Sharpen
        Return New Sharpen
    End Function
    
    Function Sharpen(params As SharpenParams) As Sharpen
        Set Sharpen = New Sharpen
        Sharpen.Parameters = params
    End Function
    
    Function Sharpen(ByVal radius!, ByVal amount!) As Sharpen
        Set Sharpen = New Sharpen
        Sharpen.Parameters = SharpenParams(radius, amount)
    End Function
    
    Function RedEyeCorrection() As RedEyeCorrection
        Return New RedEyeCorrection
    End Function
    
    Function RedEyeCorrection(params As RedEyeCorrectionParams) As RedEyeCorrection
        Set RedEyeCorrection = New RedEyeCorrection
        RedEyeCorrection.Parameters = params
    End Function
    
    Function RedEyeCorrection(areas() As GpRect) As RedEyeCorrection
        Set RedEyeCorrection = New RedEyeCorrection
        RedEyeCorrection.Parameters = RedEyeCorrectionParams(areas)
    End Function
    
    Function BrightnessContrast() As BrightnessContrast
        Return New BrightnessContrast
    End Function
    
    Function BrightnessContrast(params As BrightnessContrastParams) As BrightnessContrast
        Set BrightnessContrast = New BrightnessContrast
        BrightnessContrast.Parameters = params
    End Function
    
    Function BrightnessContrast(ByVal brightness&, ByVal contrast&) As BrightnessContrast
        Set BrightnessContrast = New BrightnessContrast
        BrightnessContrast.Parameters = BrightnessContrastParams(brightness, contrast)
    End Function
    
    Function HueSaturationLightness() As HueSaturationLightness
        Return New HueSaturationLightness
    End Function
    
    Function HueSaturationLightness(params As HueSaturationLightnessParams) As HueSaturationLightness
        Set HueSaturationLightness = New HueSaturationLightness
        HueSaturationLightness.Parameters = params
    End Function
    
    Function HueSaturationLightness(ByVal hue&, ByVal saturation&, ByVal lightness&) As HueSaturationLightness
        Set HueSaturationLightness = New HueSaturationLightness
        HueSaturationLightness.Parameters = HueSaturationLightnessParams(hue, saturation, lightness)
    End Function
    
    Function Levels() As Levels
        Return New Levels
    End Function
    
    Function Levels(params As LevelsParams) As Levels
        Set Levels = New Levels
        Levels.Parameters = params
    End Function
    
    Function Levels(ByVal highlight&, ByVal midtone&, ByVal shadow&) As Levels
        Set Levels = New Levels
        Levels.Parameters = LevelsParams(highlight, midtone, shadow)
    End Function
    
    Function Tint() As Tint
        Return New Tint
    End Function
    
    Function Tint(params As TintParams) As Tint
        Set Tint = New Tint
        Tint.Parameters = params
    End Function
    
    Function Tint(ByVal hue&, ByVal amount&) As Tint
        Set Tint = New Tint
        Tint.Parameters = TintParams(hue, amount)
    End Function
    
    Function ColorBalance() As ColorBalance
        Return New ColorBalance
    End Function
    
    Function ColorBalance(params As ColorBalanceParams) As ColorBalance
        Set ColorBalance = New ColorBalance
        ColorBalance.Parameters = params
    End Function
    
    Function ColorBalance(ByVal cyanRed&, ByVal magentaGreen&, ByVal yellowBlue&) As ColorBalance
        Set ColorBalance = New ColorBalance
        ColorBalance.Parameters = ColorBalanceParams(cyanRed, magentaGreen, yellowBlue)
    End Function
    
    Function ColorMatrixEffect() As ColorMatrixEffect
        Return New ColorMatrixEffect
    End Function
    
    Function ColorMatrixEffect(params As ColorMatrix) As ColorMatrixEffect
        Set ColorMatrixEffect = New ColorMatrixEffect
        ColorMatrixEffect.Parameters = params
    End Function
    
    Function ColorLUT() As ColorLUT
        Return New ColorLUT
    End Function
    
    Function ColorLUT(params As ColorLUTParams) As ColorLUT
        Set ColorLUT = New ColorLUT
        ColorLUT.Parameters = params
    End Function
    
    Function ColorLUT(lutB() As Byte, lutG() As Byte, lutR() As Byte, lutA() As Byte) As ColorLUT
        Set ColorLUT = New ColorLUT
        ColorLUT.Parameters = ColorLutParams(lutB, lutG, lutR, lutA)
    End Function
    
    Function ColorCurve() As ColorCurve
        Return New ColorCurve
    End Function
    
    Function ColorCurve(params As ColorCurveParams) As ColorCurve
        Set ColorCurve = New ColorCurve
        ColorCurve.Parameters = params
    End Function
    
    Function ColorCurve(ByVal adjustment As CurveAdjustments, ByVal channel As CurveChannel, ByVal value&) As ColorCurve
        Set ColorCurve = New ColorCurve
        ColorCurve.Parameters = ColorCurveParams(adjustment, channel, value)
    End Function
    
    Type GpEffect
        Native As LongPtr
    End Type
        
    Function GpEffect() As GpEffect
        GpEffect.Native = 0
    End Function

    Type SharpenParams
        radius!
        amount!
    End Type
    
    Function SharpenParams(ByVal radius!, ByVal amount!) As SharpenParams
        SharpenParams.radius = radius
        SharpenParams.amount = amount
    End Function
    
    Type BlurParams
        radius!
        expandEdge As BOOL
    End Type
    
    Function BlurParams(ByVal radius!, ByVal expandEdge As Boolean) As BlurParams
        BlurParams.radius = radius
        BlurParams.expandEdge = expandEdge
    End Function
    
    Type BrightnessContrastParams
        brightness&
        contrast&
        
        ' For compatibility with MS API, these names are too verbose otherwise
        Property Get brightnessLevel&()
            Return Me.brightness
        End Property
        
        Property Let brightnessLevel(ByVal level&)
            Me.brightness = level
        End Property
                
        Property Get contrastLevel&()
            Return Me.contrast&
        End Property

        Property Let contrastLevel(ByVal level&)
            Me.contrast = level
        End Property
    End Type
    
    Function BrightnessContrastParams(ByVal brightness&, ByVal contrast&) As BrightnessContrastParams
        BrightnessContrastParams.brightness = brightness&
        BrightnessContrastParams.contrast = contrast&
    End Function
    
    Type RedEyeCorrectionParams
        numberOfAreas As UINT
        areas As LongPtr ' Pointer to RECT
    
        Sub Free()
            GdipFree(Me.areas)
            Me.numberOfAreas = 0
            Me.areas = 0
        End Sub
        
        Function Allocate(ByVal newCount&) As GpStatus
            Return AllocateArrayInUDT(newCount, LenB(Of RECT), Me.areas, Me.numberOfAreas)
        End Function
        
        Property Get Area(ByVal index&) As RECT
            Dim src As LongPtr = Me.areas + (index - 1) * LenB(Of RECT)
            If index >= Me.numberOfAreas Then Err.Raise ERROR_INVALID_PARAMETER
            vbaCopyBytesRef(LenB(Of RECT), Area, ByVal src)
        End Property
        
        Property Set Area(ByVal index&, value As RECT)
            Dim dst As LongPtr = Me.areas + (index - 1) * LenB(Of RECT)
            If index >= Me.numberOfAreas Then Err.Raise ERROR_INVALID_PARAMETER
            vbaCopyBytesRef(LenB(Of RECT), ByVal dst, value)
        End Property
        
        [Description("Returns a view into the areas array." & vbCrLf & _
                "The view is valid only as long as the RedEyeCorrectionParams exists.")]
        Function GetAreas() As RECT()
            Return GetStaticTrailingSA(Of RECT)(Me.areas, Me.numberOfAreas)
        End Function
    End Type
    
    Function RedEyeCorrectionParams(areas() As GpRect) As RedEyeCorrectionParams
        Dim count& = Len(areas)
        If count > 0 Then
            RedEyeCorrectionParams.Allocate(count)
            Dim dst As LongPtr = RedEyeCorrectionParams.areas
            For i As Long = LBound(areas) To UBound(areas)
                SetRECT(ByVal dst, areas(i))
                dst += LenB(Of RECT)
            Next
        End If
        RedEyeCorrectionParams.numberOfAreas = count
    End Function

    Private Sub SetGpRect(dst As GpRect, src As RECT)
        dst.X = src.Left
        dst.Y = src.Top
        dst.Width = src.Right - src.Left
        dst.Height = src.Bottom - src.Top
    End Sub
    
    Private Sub SetRECT(dst As RECT, src As GpRect)
        dst.Left = src.X
        dst.Top = src.Y
        dst.Right = src.X + src.Width
        dst.Bottom = src.Y + src.Height
    End Sub
    
    Type HueSaturationLightnessParams
        hue&
        saturation&
        lightness&
        
        ' For compatibility with MS API, these names are too verbose otherwise
        Property Get hueLevel&()
            Return Me.hue
        End Property
        
        Property Set hueLevel(ByVal level&)
            Me.hue = level
        End Property
        
        Property Get saturationLevel&()
            Return Me.saturation
        End Property

        Property Set saturationLevel(ByVal level&)
            Me.saturation = level
        End Property
        
        Property Get lightnessLevel&()
            Return Me.lightness
        End Property
        
        Property Set lightnessLevel(ByVal level&)
            Me.lightness = level
        End Property
    End Type
    
    Function HueSaturationLightnessParams(ByVal hue&, ByVal saturation&, ByVal lightness&) As HueSaturationLightnessParams
        HueSaturationLightnessParams.hue = hue
        HueSaturationLightnessParams.saturation = saturation
        HueSaturationLightnessParams.lightness = lightness
    End Function
    
    Type TintParams
        hue&
        amount&
    End Type
    
    Function TintParams(ByVal hue&, ByVal amount&) As TintParams
        TintParams.hue = hue
        TintParams.amount = amount
    End Function
    
    Type LevelsParams
        highight&
        midtone&
        shadow&
    End Type
    
    Function LevelsParams(ByVal highlight&, ByVal midtone&, ByVal shadow&) As LevelsParams
        LevelsParams.highight = highlight
        LevelsParams.midtone = midtone
        LevelsParams.shadow = shadow
    End Function
    
    Type ColorBalanceParams
        cyanRed&
        magentaGreen&
        yellowBlue&
    End Type
    
    Function ColorBalanceParams(ByVal cyanRed&, ByVal magentaGreen&, ByVal yellowBlue&) As ColorBalanceParams
        ColorBalanceParams.cyanRed = cyanRed
        ColorBalanceParams.magentaGreen = magentaGreen&
        ColorBalanceParams.yellowBlue = yellowBlue&
    End Function
    
    Type ColorLUTParams
        lutB As ColorChannelLUT
        lutG As ColorChannelLUT
        lutR As ColorChannelLUT
        lutA As ColorChannelLUT
    End Type
    
    Function ColorLutParams(lutB() As Byte, lutG() As Byte, lutR() As Byte, lutA() As Byte) As ColorLUTParams
        If Len(lutB) = 256 And Len(lutG) = 256 And Len(lutR) = 256 And Len(lutA) = 256 Then
            vbaCopyBytesRef(256, ColorLutParams.lutB, lutB)
            vbaCopyBytesRef(256, ColorLutParams.lutG, lutG)
            vbaCopyBytesRef(256, ColorLutParams.lutR, lutR)
            vbaCopyBytesRef(256, ColorLutParams.lutA, lutB)
        End If
    End Function
    
    Enum CurveAdjustments
        AdjustExposure
        AdjustDensity
        AdjustContrast
        AdjustHighlight
        AdjustShadow
        AdjustMidtone
        AdjustWhiteSaturation
        AdjustBlackSaturation
    End Enum
    
    Enum CurveChannel
        CurveChannelAll
        CurveChannelRed
        CurveChannelGreen
        CurveChannelBlue
    End Enum
    
    Type ColorCurveParams
        adjustment As CurveAdjustments
        channel As CurveChannel
        value&
        
        Property Get adjustValue&()
            Return Me.value&
        End Property
        
        Property Let adjustValue(ByVal newValue&)
            Me.value& = newValue
        End Property
    End Type
    
    Function ColorCurveParams(ByVal adjustment As CurveAdjustments, ByVal channel As CurveChannel, ByVal value&) As ColorCurveParams
        ColorCurveParams.adjustment = adjustment
        ColorCurveParams.channel = channel
        ColorCurveParams.adjustValue = value
    End Function
    
    '-----------------------------------------------------------------------------
    ' GDI+ effect GUIDs
    '-----------------------------------------------------------------------------
    
    Public Function BlurEffectGuid() As UUID
    '{633C80A4-1843-482b-9EF2-BE2834C5FDD4}
    Static iid As UUID
     If (iid.Data1 = 0) Then Call DEFINE_UUID(iid, &H633C80A4, CInt(&H1843), CInt(&H482b), &H9E, &HF2, &HBE, &H28, &H34, &HC5, &HFD, &HD4)
     BlurEffectGuid = iid
    End Function
    Public Function SharpenEffectGuid() As UUID
    '{63CBF3EE-C526-402c-8F71-62C540BF5142}
    Static iid As UUID
     If (iid.Data1 = 0) Then Call DEFINE_UUID(iid, &H63CBF3EE, CInt(&HC526), CInt(&H402c), &H8F, &H71, &H62, &HC5, &H40, &HBF, &H51, &H42)
     SharpenEffectGuid = iid
    End Function
    Public Function ColorMatrixEffectGuid() As UUID
    '{718F2615-7933-40e3-A511-5F68FE14DD74}
    Static iid As UUID
     If (iid.Data1 = 0) Then Call DEFINE_UUID(iid, &H718F2615, CInt(&H7933), CInt(&H40e3), &HA5, &H11, &H5F, &H68, &HFE, &H14, &HDD, &H74)
     ColorMatrixEffectGuid = iid
    End Function
    Public Function ColorLUTEffectGuid() As UUID
    '{A7CE72A9-0F7F-40d7-B3CC-D0C02D5C3212}
    Static iid As UUID
     If (iid.Data1 = 0) Then Call DEFINE_UUID(iid, &HA7CE72A9, CInt(&H0F7F), CInt(&H40d7), &HB3, &HCC, &HD0, &HC0, &H2D, &H5C, &H32, &H12)
     ColorLUTEffectGuid = iid
    End Function
    Public Function BrightnessContrastEffectGuid() As UUID
    '{D3A1DBE1-8EC4-4c17-9F4C-EA97AD1C343D}
    Static iid As UUID
     If (iid.Data1 = 0) Then Call DEFINE_UUID(iid, &HD3A1DBE1, CInt(&H8EC4), CInt(&H4c17), &H9F, &H4C, &HEA, &H97, &HAD, &H1C, &H34, &H3D)
     BrightnessContrastEffectGuid = iid
    End Function
    Public Function HueSaturationLightnessEffectGuid() As UUID
    '{8B2DD6C3-EB07-4d87-A5F0-7108E26A9C5F}
    Static iid As UUID
     If (iid.Data1 = 0) Then Call DEFINE_UUID(iid, &H8B2DD6C3, CInt(&HEB07), CInt(&H4d87), &HA5, &HF0, &H71, &H08, &HE2, &H6A, &H9C, &H5F)
     HueSaturationLightnessEffectGuid = iid
    End Function
    Public Function LevelsEffectGuid() As UUID
    '{99C354EC-2A31-4f3a-8C34-17A803B33A25}
    Static iid As UUID
     If (iid.Data1 = 0) Then Call DEFINE_UUID(iid, &H99C354EC, CInt(&H2A31), CInt(&H4f3a), &H8C, &H34, &H17, &HA8, &H03, &HB3, &H3A, &H25)
     LevelsEffectGuid = iid
    End Function
    Public Function TintEffectGuid() As UUID
    '{1077AF00-2848-4441-9489-44AD4C2D7A2C}
    Static iid As UUID
     If (iid.Data1 = 0) Then Call DEFINE_UUID(iid, &H1077AF00, CInt(&H2848), CInt(&H4441), &H94, &H89, &H44, &HAD, &H4C, &H2D, &H7A, &H2C)
     TintEffectGuid = iid
    End Function
    Public Function ColorBalanceEffectGuid() As UUID
    '{537E597D-251E-48da-9664-29CA496B70F8}
    Static iid As UUID
     If (iid.Data1 = 0) Then Call DEFINE_UUID(iid, &H537E597D, CInt(&H251E), CInt(&H48da), &H96, &H64, &H29, &HCA, &H49, &H6B, &H70, &HF8)
     ColorBalanceEffectGuid = iid
    End Function
    Public Function RedEyeCorrectionEffectGuid() As UUID
    '{74D29D05-69A4-4266-9549-3CC52836B632}
    Static iid As UUID
     If (iid.Data1 = 0) Then Call DEFINE_UUID(iid, &H74D29D05, CInt(&H69A4), CInt(&H4266), &H95, &H49, &H3C, &HC5, &H28, &H36, &HB6, &H32)
     RedEyeCorrectionEffectGuid = iid
    End Function
    Public Function ColorCurveEffectGuid() As UUID
    '{DD6A0022-58E4-4a67-9D9B-D48EB881A53D}
    Static iid As UUID
     If (iid.Data1 = 0) Then Call DEFINE_UUID(iid, &HDD6A0022, CInt(&H58E4), CInt(&H4a67), &H9D, &H9B, &HD4, &H8E, &HB8, &H81, &HA5, &H3D)
     ColorCurveEffectGuid = iid
    End Function
    
    Public Declare PtrSafe Function GdipCreateEffect Lib "gdiplus" (ByVal guid As UUID, effect As GpEffect) As GpStatus
    Public Declare PtrSafe Function GdipDeleteEffect Lib "gdiplus" (ByVal Me As GpEffect) As GpStatus
    Public Declare PtrSafe Function GdipGetEffectParameterSize Lib "gdiplus" (ByVal Me As GpEffect, size As Long) As GpStatus
    Public Declare PtrSafe Function GdipSetEffectParameters Lib "gdiplus" (ByVal Me As GpEffect, params As Any, ByVal size As Long) As GpStatus
    Public Declare PtrSafe Function GdipGetEffectParameters Lib "gdiplus" (ByVal Me As GpEffect, size As Long, params As Any) As GpStatus
End Module
