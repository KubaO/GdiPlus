' A part of the GDI+ Package for TwinBasic  https://github.com/kubao/GdiPlus
' Licensed under the MIT license. (c) 2026 Sunderland Ober Consulting.
' "Windows" is a trademark of the Microsoft Corporation.
' Modeled after code and descriptions (c) Microsoft, taken from SDK headers and official documentation.
' Certain flat API function and type declarations taken from WinDevLib by fafalone, with thanks.

[COMCreatable(False)]
Class LineCap
    Inherits GdiPlusBase
    Protected lc As GpCustomLineCap
        
    ' Used by Pen
    Public Sub New(ByVal nativeLineCap As GpCustomLineCap)
        LastStatus = Ok
        SetNativeLineCap(nativeLineCap)
    End Sub
        
    Sub New(fillPath As GraphicsPath, strokePath As GraphicsPath, ByVal baseCap As GpLineCap = linecapflat, ByVal baseInset& = 0)
        Dim nativeCap As GpCustomLineCap
        LastStatus = GdipCreateCustomLineCap(GetNative(fillPath), GetNative(strokePath), baseCap, baseInset, nativeCap)
        SetNativeLineCap(nativeCap)
    End Sub
    
    Overridable Sub Class_Terminate()
        GdipDeleteCustomLineCap(lc)
    End Sub
    
    Function Clone() As LineCap
        Dim newNativeLineCap As GpCustomLineCap
        SetStatus(GdipCloneCustomLineCap(lc, newNativeLineCap))
        If LastResult = Ok Then Set Clone = New LineCap(newNativeLineCap, LastResult)
    End Function
    
    ' This changes both the start and end cap.
    Property Let StrokeCap(ByVal strokeCap As GpLineCap)
        SetStrokeCaps(strokeCap, strokeCap)
    End Property
    
    Property Let StartStrokeCap(ByVal startCap As GpLineCap)
        Dim dummyCap As GpLineCap
        Dim endCap As GpLineCap
        SetStatus(GdipGetCustomLineCapStrokeCaps(lc, dummyCap, endCap))
        If LastResult = Ok Then
            SetStatus(GdipSetCustomLineCapStrokeCaps(lc, startCap, endCap))
        End If
    End Property
    
    Property Let EndStrokeCap(ByVal endCap As GpLineCap)
        Dim startCap As GpLineCap
        Dim dummyCap As GpLineCap
        SetStatus(GdipGetCustomLineCapStrokeCaps(lc, startCap, dummyCap))
        If LastResult = Ok Then
            SetStatus(GdipSetCustomLineCapStrokeCaps(lc, startCap, endCap))
        End If
    End Property

    Property Get StartStrokeCap() As GpLineCap
        Dim dummyCap As GpLineCap
        SetStatus(GdipGetCustomLineCapStrokeCaps(lc, StartStrokeCap, dummyCap))
    End Property

    Property Get EndStrokeCap() As GpLineCap
        Dim dummyCap As GpLineCap
        SetStatus(GdipGetCustomLineCapStrokeCaps(lc, dummyCap, EndStrokeCap))
    End Property
    
    Function SetStrokeCaps(ByVal startCap As GpLineCap, ByVal endCap As GpLineCap) As GpStatus
        Return SetStatus(GdipSetCustomLineCapStrokeCaps(lc, startCap, endCap))
    End Function
    
    Function GetStrokeCaps(startCap As GpLineCap, endCap As GpLineCap) As GpStatus
        Return SetStatus(GdipGetCustomLineCapStrokeCaps(lc, startCap, endCap))
    End Function

    Property Let StrokeJoin(ByVal lineJoin As GpLineJoin)
        SetStatus(GdipSetCustomLineCapStrokeJoin(lc, lineJoin))
    End Property
    
    Property Get StrokeJoin() As GpLineJoin
        SetStatus(GdipGetCustomLineCapStrokeJoin(lc, StrokeJoin))
    End Property
    
    Property Let BaseCap(ByVal baseCap As GpLineCap)
        SetStatus(GdipSetCustomLineCapBaseCap(lc, baseCap))
    End Property
    
    Property Get BaseCap() As GpLineCap
        SetStatus(GdipGetCustomLineCapBaseCap(lc, BaseCap))
    End Property
        
    Property Let BaseInset(ByVal inset!)
        SetStatus(GdipSetCustomLineCapBaseInset(lc, inset))
    End Property
    
    Property Get BaseInset() As Single
        Return SetStatus(GdipGetCustomLineCapBaseInset(lc, BaseInset))
    End Property
        
    Property Let WidthScale(ByVal widthScale!)
        SetStatus(GdipSetCustomLineCapWidthScale(lc, widthScale))
    End Property
    
    Property Get WidthScale() As Single
        SetStatus(GdipGetCustomLineCapWidthScale(lc, WidthScale))
    End Property
   
    Protected Sub New()
        lc.Native = vbNullPtr
    End Sub
    
    Protected Sub New(ByVal nativeCapArg As GpCustomLineCap, ByVal status As GpStatus)
        LastStatus = status
        SetNativeLineCap(nativeCapArg)
    End Sub
    
    Protected Property Get Native() As GpCustomLineCap
        Return lc
    End Property
    
    Protected Sub SetNativeLineCap(ByVal nativeLineCap As GpCustomLineCap)
        lc = nativeLineCap
    End Sub
End Class

Alias CustomLineCap As LineCap

Module GdipLineCap
    Function LineCap(fillPath As GraphicsPath, strokePath As GraphicsPath, ByVal baseCap As GpLineCap = linecapflat, ByVal baseInset& = 0) As LineCap
        Return New LineCap(fillPath, strokePath, baseCap, baseInset)
    End Function
    
    Type GpCustomLineCap
        Native As LongPtr
    End Type
    
    Function GpCustomLineCap() As GpCustomLineCap
        GpCustomLineCap.Native = 0
    End Function
    
    Public Declare PtrSafe Function GdipCreateCustomLineCap Lib "gdiplus" (ByVal fillPath As GpPath, ByVal strokePath As GpPath, ByVal baseCap As GpLineCap, ByVal baseInset As Single, customCap As GpCustomLineCap) As GpStatus
    Public Declare PtrSafe Function GdipDeleteCustomLineCap Lib "gdiplus" (ByVal Me As GpCustomLineCap) As GpStatus
    Public Declare PtrSafe Function GdipCloneCustomLineCap Lib "gdiplus" (ByVal Me As GpCustomLineCap, clonedCap As GpCustomLineCap) As GpStatus
    Public Declare PtrSafe Function GdipGetCustomLineCapType Lib "gdiplus" (ByVal Me As GpCustomLineCap, capType As GpCustomLineCapType) As GpStatus
    Public Declare PtrSafe Function GdipSetCustomLineCapStrokeCaps Lib "gdiplus" (ByVal Me As GpCustomLineCap, ByVal startCap As GpLineCap, ByVal endCap As GpLineCap) As GpStatus
    Public Declare PtrSafe Function GdipGetCustomLineCapStrokeCaps Lib "gdiplus" (ByVal Me As GpCustomLineCap, startCap As GpLineCap, endCap As GpLineCap) As GpStatus
    Public Declare PtrSafe Function GdipSetCustomLineCapStrokeJoin Lib "gdiplus" (ByVal Me As GpCustomLineCap, ByVal lineJoin As GpLineJoin) As GpStatus
    Public Declare PtrSafe Function GdipGetCustomLineCapStrokeJoin Lib "gdiplus" (ByVal Me As GpCustomLineCap, lineJoin As GpLineJoin) As GpStatus
    Public Declare PtrSafe Function GdipSetCustomLineCapBaseCap Lib "gdiplus" (ByVal Me As GpCustomLineCap, ByVal baseCap As GpLineCap) As GpStatus
    Public Declare PtrSafe Function GdipGetCustomLineCapBaseCap Lib "gdiplus" (ByVal Me As GpCustomLineCap, baseCap As GpLineCap) As GpStatus
    Public Declare PtrSafe Function GdipSetCustomLineCapBaseInset Lib "gdiplus" (ByVal Me As GpCustomLineCap, ByVal inset As Single) As GpStatus
    Public Declare PtrSafe Function GdipGetCustomLineCapBaseInset Lib "gdiplus" (ByVal Me As GpCustomLineCap, inset As Single) As GpStatus
    Public Declare PtrSafe Function GdipSetCustomLineCapWidthScale Lib "gdiplus" (ByVal Me As GpCustomLineCap, ByVal widthScale As Single) As GpStatus
    Public Declare PtrSafe Function GdipGetCustomLineCapWidthScale Lib "gdiplus" (ByVal Me As GpCustomLineCap, widthScale As Single) As GpStatus
End Module
