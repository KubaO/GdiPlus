' A part of the GDI+ Package for TwinBasic  https://github.com/kubao/GdiPlus
' Licensed under the MIT license. (c) 2026 Sunderland Ober Consulting.
' "Windows" is a trademark of the Microsoft Corporation.
' Modeled after code and descriptions (c) Microsoft, taken from SDK headers and official documentation.
' Certain flat API function and type declarations taken from WinDevLib by fafalone, with thanks.

[COMCreatable(False)]
Class LineCap
    Inherits GdiPlusBase
    Protected lc As GpCustomLineCap
        
    ' Used by Pen
    Public Sub New(ByVal nativeLineCap As GpCustomLineCap)
        SetNativeLineCap(nativeLineCap)
    End Sub
        
    Sub New(fillPath As GraphicsPath, strokePath As GraphicsPath, ByVal baseCap As GpLineCap = linecapflat, ByVal baseInset& = 0)
        SetStatus(lc.CreateInto(GetNative(fillPath), GetNative(strokePath), baseCap, baseInset, lc))
    End Sub
    
    Overridable Sub Class_Terminate()
        lc.Delete
    End Sub
    
    Function Clone() As LineCap
        Dim newNativeLineCap As GpCustomLineCap
        SetStatus(lc.Clone(newNativeLineCap))
        If LastResult = Ok Then Set Clone = New LineCap(newNativeLineCap, LastResult)
    End Function
    
    ' This changes both the start and end cap.
    Property Let StrokeCap(ByVal strokeCap As GpLineCap)
        SetStrokeCaps(strokeCap, strokeCap)
    End Property
    
    Property Let StartStrokeCap(ByVal startCap As GpLineCap)
        Dim dummyCap As GpLineCap
        Dim endCap As GpLineCap
        SetStatus(lc.GetStrokeCaps(dummyCap, endCap))
        If LastResult = Ok Then
            SetStatus(lc.SetStrokeCaps(startCap, endCap))
        End If
    End Property
    
    Property Let EndStrokeCap(ByVal endCap As GpLineCap)
        Dim startCap As GpLineCap
        Dim dummyCap As GpLineCap
        SetStatus(lc.GetStrokeCaps(startCap, dummyCap))
        If LastResult = Ok Then
            SetStatus(lc.SetStrokeCaps(startCap, endCap))
        End If
    End Property

    Property Get StartStrokeCap() As GpLineCap
        Dim dummyCap As GpLineCap
        SetStatus(lc.GetStrokeCaps(StartStrokeCap, dummyCap))
    End Property

    Property Get EndStrokeCap() As GpLineCap
        Dim dummyCap As GpLineCap
        SetStatus(lc.GetStrokeCaps(dummyCap, EndStrokeCap))
    End Property
    
    Function SetStrokeCaps(ByVal startCap As GpLineCap, ByVal endCap As GpLineCap) As GpStatus
        Return SetStatus(lc.SetStrokeCaps(startCap, endCap))
    End Function
    
    Function GetStrokeCaps(startCap As GpLineCap, endCap As GpLineCap) As GpStatus
        Return SetStatus(lc.GetStrokeCaps(startCap, endCap))
    End Function

    Property Let StrokeJoin(ByVal lineJoin As GpLineJoin)
        SetStatus(lc.SetStrokeJoin(lineJoin))
    End Property
    
    Property Get StrokeJoin() As GpLineJoin
        SetStatus(lc.GetStrokeJoin(StrokeJoin))
    End Property
    
    Property Let BaseCap(ByVal baseCap As GpLineCap)
        SetStatus(lc.SetBaseCap(baseCap))
    End Property
    
    Property Get BaseCap() As GpLineCap
        SetStatus(lc.GetBaseCap(BaseCap))
    End Property
        
    Property Let BaseInset(ByVal inset!)
        SetStatus(lc.SetBaseInset(inset))
    End Property
    
    Property Get BaseInset() As Single
        Return SetStatus(lc.GetBaseInset(BaseInset))
    End Property
        
    Property Let WidthScale(ByVal widthScale!)
        SetStatus(lc.SetWidthScale(widthScale))
    End Property
    
    Property Get WidthScale() As Single
        SetStatus(lc.GetWidthScale(WidthScale))
    End Property
   
    Protected Sub New()
        lc.Native = vbNullPtr
    End Sub
    
    Protected Sub New(ByVal nativeCapArg As GpCustomLineCap, ByVal status As GpStatus)
        SetStatus(status)
        SetNativeLineCap(nativeCapArg)
    End Sub
    
    Protected Property Get Native() As GpCustomLineCap
        Return lc
    End Property
    
    Protected Sub SetNativeLineCap(ByVal nativeLineCap As GpCustomLineCap)
        lc = nativeLineCap
    End Sub
End Class

Alias CustomLineCap As LineCap

Module GdipLineCap
    Function LineCap(fillPath As GraphicsPath, strokePath As GraphicsPath, ByVal baseCap As GpLineCap = linecapflat, ByVal baseInset& = 0) As LineCap
        Return New LineCap(fillPath, strokePath, baseCap, baseInset)
    End Function
    
    Type GpCustomLineCap
        Native As LongPtr
        
        [UseGetLastError(False)]
        Public Declare PtrSafe Function CreateInto Lib "gdiplus" Alias "GdipCreateCustomLineCap" (ByVal fillPath As GpPath, ByVal strokePath As GpPath, ByVal baseCap As GpLineCap, ByVal baseInset As Single, customCap As GpCustomLineCap) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function Delete Lib "gdiplus" Alias "GdipDeleteCustomLineCap" (ByVal Me As GpCustomLineCap) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function Clone Lib "gdiplus" Alias "GdipCloneCustomLineCap" (ByVal Me As GpCustomLineCap, clonedCap As GpCustomLineCap) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function GetType Lib "gdiplus" Alias "GdipGetCustomLineCapType" (ByVal Me As GpCustomLineCap, capType As GpCustomLineCapType) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function SetStrokeCaps Lib "gdiplus" Alias "GdipSetCustomLineCapStrokeCaps" (ByVal Me As GpCustomLineCap, ByVal startCap As GpLineCap, ByVal endCap As GpLineCap) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function GetStrokeCaps Lib "gdiplus" Alias "GdipGetCustomLineCapStrokeCaps" (ByVal Me As GpCustomLineCap, startCap As GpLineCap, endCap As GpLineCap) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function SetStrokeJoin Lib "gdiplus" Alias "GdipSetCustomLineCapStrokeJoin" (ByVal Me As GpCustomLineCap, ByVal lineJoin As GpLineJoin) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function GetStrokeJoin Lib "gdiplus" Alias "GdipGetCustomLineCapStrokeJoin" (ByVal Me As GpCustomLineCap, lineJoin As GpLineJoin) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function SetBaseCap Lib "gdiplus" Alias "GdipSetCustomLineCapBaseCap" (ByVal Me As GpCustomLineCap, ByVal baseCap As GpLineCap) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function GetBaseCap Lib "gdiplus" Alias "GdipGetCustomLineCapBaseCap" (ByVal Me As GpCustomLineCap, baseCap As GpLineCap) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function SetBaseInset Lib "gdiplus" Alias "GdipSetCustomLineCapBaseInset" (ByVal Me As GpCustomLineCap, ByVal inset As Single) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function GetBaseInset Lib "gdiplus" Alias "GdipGetCustomLineCapBaseInset" (ByVal Me As GpCustomLineCap, inset As Single) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function SetWidthScale Lib "gdiplus" Alias "GdipSetCustomLineCapWidthScale" (ByVal Me As GpCustomLineCap, ByVal widthScale As Single) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function GetWidthScale Lib "gdiplus" Alias "GdipGetCustomLineCapWidthScale" (ByVal Me As GpCustomLineCap, widthScale As Single) As GpStatus
    End Type
    
    Function GpCustomLineCap() As GpCustomLineCap
        GpCustomLineCap.Native = 0
    End Function
End Module
