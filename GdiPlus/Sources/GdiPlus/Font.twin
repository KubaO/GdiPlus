' Reviewed 2026-01-11
[COMCreatable(False)]
Class Font
    Inherits GdiPlusBase
    Dim ft As GpFont
    
    Sub New(ByVal hdc As LongPtr)
        Dim font As GpFont
        LastStatus = GdipCreateFontFromDC(hdc, font)
        SetNativeFont(font)
    End Sub

    Sub New(ByVal hdc As LongPtr, ByVal hfont As LongPtr)
        Dim font As GpFont
        If hfont <> 0 Then
            Dim lf As LOGFONTW

            If GetObjectW(hfont, LenB(Of LOGFONTW), lf) <> 0 Then
                LastStatus = GdipCreateFontFromLogfontW(hdc, lf, font)
            Else
                LastStatus = GdipCreateFontFromDC(hdc, font)
            End If
        Else
            LastStatus = GdipCreateFontFromDC(hdc, font)
        End If
        SetNativeFont(font)
    End Sub
    
    Sub New(ByVal hdc As LongPtr, logfont As LOGFONTA)
        Dim font As GpFont
        If VarPtr(logfont) <> 0 Then
            LastStatus = GdipCreateFontFromLogfontA(hdc, logfont, font)
        Else
            LastStatus = GdipCreateFontFromDC(hdc, font)
        End If
        SetNativeFont(font)
    End Sub

    Sub New(ByVal hdc As LongPtr, logfont As LOGFONTW)
        Dim font As GpFont
        If VarPtr(logfont) <> 0 Then
            LastStatus = GdipCreateFontFromLogfontW(hdc, logfont, font)
        Else
            LastStatus = GdipCreateFontFromDC(hdc, font)
        End If
        SetNativeFont(font)
    End Sub

    Sub New(family As FontFamily, ByVal emSize!, ByVal style As FontStyle = FontStyleRegular, _
            ByVal unit As GpUnit = UnitPoint)
        Dim font As GpFont
        LastStatus = GdipCreateFont(GetNative(family), emSize, style, unit, font)
        SetNativeFont(font)
    End Sub

    Sub New(ByVal familyName$, ByVal emSize!, ByVal style As FontStyle = FontStyleRegular, _
            ByVal unit As GpUnit = UnitPoint, Optional fontCollection As FontCollection)

        Dim family As FontFamily = New FontFamily(familyName, fontCollection)
        Dim nativeFamily As GpFontFamily = GetNative(family)

        LastStatus = family.GetLastResult()

        If LastResult <> Ok Then
            nativeFamily = GetNative(GenericSansSerif())
            LastStatus = GenericSansSerif().GetLastResult
            If LastResult <> Ok Then Exit Sub
        End If

        LastStatus = GdipCreateFont(nativeFamily, emSize, style, unit, ft)

        If LastResult <> Ok Then
            nativeFamily = GetNative(GenericSansSerif())
            LastStatus = GenericSansSerif().GetLastResult
            If LastResult <> Ok Then Exit Sub

            LastStatus = GdipCreateFont(nativeFamily, emSize, style, unit, ft)
        End If
    End Sub

    Function GetLogFontA(g As Graphics, logfonta As LOGFONTA) As GpStatus
        Return SetStatus(GdipGetLogFontA(ft, GetNative(g), logfonta))
    End Function
    
    Function GetLogFontW(g As Graphics, logfontw As LOGFONTW) As GpStatus
        Return SetStatus(GdipGetLogFontW(ft, GetNative(g), logfontw))
    End Function

    Function Clone() As Font
        Dim cloneFont As GpFont
        SetStatus(GdipCloneFont(ft, cloneFont))
        If LastResult = Ok Then Set Clone = New Font(cloneFont, LastResult)
    End Function
    
    Sub Class_Terminate()
        GdipDeleteFont(ft)
    End Sub

    Property Get IsAvailable() As Boolean
        Return ft.Native <> 0
    End Property
    
    Property Get Style() As FontStyle
        SetStatus(GdipGetFontStyle(ft, Style))
    End Property
    
    Property Get Size!()
        SetStatus(GdipGetFontSize(ft, Size))
    End Property
    
    Property Get Unit() As GpUnit
        SetStatus(GdipGetFontUnit(ft, Unit))
    End Property
    
    Property Get PtHeight!()
        Dim height!
        SetStatus(GdipGetFontHeightGivenDPI(ft, 1000, height))
        If LastResult = Ok Then PtHeight = height / (1000.0 / 72.0)
    End Property
    
    Function GetHeight!(graphics As Graphics)
        SetStatus(GdipGetFontHeight(ft, GetNative(graphics), GetHeight))
    End Function
    
    Function GetHeight!(ByVal dpi!)
        SetStatus(GdipGetFontHeightGivenDPI(ft, dpi, GetHeight))
    End Function
    
    Property Get Family() As FontFamily
        Dim nativeFamily As GpFontFamily
        GdipGetFamily(ft, nativeFamily)
        If LastStatus = Ok Then Set Family = New FontFamily(nativeFamily)
    End Property
        
    Protected Sub New(ByVal font As GpFont, ByVal status As GpStatus)
        LastStatus = status
        SetNativeFont(font)
    End Sub
    
    Protected Property Get Native() As GpFont
        Return ft
    End Property
    
    Protected Sub New(ByVal nativeFont As GpFont)
        LastStatus = Ok
        SetNativeFont(nativeFont)
    End Sub
    
    Protected Sub SetNativeFont(ByVal nativeFont As GpFont)
        ft = nativeFont
    End Sub
End Class

Module FontModule
    Type GpFont
        Native As LongPtr
    End Type
    
    Function GpFont() As GpFont
        GpFont.Native = 0
    End Function
    
    Public Declare PtrSafe Function GdipCreateFont Lib "gdiplus" (ByVal fontFamily As GpFontFamily, ByVal emSize As Single, ByVal style As Long, ByVal unit As GpUnit, font As GpFont) As GpStatus
    Public Declare PtrSafe Function GdipCreateFontFromDC Lib "gdiplus" (ByVal hdc As LongPtr, font As GpFont) As GpStatus
    Public Declare PtrSafe Function GdipCreateFontFromLogfontA Lib "gdiplus" (ByVal hdc As LongPtr, logfont As LOGFONTA, font As GpFont) As GpStatus
    Public Declare PtrSafe Function GdipCreateFontFromLogfontW Lib "gdiplus" (ByVal hdc As LongPtr, logfont As LOGFONTW, font As GpFont) As GpStatus
    Public Declare PtrSafe Function GdipCloneFont Lib "gdiplus" (ByVal Me As GpFont, cloneFont As GpFont) As GpStatus
    Public Declare PtrSafe Function GdipDeleteFont Lib "gdiplus" (ByVal Me As GpFont) As GpStatus
    Public Declare PtrSafe Function GdipGetLogFontA Lib "gdiplus" (ByVal Me As GpFont, ByVal Graphics As GpGraphics, logfontA As LOGFONTA) As GpStatus
    Public Declare PtrSafe Function GdipGetLogFontW Lib "gdiplus" (ByVal Me As GpFont, ByVal Graphics As GpGraphics, logfontA As LOGFONTW) As GpStatus
    
    Public Declare PtrSafe Function GdipGetFamily Lib "gdiplus" (ByVal Me As GpFont, family As GpFontFamily) As GpStatus
    Public Declare PtrSafe Function GdipGetFontStyle Lib "gdiplus" (ByVal Me As GpFont, style As Long) As GpStatus
    Public Declare PtrSafe Function GdipGetFontSize Lib "gdiplus" (ByVal Me As GpFont, size As Single) As GpStatus
    Public Declare PtrSafe Function GdipGetFontUnit Lib "gdiplus" (ByVal Me As GpFont, unit As GpUnit) As GpStatus
    Public Declare PtrSafe Function GdipGetFontHeight Lib "gdiplus" (ByVal Me As GpFont, ByVal Graphics As GpGraphics, height As Single) As GpStatus
    Public Declare PtrSafe Function GdipGetFontHeightGivenDPI Lib "gdiplus" (ByVal Me As GpFont, ByVal dpi As Single, height As Single) As GpStatus
End Module
