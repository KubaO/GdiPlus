' A part of the GDI+ Package for TwinBasic  https://github.com/kubao/GdiPlus
' Licensed under the MIT license. (c) 2026 Sunderland Ober Consulting.
' "Windows" is a trademark of the Microsoft Corporation.
' Modeled after code and descriptions (c) Microsoft, taken from SDK headers and official documentation.
' Certain flat API function and type declarations taken from WinDevLib by fafalone, with thanks.

[COMCreatable(False)]
Class Font
    Inherits GdiPlusBase
    Dim ft As GpFont
    Protected metrics As Metrics

    Sub New(ByVal hdc As LongPtr)
        Dim font As GpFont
        LastStatus = GdipCreateFontFromDC(hdc, font)
        SetNativeFont(font)
    End Sub

    Sub New(ByVal hdc As LongPtr, ByVal hfont As LongPtr)
        Dim font As GpFont
        If hfont <> 0 Then
            Dim lf As GDIP_LOGFONTW
            If GetObjectW(hfont, LenB(Of GDIP_LOGFONTW), lf) <> 0 Then
                LastStatus = GdipCreateFontFromLogfontW(hdc, lf, font)
            Else
                LastStatus = GdipCreateFontFromDC(hdc, font)
            End If
        Else
            LastStatus = GdipCreateFontFromDC(hdc, font)
        End If
        SetNativeFont(font)
    End Sub

    Sub New(ByVal hdc As LongPtr, logfont As GDIP_LOGFONTA)
        Dim font As GpFont
        If VarPtr(logfont) <> 0 Then
            LastStatus = GdipCreateFontFromLogfontA(hdc, logfont, font)
        Else
            LastStatus = GdipCreateFontFromDC(hdc, font)
        End If
        SetNativeFont(font)
    End Sub

    Sub New(ByVal hdc As LongPtr, logfont As GDIP_LOGFONTW)
        Dim font As GpFont
        If VarPtr(logfont) <> 0 Then
            LastStatus = GdipCreateFontFromLogfontW(hdc, logfont, font)
        Else
            LastStatus = GdipCreateFontFromDC(hdc, font)
        End If
        SetNativeFont(font)
    End Sub

    Sub New(family As FontFamily, ByVal emSize!, ByVal style As GpFontStyle = FontStyleRegular, _
            ByVal unit As GpUnit = UnitPoint)
        Dim font As GpFont
        LastStatus = GdipCreateFont(GetNative(family), emSize, style, unit, font)
        InitMetrics(GetNative(family))
        SetNativeFont(font)
    End Sub

    Sub New(ByVal familyName$, ByVal emSize!, ByVal style As GpFontStyle = FontStyleRegular, _
            ByVal unit As GpUnit = UnitPoint, Optional fontCollection As FontCollection)

        Dim nativeFamily As GpFontFamily
        LastStatus = GdipCreateFontFamilyFromName(familyName, GetNative(fontCollection), nativeFamily)

        If LastResult <> Ok Then
            nativeFamily = GetNative(GenericSansSerif())
            LastStatus = GenericSansSerif().GetLastResult
            If LastResult <> Ok Then GoTo Error
        End If

        LastStatus = GdipCreateFont(nativeFamily, emSize, style, unit, ft)

        If LastResult <> Ok Then
            nativeFamily = GetNative(GenericSansSerif())
            LastStatus = GenericSansSerif().GetLastResult
            If LastResult <> Ok Then GoTo Error

            LastStatus = GdipCreateFont(nativeFamily, emSize, style, unit, ft)
        End If
        
        If LastResult = Ok Then InitMetrics(nativeFamily)
        
        Error:
        GdipDeleteFontFamily(nativeFamily)
    End Sub

    Function GetLogFontA(g As Graphics) As GDIP_LOGFONTA
        SetStatus(GdipGetLogFontA(ft, GetNative(g), GetLogFontA))
    End Function
    
    Function GetLogFontW(g As Graphics) As GDIP_LOGFONTW
        SetStatus(GdipGetLogFontW(ft, GetNative(g), GetLogFontW))
    End Function
    
    Function GetLogFontA(g As Graphics, logfonta As GDIP_LOGFONTA) As GpStatus
        Return SetStatus(GdipGetLogFontA(ft, GetNative(g), logfonta))
    End Function
    
    Function GetLogFontW(g As Graphics, logfontw As GDIP_LOGFONTW) As GpStatus
        Return SetStatus(GdipGetLogFontW(ft, GetNative(g), logfontw))
    End Function

    Function Clone() As Font
        Dim cloneFont As GpFont
        SetStatus(GdipCloneFont(ft, cloneFont))
        If LastResult = Ok Then
            Set Clone = New Font(cloneFont, LastResult)
            CloneMetricsTo(Clone)
        End If
    End Function
    
    Sub Class_Terminate()
        GdipDeleteFont(ft)
    End Sub

    Property Get IsAvailable() As Boolean
        Return ft.Native <> 0
    End Property
    
    Property Get Style() As GpFontStyle
        SetStatus(GdipGetFontStyle(ft, Style))
    End Property
    
    Property Get Size() As Single
        SetStatus(GdipGetFontSize(ft, Size))
    End Property
    
    Property Get Unit() As GpUnit
        SetStatus(GdipGetFontUnit(ft, Unit))
    End Property
       
    Function GetHeight(graphics As Graphics) As Single
        SetStatus(GdipGetFontHeight(ft, GetNative(graphics), GetHeight))
    End Function
    
    Function GetHeight(ByVal dpi!) As Single
        SetStatus(GdipGetFontHeightGivenDPI(ft, dpi, GetHeight))
    End Function
    
    Property Get Family() As FontFamily
        Dim nativeFamily As GpFontFamily
        SetStatus(GdipGetFamily(ft, nativeFamily))
        If LastStatus = Ok Then Set Family = New FontFamily(nativeFamily)
    End Property
    
    Property Get Ascent() As Single
        If InitMetrics Then
            Ascent = Size * metrics.ascent / metrics.emHeight
        End If
    End Property

    Property Get Descent() As Single
        If InitMetrics Then
            Descent = Size * metrics.descent / metrics.emHeight
        End If
    End Property

    Property Get LineSpacing() As Single
        If InitMetrics Then
            LineSpacing = Size * metrics.lineSpacing / metrics.emHeight
        End If
    End Property
    
    Protected Sub New(ByVal font As GpFont, ByVal status As GpStatus)
        LastStatus = status
        SetNativeFont(font)
    End Sub
    
    Protected Property Get Native() As GpFont
        Return ft
    End Property
    
    Protected Sub New(ByVal nativeFont As GpFont)
        LastStatus = Ok
        SetNativeFont(nativeFont)
    End Sub
    
    Protected Sub SetNativeFont(ByVal nativeFont As GpFont)
        ft = nativeFont
    End Sub

    Protected Type Metrics
        emHeight%
        ascent%
        descent%
        lineSpacing%
    End Type
    
    Protected Function InitMetrics() As Boolean
        InitMetrics = True
        If metrics.emHeight < 1 Then
            Dim nativeFamily As GpFontFamily
            SetStatus(GdipGetFamily(ft, nativeFamily))
            InitMetrics = InitMetrics(nativeFamily)
            GdipDeleteFontFamily(nativeFamily)
        End If
    End Function
    
    Protected Function InitMetrics(ByVal nativeFamily As GpFontFamily) As Boolean
        InitMetrics = True
        If metrics.emHeight < 1 Then
            Dim myStyle As GpFontStyle = Style
            SetStatus(GdipGetEmHeight(nativeFamily, myStyle, metrics.emHeight))
            SetStatus(GdipGetCellAscent(nativeFamily, myStyle, metrics.ascent))
            SetStatus(GdipGetCellDescent(nativeFamily, myStyle, metrics.descent))
            SetStatus(GdipGetLineSpacing(nativeFamily, myStyle, metrics.lineSpacing))
            InitMetrics = LastStatus = Ok
        End If
        Return True
    End Function
    
    Protected Sub CloneMetricsTo(other As Font)
        If metrics.emHeight > 0 Then
            UnprotectedAccess(other).metrics = metrics
        End If
    End Sub
    
    Private DeclareWide PtrSafe Function GetObjectW Lib "gdi32" (ByVal hObject As LongPtr, ByVal nCount As Long, lpObject As Any) As Long
End Class

Module GdipFont
    Function Font(ByVal hdc As LongPtr) As Font
        Return New Font(hdc)
    End Function
    
    Function Font(ByVal hdc As LongPtr, ByVal hfont As LongPtr) As Font
        Return New Font(hdc, hfont)
    End Function
    
    Function Font(ByVal hdc As LongPtr, logfont As GDIP_LOGFONTA) As Font
        Return New Font(hdc, logfont)
    End Function
    
    Function Font(ByVal hdc As LongPtr, logfont As GDIP_LOGFONTW) As Font
        Return New Font(hdc, logfont)
    End Function
    
    Function Font(family As FontFamily, ByVal emSize!, ByVal style As GpFontStyle = FontStyleRegular, _
                ByVal unit As GpUnit = UnitPoint) As Font
        Return New Font(family, emSize, style, unit)
    End Function
    
    Function Font(family As FontFamily, ByVal emSize!, ByVal style As GpFontStyle = FontStyleRegular, _
                ByVal unit As GpUnit = UnitPoint, Optional fontCollection As FontCollection) As Font
        Return New Font(family, emSize, style, unit, fontCollection)
    End Function
    
    Function Font(ByVal familyName$, ByVal emSize!, ByVal style As GpFontStyle = FontStyleRegular, _
                ByVal unit As GpUnit = UnitPoint, Optional fontCollection As FontCollection) As Font
        Return New Font(familyName, emSize, style, unit, fontCollection)
    End Function
    
    Type GpFont
        Native As LongPtr
    End Type
    
    Function GpFont() As GpFont
        GpFont.Native = 0
    End Function
    
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipCreateFont Lib "gdiplus" (ByVal fontFamily As GpFontFamily, ByVal emSize As Single, ByVal style As Long, ByVal unit As GpUnit, font As GpFont) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipCreateFontFromDC Lib "gdiplus" (ByVal hdc As LongPtr, font As GpFont) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipCreateFontFromLogfontA Lib "gdiplus" (ByVal hdc As LongPtr, logfont As GDIP_LOGFONTA, font As GpFont) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipCreateFontFromLogfontW Lib "gdiplus" (ByVal hdc As LongPtr, logfont As GDIP_LOGFONTW, font As GpFont) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipCloneFont Lib "gdiplus" (ByVal Me As GpFont, cloneFont As GpFont) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipDeleteFont Lib "gdiplus" (ByVal Me As GpFont) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipGetLogFontA Lib "gdiplus" (ByVal Me As GpFont, ByVal Graphics As GpGraphics, logfontA As GDIP_LOGFONTA) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipGetLogFontW Lib "gdiplus" (ByVal Me As GpFont, ByVal Graphics As GpGraphics, logfontA As GDIP_LOGFONTW) As GpStatus
    
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipGetFamily Lib "gdiplus" (ByVal Me As GpFont, family As GpFontFamily) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipGetFontStyle Lib "gdiplus" (ByVal Me As GpFont, style As Long) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipGetFontSize Lib "gdiplus" (ByVal Me As GpFont, size As Single) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipGetFontUnit Lib "gdiplus" (ByVal Me As GpFont, unit As GpUnit) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipGetFontHeight Lib "gdiplus" (ByVal Me As GpFont, ByVal Graphics As GpGraphics, height As Single) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipGetFontHeightGivenDPI Lib "gdiplus" (ByVal Me As GpFont, ByVal dpi As Single, height As Single) As GpStatus
End Module
