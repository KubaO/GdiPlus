' A part of the GDI+ Package for TwinBasic  https://github.com/kubao/GdiPlus
' Licensed under the MIT license. (c) 2026 Sunderland Ober Consulting.
' "Windows" is a trademark of the Microsoft Corporation.
' Modeled after code and descriptions (c) Microsoft, taken from SDK headers and official documentation.
' Certain flat API function and type declarations taken from WinDevLib by fafalone, with thanks.

[COMCreatable(False)]
Class LinearGradientBrush
    Inherits Brush
    
    Sub New(rect As GpRectF, ByVal rgb1&, ByVal rgb2&)
        Dim brush As GpLineGradient
        LastStatus = GdipCreateLineBrush(rect.TopLeft, rect.BottomRight, _
                FromCOLORREF(rgb1), FromCOLORREF(rgb2), WrapModeTile, brush)
        SetNativeLineGradient(brush)
    End Sub
    
    Sub New(rect As GpRectF, ByVal color1 As Color, ByVal color2 As Color)
        Dim brush As GpLineGradient
        LastStatus = GdipCreateLineBrush(rect.TopLeft, rect.BottomRight, color1, color2, WrapModeTile, brush)
        SetNativeLineGradient(brush)
    End Sub
    
    Sub New(point1 As GpPointF, point2 As GpPointF, ByVal rgb1&, ByVal rgb2&)
        Dim brush As GpLineGradient
        LastStatus = GdipCreateLineBrush(point1, point2, FromCOLORREF(rgb1), FromCOLORREF(rgb2), WrapModeTile, brush)
        SetNativeLineGradient(brush)
    End Sub
    
    Sub New(point1 As GpPointF, point2 As GpPointF, ByVal color1 As Color, ByVal color2 As Color)
        Dim brush As GpLineGradient
        LastStatus = GdipCreateLineBrush(point1, point2, color1, color2, WrapModeTile, brush)
        SetNativeLineGradient(brush)
    End Sub
    
    Sub New(point1 As GpPoint, point2 As GpPoint, ByVal rgb1&, ByVal rgb2&)
        Dim brush As GpLineGradient
        LastStatus = GdipCreateLineBrushI(point1, point2, FromCOLORREF(rgb1), FromCOLORREF(rgb2), WrapModeTile, brush)
        SetNativeLineGradient(brush)
    End Sub
    
    Sub New(point1 As GpPoint, point2 As GpPoint, ByVal color1 As Color, ByVal color2 As Color)
        Dim brush As GpLineGradient
        LastStatus = GdipCreateLineBrushI(point1, point2, color1, color2, WrapModeTile, brush)
        SetNativeLineGradient(brush)
    End Sub
    
    Sub New(rect As GpRectF, ByVal rgb1&, ByVal rgb2&, ByVal mode As GpLinearGradientMode)
        Dim brush As GpLineGradient
        LastStatus = GdipCreateLineBrushFromRect(rect, FromCOLORREF(rgb1), FromCOLORREF(rgb2), mode, WrapModeTile, brush)
        SetNativeLineGradient(brush)
    End Sub
    
    Sub New(rect As GpRectF, ByVal color1 As Color, ByVal color2 As Color, ByVal mode As GpLinearGradientMode)
        Dim brush As GpLineGradient
        LastStatus = GdipCreateLineBrushFromRect(rect, color1, color2, mode, WrapModeTile, brush)
        SetNativeLineGradient(brush)
    End Sub

    Sub New(rect As GpRect, ByVal rgb1&, ByVal rgb2&, ByVal mode As GpLinearGradientMode)
        Dim brush As GpLineGradient
        LastStatus = GdipCreateLineBrushFromRectI(rect, FromCOLORREF(rgb1), FromCOLORREF(rgb2), mode, WrapModeTile, brush)
        SetNativeLineGradient(brush)
    End Sub
    
    Sub New(rect As GpRect, ByVal color1 As Color, ByVal color2 As Color, ByVal mode As GpLinearGradientMode)
        Dim brush As GpLineGradient
        LastStatus = GdipCreateLineBrushFromRectI(rect, color1, color2, mode, WrapModeTile, brush)
        SetNativeLineGradient(brush)
    End Sub
    
    Sub New(rect As GpRectF, ByVal rgb1&, ByVal rgb2&, ByVal angle!, ByVal isAngleScalable As Boolean = False)
        Dim brush As GpLineGradient
        LastStatus = GdipCreateLineBrushFromRectWithAngle(rect, FromCOLORREF(rgb1), FromCOLORREF(rgb2), _
                angle, isAngleScalable, WrapModeTile, brush)
        SetNativeLineGradient(brush)
    End Sub
    
    Sub New(rect As GpRectF, ByVal color1 As Color, ByVal color2 As Color, ByVal angle!, ByVal isAngleScalable As Boolean = False)
        Dim brush As GpLineGradient
        LastStatus = GdipCreateLineBrushFromRectWithAngle(rect, color1, color2, angle, isAngleScalable, WrapModeTile, brush)
        SetNativeLineGradient(brush)
    End Sub
    
    Sub New(rect As GpRect, ByVal rgb1&, ByVal rgb2&, ByVal angle!, ByVal isAngleScalable As Boolean = False)
        Dim brush As GpLineGradient
        LastStatus = GdipCreateLineBrushFromRectWithAngleI(rect, FromCOLORREF(rgb1), FromCOLORREF(rgb2), _
                angle, isAngleScalable, WrapModeTile, brush)
        SetNativeLineGradient(brush)
    End Sub
    
    Sub New(rect As GpRect, ByVal color1 As Color, ByVal color2 As Color, ByVal angle!, ByVal isAngleScalable As Boolean = False)
        Dim brush As GpLineGradient
        LastStatus = GdipCreateLineBrushFromRectWithAngleI(rect, color1, color2, angle, isAngleScalable, WrapModeTile, brush)
        SetNativeLineGradient(brush)
    End Sub
    
    Function SetLinearColors(ByVal rgb1&, ByVal rgb2&) As GpStatus
        Return SetStatus(GdipSetLineColors(br, FromCOLORREF(rgb1), FromCOLORREF(rgb2)))
    End Function
    
    Function SetLinearColors(ByVal color1 As Color, ByVal color2 As Color) As GpStatus
        Return SetStatus(GdipSetLineColors(br, color1, color2))
    End Function
    
    Property Get LinearRGBs() As Long()
        Dim result(2) As Long
        SetStatus(GdipGetLineColors(br, result(1)))
        If LastStatus = Ok Then LinearRGBs = result
    End Property
    
    Property Get LinearColors() As Color()
        Dim result(2) As Color
        SetStatus(GdipGetLineColors(br, result(1)))
        If LastStatus = Ok Then LinearColors = result
    End Property
        
    Property Get Rectangle() As GpRectF
        SetStatus(GdipGetLineRect(br, Rectangle))
    End Property
        
    Property Get RectangleI() As GpRect
        SetStatus(GdipGetLineRectI(br, RectangleI))
    End Property
    
    Property Let GammaCorrection(ByVal useGammaCorrection As Boolean)
        SetStatus(GdipSetLineGammaCorrection(br, useGammaCorrection))
    End Property

    Property Get GammaCorrection() As Boolean
        Dim useGammaCorrection As BOOL
        SetStatus(GdipGetLineGammaCorrection(br, useGammaCorrection))
        Return useGammaCorrection
    End Property

    Property Get BlendCount&()
        SetStatus(GdipGetLineBlendCount(br, BlendCount))
    End Property
    
    Function SetBlend(blendFactors!(), blendPositions!()) As GpStatus
        Dim count& = Len(blendFactors)
        If Len(blendPositions) <> count Then Return SetStatus(InvalidParameter)
        Return SetStatus(GdipSetLineBlend(br, _
            blendFactors(LBound(blendFactors)), blendPositions(LBound(blendPositions)), count))
    End Function
    
    Function SetBlend(blendFactors!, blendPositions!, ByVal count&) As GpStatus
        Return SetStatus(GdipSetLineBlend(br, blendFactors, blendPositions, count))
    End Function
    
    Function GetBlend(blendFactors!(), blendPositions!()) As GpStatus
        Dim count& = BlendCount
        ReDim blendFactors(1 To count)
        ReDim blendPositions(1 To count)
        Return SetStatus(GdipSetLineBlend(br, blendFactors(1), blendPositions(1), count))
    End Function
    
    Function GetBlend(blendFactors!, blendPositions!, ByVal count&) As GpStatus
        Return SetStatus(GdipGetLineBlend(br, blendFactors, blendPositions, count))
    End Function
    
    Property Get InterpolationColorCount&()
        SetStatus(GdipGetLinePresetBlendCount(br, InterpolationColorCount&))
    End Property
    
    Function SetInterpolationColors(presetColors() As Color, blendPositions!()) As GpStatus
        Dim count& = Len(presetColors)
        If count <> Len(blendPositions) Or count = 0 Then Return SetStatus(InvalidParameter)
        Return SetStatus(GdipSetLinePresetBlend(br, _
            presetColors(LBound(presetColors)), blendPositions(LBound(blendPositions)), count))
    End Function
        
    Function SetInterpolationColors(presetColors As Color, blendPositions!, ByVal count&) As GpStatus
        If count <= 0 OrElse VarPtr(presetColors) = vbNullPtr Then
            Return SetStatus(InvalidParameter)
        End If
        Return SetStatus(GdipSetLinePresetBlend(br, presetColors, blendPositions, count))
    End Function
    
    Function GetInterpolationColors(presetColors() As Color, blendPositions!()) As GpStatus
        Dim count& = InterpolationColorCount
        ReDim presetColors(1 To count)
        ReDim blendPositions(1 To count)
        Return SetStatus(GdipGetLinePresetBlend(br, presetColors(1), blendPositions(1), count))
    End Function
    
    Function GetInterpolationColors(presetColors As Color, blendPositions!, ByVal count&) As GpStatus
        If count <= 0 OrElse VarPtr(presetColors) = vbNullPtr Then
            Return SetStatus(InvalidParameter)
        End If
        Return SetStatus(GdipGetLinePresetBlend(br, presetColors, blendPositions, count))
    End Function
    
    Function SetBlendBellShape(ByVal focus!, ByVal scale! = 1.0!) As GpStatus
        Return SetStatus(GdipSetLineSigmaBlend(br, focus, scale))
    End Function
    
    Function SetBlendTriangularShape(ByVal focus!, ByVal scale! = 1.0!) As GpStatus
        Return SetStatus(GdipSetLineLinearBlend(br, focus, scale))
    End Function
    
    Property Let Transform(matrix As Matrix)
        SetStatus(GdipSetLineTransform(br, GetNative(matrix)))
    End Property
    
    Property Get Transform() As Matrix
        Dim nativeMatrix As GpMatrix
        SetStatus(GdipGetLineTransform(br, nativeMatrix))
        If LastStatus = Ok Then Set Transform = New Matrix(nativeMatrix)
    End Property
        
    Function ResetTransform() As GpStatus
        Return SetStatus(GdipResetLineTransform(br))
    End Function
    
    Function MultiplyTransform(matrix As Matrix, ByVal order As GpMatrixOrder = MatrixOrderPrepend) As GpStatus
        Return SetStatus(GdipMultiplyLineTransform(br, GetNative(matrix), order))
    End Function
    
    Function TranslateTransform(ByVal dx!, ByVal dy!, ByVal order As GpMatrixOrder = MatrixOrderPrepend) As GpStatus
        Return SetStatus(GdipTranslateLineTransform(br, dx, dy, order))
    End Function
    
    Function ScaleTransform(ByVal sx!, ByVal sy!, ByVal order As GpMatrixOrder = MatrixOrderPrepend) As GpStatus
        Return SetStatus(GdipScaleLineTransform(br, sx, sy, order))
    End Function
    
    Function RotateTransform(ByVal angle!, ByVal order As GpMatrixOrder = MatrixOrderPrepend) As GpStatus
        Return SetStatus(GdipRotateLineTransform(br, angle, order))
    End Function
    
    Property Let WrapMode(ByVal wrapMode As GpWrapMode)
        SetStatus(GdipSetLineWrapMode(br, wrapMode))
    End Property
    
    Property Get WrapMode() As GpWrapMode
        SetStatus(GdipGetLineWrapMode(br, WrapMode))
    End Property
    
    Protected Sub New()
    End Sub
    
    ' Used by Pen
    Public Sub New(ByVal nativeGradient As GpLineGradient, ByVal status As GpStatus = Ok)
        LastStatus = status
        SetNativeLineGradient(nativeGradient)
    End Sub
    
    Protected Property Get Native() As GpLineGradient
        Native.Native = br.Native
    End Property
    
    Protected Sub SetNativeLineGradient(ByVal nativeGradient As GpLineGradient)
        br.Native = nativeGradient.Native
    End Sub
End Class

Module GdipLineGradient
    Function LinearGradientBrush(rect As GpRectF, ByVal rgb1&, ByVal rgb2&) As LinearGradientBrush
        Return New LinearGradientBrush(rect, rgb1, rgb2)
    End Function

    Function LinearGradientBrush(rect As GpRectF, ByVal color1 As Color, ByVal color2 As Color) As LinearGradientBrush
        Return New LinearGradientBrush(rect, color1, color2)
    End Function

    Function LinearGradientBrush(point1 As GpPointF, point2 As GpPointF, ByVal rgb1&, ByVal rgb2&) As LinearGradientBrush
        Return New LinearGradientBrush(point1, point2, rgb1, rgb2)
    End Function

    Function LinearGradientBrush(point1 As GpPointF, point2 As GpPointF, ByVal color1 As Color, ByVal color2 As Color) As LinearGradientBrush
        Return New LinearGradientBrush(point1, point2, color1, color2)
    End Function

    Function LinearGradientBrush(point1 As GpPoint, point2 As GpPoint, ByVal rgb1&, ByVal rgb2&) As LinearGradientBrush
        Return New LinearGradientBrush(point1, point2, rgb1, rgb2)
    End Function

    Function LinearGradientBrush(point1 As GpPoint, point2 As GpPoint, ByVal color1 As Color, ByVal color2 As Color) As LinearGradientBrush
        Return New LinearGradientBrush(point1, point2, color1, color2)
    End Function

    Function LinearGradientBrush(rect As GpRectF, ByVal rgb1&, ByVal rgb2&, ByVal mode As GpLinearGradientMode) As LinearGradientBrush
        Return New LinearGradientBrush(rect, rgb1, rgb2, mode)
    End Function

    Function LinearGradientBrush(rect As GpRectF, ByVal color1 As Color, ByVal color2 As Color, ByVal mode As GpLinearGradientMode) As LinearGradientBrush
        Return New LinearGradientBrush(rect, color1, color2, mode)
    End Function

    Function LinearGradientBrush(rect As GpRect, ByVal rgb1&, ByVal rgb2&, ByVal mode As GpLinearGradientMode) As LinearGradientBrush
        Return New LinearGradientBrush(rect, rgb1, rgb2, mode)
    End Function
    
    Function LinearGradientBrush(rect As GpRect, ByVal color1 As Color, ByVal color2 As Color, ByVal mode As GpLinearGradientMode) As LinearGradientBrush
        Return New LinearGradientBrush(rect, color1, color2, mode)
    End Function

    Function LinearGradientBrush(rect As GpRectF, ByVal rgb1&, ByVal rgb2&, _
            ByVal angle!, ByVal isAngleScalable As Boolean = False) As LinearGradientBrush
        Return New LinearGradientBrush(rect, rgb1&, rgb2&, angle, isAngleScalable)
    End Function

    Function LinearGradientBrush(rect As GpRectF, ByVal color1 As Color, ByVal color2 As Color, _
            ByVal angle!, ByVal isAngleScalable As Boolean = False) As LinearGradientBrush
        Return New LinearGradientBrush(rect, color1, color2, angle, isAngleScalable)
    End Function

    Function LinearGradientBrush(rect As GpRect, ByVal rgb1&, ByVal rgb2&, _
            ByVal angle!, ByVal isAngleScalable As Boolean = False) As LinearGradientBrush
        Return New LinearGradientBrush(rect, rgb1&, rgb2&, angle, isAngleScalable)
    End Function

    Function LinearGradientBrush(rect As GpRect, ByVal color1 As Color, ByVal color2 As Color, _
            ByVal angle!, ByVal isAngleScalable As Boolean = False) As LinearGradientBrush
        Return New LinearGradientBrush(rect, color1, color2, angle, isAngleScalable)
    End Function
    
    Type GpLineGradient
        'Inherits GpBrush
        Native As LongPtr
    End Type
    
    Function GpLineGradient() As GpLineGradient
        GpLineGradient.Native = 0
    End Function
    
    Function GpLineGradient(nativeBrush As GpBrush) As GpLineGradient
        GpLineGradient.Native = nativeBrush.Native
    End Function
    
    Public Declare PtrSafe Function GdipCreateLineBrush Lib "gdiplus" (point1 As GpPointF, point2 As GpPointF, ByVal color1 As Color, ByVal color2 As Color, ByVal wrapMode As GpWrapMode, lineGradient As GpLineGradient) As GpStatus
    Public Declare PtrSafe Function GdipCreateLineBrushI Lib "gdiplus" (point1 As GpPoint, point2 As GpPoint, ByVal color1 As Color, ByVal color2 As Color, ByVal wrapMode As GpWrapMode, lineGradient As GpLineGradient) As GpStatus
    Public Declare PtrSafe Function GdipCreateLineBrushFromRect Lib "gdiplus" (Rect As GpRectF, ByVal color1 As Color, ByVal color2 As Color, ByVal mode As GpLinearGradientMode, ByVal wrapMode As GpWrapMode, lineGradient As GpLineGradient) As GpStatus
    Public Declare PtrSafe Function GdipCreateLineBrushFromRectI Lib "gdiplus" (Rect As GpRect, ByVal color1 As Color, ByVal color2 As Color, ByVal mode As GpLinearGradientMode, ByVal wrapMode As GpWrapMode, lineGradient As GpLineGradient) As GpStatus
    Public Declare PtrSafe Function GdipCreateLineBrushFromRectWithAngle Lib "gdiplus" (Rect As GpRectF, ByVal color1 As Color, ByVal color2 As Color, ByVal angle!, ByVal isAngleScalable As BOOL, ByVal wrapMode As GpWrapMode, lineGradient As GpLineGradient) As GpStatus
    Public Declare PtrSafe Function GdipCreateLineBrushFromRectWithAngleI Lib "gdiplus" (Rect As GpRect, ByVal color1 As Color, ByVal color2 As Color, ByVal angle!, ByVal isAngleScalable As BOOL, ByVal wrapMode As GpWrapMode, lineGradient As GpLineGradient) As GpStatus
    
    Public Declare PtrSafe Function GdipSetLineColors Lib "gdiplus" (ByVal brush As GpBrush, ByVal color1 As Color, ByVal color2 As Color) As GpStatus
    Public Declare PtrSafe Function GdipGetLineColors Lib "gdiplus" (ByVal brush As GpBrush, rgbs As Long) As GpStatus
    Public Declare PtrSafe Function GdipGetLineColors Lib "gdiplus" (ByVal brush As GpBrush, colors As Color) As GpStatus
    Public Declare PtrSafe Function GdipGetLineRect Lib "gdiplus" (ByVal brush As GpBrush, rect As GpRectF) As GpStatus
    Public Declare PtrSafe Function GdipGetLineRectI Lib "gdiplus" (ByVal brush As GpBrush, rect As GpRect) As GpStatus
    Public Declare PtrSafe Function GdipSetLineGammaCorrection Lib "gdiplus" (ByVal brush As GpBrush, ByVal useGammaCorrection As BOOL) As GpStatus
    Public Declare PtrSafe Function GdipGetLineGammaCorrection Lib "gdiplus" (ByVal brush As GpBrush, useGammaCorrection As BOOL) As GpStatus
    Public Declare PtrSafe Function GdipGetLineBlendCount Lib "gdiplus" (ByVal brush As GpBrush, count&) As GpStatus
    Public Declare PtrSafe Function GdipSetLineBlend Lib "gdiplus" (ByVal brush As GpBrush, blend!, positions!, ByVal count&) As GpStatus
    Public Declare PtrSafe Function GdipGetLineBlend Lib "gdiplus" (ByVal brush As GpBrush, blend!, positions!, ByVal count&) As GpStatus
    
    Public Declare PtrSafe Function GdipGetLinePresetBlendCount Lib "gdiplus" (ByVal brush As GpBrush, count As Long) As GpStatus
    Public Declare PtrSafe Function GdipGetLinePresetBlend Lib "gdiplus" (ByVal brush As GpBrush, blend As Color, positions As Single, ByVal count As Long) As GpStatus
    Public Declare PtrSafe Function GdipSetLinePresetBlend Lib "gdiplus" (ByVal brush As GpBrush, blend As Color, positions As Single, ByVal count As Long) As GpStatus
    Public Declare PtrSafe Function GdipSetLineSigmaBlend Lib "gdiplus" (ByVal brush As GpBrush, ByVal focus As Single, ByVal scale As Single) As GpStatus
    Public Declare PtrSafe Function GdipSetLineLinearBlend Lib "gdiplus" (ByVal brush As GpBrush, ByVal focus As Single, ByVal scale As Single) As GpStatus
    Public Declare PtrSafe Function GdipSetLineWrapMode Lib "gdiplus" (ByVal brush As GpBrush, ByVal wrapMode As GpWrapMode) As GpStatus
    Public Declare PtrSafe Function GdipGetLineWrapMode Lib "gdiplus" (ByVal brush As GpBrush, wrapMode As GpWrapMode) As GpStatus
    Public Declare PtrSafe Function GdipSetLineTransform Lib "gdiplus" (ByVal brush As GpBrush, ByVal matrix As GpMatrix) As GpStatus
    Public Declare PtrSafe Function GdipGetLineTransform Lib "gdiplus" (ByVal brush As GpBrush, ByVal matrix As GpMatrix) As GpStatus
    Public Declare PtrSafe Function GdipResetLineTransform Lib "gdiplus" (ByVal brush As GpBrush) As GpStatus
    Public Declare PtrSafe Function GdipMultiplyLineTransform Lib "gdiplus" (ByVal brush As GpBrush, ByVal matrix As GpMatrix, ByVal order As GpMatrixOrder) As GpStatus
    Public Declare PtrSafe Function GdipTranslateLineTransform Lib "gdiplus" (ByVal brush As GpBrush, ByVal dx As Single, ByVal dy As Single, ByVal order As GpMatrixOrder) As GpStatus
    Public Declare PtrSafe Function GdipScaleLineTransform Lib "gdiplus" (ByVal brush As GpBrush, ByVal sx As Single, ByVal sy As Single, ByVal order As GpMatrixOrder) As GpStatus
    Public Declare PtrSafe Function GdipRotateLineTransform Lib "gdiplus" (ByVal brush As GpBrush, ByVal angle As Single, ByVal order As GpMatrixOrder) As GpStatus
End Module
