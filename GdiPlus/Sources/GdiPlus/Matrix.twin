' A part of the GDI+ Package for TwinBasic  https://github.com/kubao/GdiPlus
' Licensed under the MIT license. (c) 2026 Sunderland Ober Consulting.
' "Windows" is a trademark of the Microsoft Corporation.
' Modeled after code and descriptions (c) Microsoft, taken from SDK headers and official documentation.
' Certain flat API function and type declarations taken from WinDevLib by fafalone, with thanks.

[COMCreatable(False)]
Class Matrix
    Inherits GdiPlusBase
    Protected mx As GpMatrix
    
    Sub New(ByVal nativeMatrix As GpMatrix)
        LastStatus = Ok
        SetNativeMatrix(nativeMatrix)
    End Sub
        
    [Description("Become an identity matrix by default.")]
    Sub New()
        Dim nativeMatrix As GpMatrix
        LastStatus = GdipCreateMatrix(nativeMatrix)
        SetNativeMatrix(nativeMatrix)
    End Sub
    
    Sub New(ByVal m11!, ByVal m12!, ByVal m21!, ByVal m22!, ByVal dx!, ByVal dy!)
        Dim nativeMatrix As GpMatrix
        LastStatus = GdipCreateMatrix2(m11, m12, m21, m22, dx, dy, nativeMatrix)
        SetNativeMatrix(nativeMatrix)
    End Sub
    
    Sub New(rect As GpRectF, ByVal dstplg As GpPointF)
        Dim nativeMatrix As GpMatrix
        LastStatus = GdipCreateMatrix3(rect, dstplg, nativeMatrix)
        SetNativeMatrix(nativeMatrix)
    End Sub

    Sub New(rect As GpRect, ByVal dstplg As GpPoint)
        Dim nativeMatrix As GpMatrix
        LastStatus = GdipCreateMatrix3I(rect, dstplg, nativeMatrix)
        SetNativeMatrix(nativeMatrix)
    End Sub

    Sub Class_Terminate()
        GdipDeleteMatrix(mx)
    End Sub
    
    Function Clone() As Matrix
        Dim nativeClone As GpMatrix
        SetStatus(GdipCloneMatrix(mx, nativeClone))
        If LastResult = Ok Then Set Clone = New Matrix(nativeClone)
    End Function
        
    Property Get Elements() As Single()
        Dim results!()
        ReDim results(1 To 6)
        SetStatus(GdipGetMatrixElements(mx, results(1)))
        If LastStatus = Ok Then Elements = results
    End Property
    
    Function GetElements(m!) As GpStatus
        Return SetStatus(GdipGetMatrixElements(mx, m))
    End Function
    
    Function SetElements(ByVal m11!, ByVal m12!, ByVal m21!, ByVal m22!, ByVal dx!, ByVal dy!) As GpStatus
        Return SetStatus(GdipSetMatrixElements(mx, m11, m12, m21, m22, dx, dy))
    End Function
    
    Property Get OffsetX() As Single
        Dim elements!(1 To 6)
        If GetElements(elements(1)) = Ok Then OffsetX = elements(5)
    End Property
    
    Property Get OffsetY() As Single
        Dim elements!(1 To 6)
        If GetElements(elements(1)) = Ok Then OffsetY = elements(6)
    End Property
    
    [Description("Set to identity matrix")]
    Function Reset() As GpStatus
        Return SetStatus(GdipSetMatrixElements(mx, 1.0, 0.0, 0.0, 1.0, 0.0, 0.0))
    End Function
    
    Function Multiply(matrix As Matrix, ByVal order As GpMatrixOrder = MatrixOrderPrepend) As GpStatus
        Return SetStatus(GdipMultiplyMatrix(mx, GetNative(matrix), order))
    End Function
    
    Function Translate(ByVal offsetX!, ByVal offsety!, ByVal order As GpMatrixOrder = MatrixOrderPrepend) As GpStatus
        Return SetStatus(GdipTranslateMatrix(mx, offsetX, offsety, order))
    End Function
    
    Function Translate(ByVal offset As GpPointF, ByVal order As GpMatrixOrder = MatrixOrderPrepend) As GpStatus
        Return SetStatus(GdipTranslateMatrix(mx, offset.X, offset.Y, order))
    End Function
    
    Function Scale(ByVal scaleX!, ByVal scaleY!, ByVal order As GpMatrixOrder = MatrixOrderPrepend) As GpStatus
        Return SetStatus(GdipScaleMatrix(mx, scaleX, scaleY, order))
    End Function
        
    Function Rotate(ByVal angle!, ByVal order As GpMatrixOrder = MatrixOrderPrepend) As GpStatus
        Return SetStatus(GdipRotateMatrix(mx, angle, order))
    End Function
    
    Function RotateAt(ByVal angle!, ByVal center As GpPointF, ByVal order As GpMatrixOrder = MatrixOrderPrepend) As GpStatus
        If order = MatrixOrderPrepend Then
            SetStatus(GdipTranslateMatrix(mx, center.X, center.Y, order))
            SetStatus(GdipRotateMatrix(mx, angle, order))
            Return SetStatus(GdipTranslateMatrix(mx, -center.X, -center.Y, order))
        Else
            SetStatus(GdipTranslateMatrix(mx, -center.X, -center.Y, order))
            SetStatus(GdipRotateMatrix(mx, angle, order))
            Return SetStatus(GdipTranslateMatrix(mx, center.X, center.Y, order))
        End If
    End Function
    
    Function Shear(ByVal shearX!, ByVal shearY!, ByVal order As GpMatrixOrder = MatrixOrderPrepend) As GpStatus
        Return SetStatus(GdipShearMatrix(mx, shearX, shearY, order))
    End Function
    
    Function Invert() As GpStatus
        Return SetStatus(GdipInvertMatrix(mx))
    End Function
    
    Function TransformPoints(pts() As GpPointF) As GpStatus
        Return SetStatus(GdipTransformMatrixPoints(mx, pts(LBound(pts)), Len(pts)))
    End Function

    Function TransformPoints(pts() As GpPoint) As GpStatus
        Return SetStatus(GdipTransformMatrixPointsI(mx, pts(LBound(pts)), Len(pts)))
    End Function
    
    Function TransformPoints(pts As GpPointF, ByVal count& = 1) As GpStatus
        Return SetStatus(GdipTransformMatrixPoints(mx, pts, count))
    End Function
    
    Function TransformPoints(pts As GpPoint, ByVal count& = 1) As GpStatus
        Return SetStatus(GdipTransformMatrixPointsI(mx, pts, count))
    End Function
    
    Function TransformVectors(pts() As GpPointF) As GpStatus
        Return SetStatus(GdipVectorTransformMatrixPoints(mx, pts(LBound(pts)), Len(pts)))
    End Function

    Function TransformVectors(pts() As GpPoint) As GpStatus
        Return SetStatus(GdipVectorTransformMatrixPointsI(mx, pts(LBound(pts)), Len(pts)))
    End Function
    
    Function TransformVectors(pts As GpPointF, ByVal count& = 1) As GpStatus
        Return SetStatus(GdipVectorTransformMatrixPoints(mx, pts, count))
    End Function
    
    Function TransformVectors(pts As GpPoint, ByVal count& = 1) As GpStatus
        Return SetStatus(GdipVectorTransformMatrixPointsI(mx, pts, count))
    End Function
    
    Property Get IsInvertible() As Boolean
        Dim result As BOOL
        SetStatus(GdipIsMatrixInvertible(mx, result))
        Return result
    End Property
    
    Property Get IsIdentity() As Boolean
        Dim result As BOOL
        SetStatus(GdipIsMatrixIdentity(mx, result))
        Return result
    End Property

    Function Equals(matrix As Matrix) As Boolean
        Dim result As BOOL
        SetStatus(GdipIsMatrixEqual(mx, GetNative(matrix), result))
        Return result
    End Function
    
    Protected Property Get Native() As GpMatrix
        Return mx
    End Property
    
    Protected Sub SetNativeMatrix(ByVal nativeMatrix As GpMatrix)
        mx = nativeMatrix
    End Sub
End Class

Module GdipMatrix
    Function Matrix() As Matrix
        Return New Matrix
    End Function
    
    Function Matrix(ByVal m11!, ByVal m12!, ByVal m21!, ByVal m22!, ByVal dx!, ByVal dy!) As Matrix
        Return New Matrix(m11, m12, m21, m22, dx, dy)
    End Function
    
    Function Matrix(rect As GpRectF, ByVal dstplg As GpPointF) As Matrix
        Return New Matrix(rect, dstplg)
    End Function
    
    Function Matrix(rect As GpRect, ByVal dstplg As GpPoint) As Matrix
        Return New Matrix(rect, dstplg)
    End Function
    
    Type GpMatrix
        Native As LongPtr
    End Type
    
    Function GpMatrix() As GpMatrix
        GpMatrix.Native = 0
    End Function
    
    Public Declare PtrSafe Function GdipCreateMatrix Lib "gdiplus" (matrix As GpMatrix) As GpStatus
    Public Declare PtrSafe Function GdipCreateMatrix2 Lib "gdiplus" (ByVal m11 As Single, ByVal m12 As Single, ByVal m21 As Single, ByVal m22 As Single, ByVal Dx As Single, ByVal Dy As Single, matrix As GpMatrix) As GpStatus
    Public Declare PtrSafe Function GdipCreateMatrix3 Lib "gdiplus" (rect As GpRectF, dstplg As GpPointF, matrix As GpMatrix) As GpStatus
    Public Declare PtrSafe Function GdipCreateMatrix3I Lib "gdiplus" (rect As GpRect, dstplg As GpPoint, matrix As GpMatrix) As GpStatus
    Public Declare PtrSafe Function GdipInvertMatrix Lib "gdiplus" (ByVal matrix As GpMatrix) As GpStatus
    Public Declare PtrSafe Function GdipSetMatrixElements Lib "gdiplus" (ByVal matrix As GpMatrix, ByVal m11 As Single, ByVal m12 As Single, ByVal m21 As Single, ByVal m22 As Single, ByVal Dx As Single, ByVal Dy As Single) As GpStatus
    Public Declare PtrSafe Function GdipGetMatrixElements Lib "gdiplus" (ByVal matrix As GpMatrix, matrixOut As Single) As GpStatus
    Public Declare PtrSafe Function GdipCloneMatrix Lib "gdiplus" (ByVal matrix As GpMatrix, cloneMatrix As GpMatrix) As GpStatus
    Public Declare PtrSafe Function GdipMultiplyMatrix Lib "gdiplus" (ByVal matrix As GpMatrix, ByVal matrix2 As GpMatrix, ByVal order As GpMatrixOrder) As GpStatus
    Public Declare PtrSafe Function GdipTranslateMatrix Lib "gdiplus" (ByVal matrix As GpMatrix, ByVal offsetX As Single, ByVal offsetY As Single, ByVal order As GpMatrixOrder) As GpStatus
    Public Declare PtrSafe Function GdipRotateMatrix Lib "gdiplus" (ByVal matrix As GpMatrix, ByVal angle As Single, ByVal order As GpMatrixOrder) As GpStatus
    Public Declare PtrSafe Function GdipScaleMatrix Lib "gdiplus" (ByVal matrix As GpMatrix, ByVal scaleX As Single, ByVal scaleY As Single, ByVal order As GpMatrixOrder) As GpStatus
    Public Declare PtrSafe Function GdipShearMatrix Lib "gdiplus" (ByVal matrix As GpMatrix, ByVal shearX As Single, ByVal shearY As Single, ByVal order As GpMatrixOrder) As GpStatus
    Public Declare PtrSafe Function GdipIsMatrixInvertible Lib "gdiplus" (ByVal matrix As GpMatrix, result As BOOL) As GpStatus
    Public Declare PtrSafe Function GdipIsMatrixIdentity Lib "gdiplus" (ByVal matrix As GpMatrix, result As BOOL) As GpStatus
    Public Declare PtrSafe Function GdipIsMatrixEqual Lib "gdiplus" (ByVal matrix1 As GpMatrix, ByVal matrix2 As GpMatrix, result As BOOL) As GpStatus
    Public Declare PtrSafe Function GdipDeleteMatrix Lib "gdiplus" (ByVal matrix As GpMatrix) As GpStatus
    
    Public Declare PtrSafe Function GdipTransformMatrixPoints Lib "gdiplus" (ByVal matrix As GpMatrix, pts As GpPointF, ByVal count As Long) As GpStatus
    Public Declare PtrSafe Function GdipVectorTransformMatrixPoints Lib "gdiplus" (ByVal matrix As GpMatrix, pts As GpPointF, ByVal count As Long) As GpStatus
    Public Declare PtrSafe Function GdipTransformMatrixPointsI Lib "gdiplus" (ByVal matrix As GpMatrix, pts As GpPoint, ByVal count As Long) As GpStatus
    Public Declare PtrSafe Function GdipVectorTransformMatrixPointsI Lib "gdiplus" (ByVal matrix As GpMatrix, pts As GpPoint, ByVal count As Long) As GpStatus
End Module
