' A part of the GDI+ Package for TwinBasic  https://github.com/kubao/GdiPlus
' Licensed under the MIT license. (c) 2026 Sunderland Ober Consulting.
' "Windows" is a trademark of the Microsoft Corporation.
' Modeled after code and descriptions (c) Microsoft, taken from SDK headers and official documentation.
' Certain flat API function and type declarations taken from WinDevLib by fafalone, with thanks.

[COMCreatable(False)]
Class Matrix
    Inherits GdiPlusBase
    Protected mx As GpMatrix
    
    Sub New(ByVal nativeMatrix As GpMatrix)
        SetNativeMatrix(nativeMatrix)
    End Sub
        
    [Description("Become an identity matrix by default.")]
    Sub New()
        SetStatus(mx.CreateInto(mx))
    End Sub
    
    Sub New(ByVal m11!, ByVal m12!, ByVal m21!, ByVal m22!, ByVal dx!, ByVal dy!)
        SetStatus(mx.CreateInto2(m11, m12, m21, m22, dx, dy, mx))
    End Sub
    
    Sub New(rect As GpRectF, ByVal dstplg As GpPointF)
        SetStatus(mx.CreateInto3(rect, dstplg, mx))
    End Sub

    Sub New(rect As GpRect, ByVal dstplg As GpPoint)
        SetStatus(mx.CreateInto3I(rect, dstplg, mx))
    End Sub

    Sub Class_Terminate()
        mx.Delete
    End Sub
    
    Function Clone() As Matrix
        Dim nativeClone As GpMatrix
        SetStatus(mx.Clone(nativeClone))
        If LastResult = Ok Then Set Clone = New Matrix(nativeClone)
    End Function
        
    Property Get Elements() As Single()
        Dim results!()
        ReDim results(1 To 6)
        SetStatus(mx.GetElements(results(1)))
        If LastResult = Ok Then Elements = results
    End Property
    
    Function GetElements(m!) As GpStatus
        Return SetStatus(mx.GetElements(m))
    End Function
    
    Function SetElements(ByVal m11!, ByVal m12!, ByVal m21!, ByVal m22!, ByVal dx!, ByVal dy!) As GpStatus
        Return SetStatus(mx.SetElements(m11, m12, m21, m22, dx, dy))
    End Function
    
    Property Get OffsetX() As Single
        Dim elements!(1 To 6)
        If GetElements(elements(1)) = Ok Then OffsetX = elements(5)
    End Property
    
    Property Get OffsetY() As Single
        Dim elements!(1 To 6)
        If GetElements(elements(1)) = Ok Then OffsetY = elements(6)
    End Property
    
    [Description("Set to identity matrix")]
    Function Reset() As GpStatus
        Return SetStatus(mx.SetElements(1.0, 0.0, 0.0, 1.0, 0.0, 0.0))
    End Function
    
    Function Multiply(matrix As Matrix, ByVal order As GpMatrixOrder = MatrixOrderPrepend) As GpStatus
        Return SetStatus(mx.Multiply(GetNative(matrix), order))
    End Function
    
    Function Translate(ByVal offsetX!, ByVal offsety!, ByVal order As GpMatrixOrder = MatrixOrderPrepend) As GpStatus
        Return SetStatus(mx.Translate(offsetX, offsety, order))
    End Function
    
    Function Translate(ByVal offset As GpPointF, ByVal order As GpMatrixOrder = MatrixOrderPrepend) As GpStatus
        Return SetStatus(mx.Translate(offset.X, offset.Y, order))
    End Function
    
    Function Scale(ByVal scaleX!, ByVal scaleY!, ByVal order As GpMatrixOrder = MatrixOrderPrepend) As GpStatus
        Return SetStatus(mx.Scale(scaleX, scaleY, order))
    End Function
        
    Function Rotate(ByVal angle!, ByVal order As GpMatrixOrder = MatrixOrderPrepend) As GpStatus
        Return SetStatus(mx.Rotate(angle, order))
    End Function
    
    Function RotateAt(ByVal angle!, ByVal center As GpPointF, ByVal order As GpMatrixOrder = MatrixOrderPrepend) As GpStatus
        If order = MatrixOrderPrepend Then
            SetStatus(mx.Translate(center.X, center.Y, order))
            SetStatus(mx.Rotate(angle, order))
            Return SetStatus(mx.Translate(-center.X, -center.Y, order))
        Else
            SetStatus(mx.Translate(-center.X, -center.Y, order))
            SetStatus(mx.Rotate(angle, order))
            Return SetStatus(mx.Translate(center.X, center.Y, order))
        End If
    End Function
    
    Function Shear(ByVal shearX!, ByVal shearY!, ByVal order As GpMatrixOrder = MatrixOrderPrepend) As GpStatus
        Return SetStatus(mx.Shear(shearX, shearY, order))
    End Function
    
    Function Invert() As GpStatus
        Return SetStatus(mx.Invert())
    End Function
    
    Function TransformPoints(pts() As GpPointF) As GpStatus
        Return SetStatus(mx.TransformPoints(pts(LBound(pts)), ArrayLen(pts)))
    End Function

    Function TransformPoints(pts() As GpPoint) As GpStatus
        Return SetStatus(mx.TransformPointsI(pts(LBound(pts)), ArrayLen(pts)))
    End Function
    
    Function TransformPoints(pts As GpPointF, ByVal count&) As GpStatus
        Return SetStatus(mx.TransformPoints(pts, count))
    End Function
    
    Function TransformPoints(pts As GpPoint, ByVal count& = 1) As GpStatus
        Return SetStatus(mx.TransformPointsI(pts, count))
    End Function
    
    Function TransformVectors(pts() As GpPointF) As GpStatus
        Return SetStatus(mx.VectorTransformPoints(pts(LBound(pts)), ArrayLen(pts)))
    End Function

    Function TransformVectors(pts() As GpPoint) As GpStatus
        Return SetStatus(mx.VectorTransformPointsI(pts(LBound(pts)), ArrayLen(pts)))
    End Function
    
    Function TransformVectors(pts As GpPointF, ByVal count& = 1) As GpStatus
        Return SetStatus(mx.TransformPoints(pts, count))
    End Function
    
    Function TransformVectors(pts As GpPoint, ByVal count& = 1) As GpStatus
        Return SetStatus(mx.TransformPointsI(pts, count))
    End Function
    
    Property Get IsInvertible() As Boolean
        Dim result As GDIP_BOOL
        SetStatus(mx.IsInvertible(result))
        Return result
    End Property
    
    Property Get IsIdentity() As Boolean
        Dim result As GDIP_BOOL
        SetStatus(mx.IsIdentity(result))
        Return result
    End Property

    Function Equals(matrix As Matrix) As Boolean
        Dim result As GDIP_BOOL
        SetStatus(mx.IsEqualTo(GetNative(matrix), result))
        Return result
    End Function
    
    Protected Property Get Native() As GpMatrix
        Return mx
    End Property
    
    Protected Sub SetNativeMatrix(ByVal nativeMatrix As GpMatrix)
        mx = nativeMatrix
    End Sub
End Class

Module GdipMatrix
    Function Matrix() As Matrix
        Return New Matrix
    End Function
    
    Function Matrix(ByVal m11!, ByVal m12!, ByVal m21!, ByVal m22!, ByVal dx!, ByVal dy!) As Matrix
        Return New Matrix(m11, m12, m21, m22, dx, dy)
    End Function
    
    Function Matrix(rect As GpRectF, ByVal dstplg As GpPointF) As Matrix
        Return New Matrix(rect, dstplg)
    End Function
    
    Function Matrix(rect As GpRect, ByVal dstplg As GpPoint) As Matrix
        Return New Matrix(rect, dstplg)
    End Function
    
    Type GpMatrix
        Native As LongPtr
        
        [UseGetLastError(False)]
        Public Declare PtrSafe Function CreateInto Lib "gdiplus" Alias "GdipCreateMatrix" (matrix As GpMatrix) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function CreateInto2 Lib "gdiplus" Alias "GdipCreateMatrix2" (ByVal m11 As Single, ByVal m12 As Single, ByVal m21 As Single, ByVal m22 As Single, ByVal Dx As Single, ByVal Dy As Single, matrix As GpMatrix) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function CreateInto3 Lib "gdiplus" Alias "GdipCreateMatrix3" (rect As GpRectF, dstplg As GpPointF, matrix As GpMatrix) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function CreateInto3I Lib "gdiplus" Alias "GdipCreateMatrix3I" (rect As GpRect, dstplg As GpPoint, matrix As GpMatrix) As GpStatus

        [UseGetLastError(False)]
        Public Declare PtrSafe Function Invert Lib "gdiplus" Alias "GdipInvertMatrix" (ByVal Me As GpMatrix) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function SetElements Lib "gdiplus" Alias "GdipSetMatrixElements" (ByVal Me As GpMatrix, ByVal m11 As Single, ByVal m12 As Single, ByVal m21 As Single, ByVal m22 As Single, ByVal Dx As Single, ByVal Dy As Single) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function GetElements Lib "gdiplus" Alias "GdipGetMatrixElements" (ByVal Me As GpMatrix, matrixOut As Single) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function Clone Lib "gdiplus" Alias "GdipCloneMatrix" (ByVal Me As GpMatrix, cloneMatrix As GpMatrix) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function Multiply Lib "gdiplus" Alias "GdipMultiplyMatrix" (ByVal Me As GpMatrix, ByVal matrix2 As GpMatrix, ByVal order As GpMatrixOrder) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function Translate Lib "gdiplus" Alias "GdipTranslateMatrix" (ByVal Me As GpMatrix, ByVal offsetX As Single, ByVal offsetY As Single, ByVal order As GpMatrixOrder) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function Rotate Lib "gdiplus" Alias "GdipRotateMatrix" (ByVal Me As GpMatrix, ByVal angle As Single, ByVal order As GpMatrixOrder) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function Scale Lib "gdiplus" Alias "GdipScaleMatrix" (ByVal Me As GpMatrix, ByVal scaleX As Single, ByVal scaleY As Single, ByVal order As GpMatrixOrder) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function Shear Lib "gdiplus" Alias "GdipShearMatrix" (ByVal Me As GpMatrix, ByVal shearX As Single, ByVal shearY As Single, ByVal order As GpMatrixOrder) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function IsInvertible Lib "gdiplus" Alias "GdipIsMatrixInvertible" (ByVal Me As GpMatrix, result As GDIP_BOOL) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function IsIdentity Lib "gdiplus" Alias "GdipIsMatrixIdentity" (ByVal Me As GpMatrix, result As GDIP_BOOL) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function IsEqualTo Lib "gdiplus" Alias "GdipIsMatrixEqual" (ByVal Me As GpMatrix, ByVal matrix2 As GpMatrix, result As GDIP_BOOL) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function Delete Lib "gdiplus" Alias "GdipDeleteMatrix" (ByVal Me As GpMatrix) As GpStatus
    
        [UseGetLastError(False)]
        Public Declare PtrSafe Function TransformPoints Lib "gdiplus" Alias "GdipTransformMatrixPoints" (ByVal Me As GpMatrix, pts As GpPointF, ByVal count As Long) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function VectorTransformPoints Lib "gdiplus" Alias "GdipVectorTransformMatrixPoints" (ByVal Me As GpMatrix, pts As GpPointF, ByVal count As Long) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function TransformPointsI Lib "gdiplus" Alias "GdipTransformMatrixPointsI" (ByVal Me As GpMatrix, pts As GpPoint, ByVal count As Long) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function VectorTransformPointsI Lib "gdiplus" Alias "GdipVectorTransformMatrixPointsI" (ByVal Me As GpMatrix, pts As GpPoint, ByVal count As Long) As GpStatus
    End Type
    
    Function GpMatrix() As GpMatrix
        GpMatrix.Native = 0
    End Function
End Module
