' A part of the GDI+ Package for TwinBasic  https://github.com/kubao/GdiPlus
' Licensed under the MIT license. (c) 2026 Sunderland Ober Consulting.
' "Windows" is a trademark of the Microsoft Corporation.
' Modeled after code and descriptions (c) Microsoft, taken from SDK headers and official documentation.
' Certain flat API function and type declarations taken from WinDevLib by fafalone, with thanks.

[COMCreatable(False)]
Class PathIterator
    Inherits GdiPlusBase
    Dim pi As GpPathIterator
        
    Sub New(path As GraphicsPath)
        Dim iter As GpPathIterator
        LastStatus = GdipCreatePathIter(iter, GetNative(path))
        SetNativeIterator(iter)
    End Sub
    
    Sub Class_Terminate()
        GdipDeletePathIter(pi)
    End Sub
      
    Function NextIndexedSubpath() As IndexedSubPath
        Dim booln As BOOL
        SetStatus(GdipPathIterNextSubpath(pi, _
            NextIndexedSubpath.count, NextIndexedSubpath.startIndex, NextIndexedSubpath.endIndex, booln))
        NextIndexedSubpath.isClosed = booln
    End Function
    
    Function NextPathSubpath() As PathSubPath
        Dim native As GpPath
        Dim booln As BOOL
        SetStatus(GdipPathIterNextSubpathPath(pi, NextPathSubpath.count, native, booln))
        If LastResult = Ok Then
            NextPathSubpath.path = New GraphicsPath(native)
            NextPathSubpath.isClosed = booln
        End If
    End Function
    
    Function NextPathType() As SubPathType
        SetStatus(GdipPathIterNextPathType(pi, _
            NextPathType.count, NextPathType.pathType, NextPathType.startIndex, NextPathType.endIndex))
    End Function
    
    Function NextMarker() As SubPathSection
        SetStatus(GdipPathIterNextMarker(pi, NextMarker.count, NextMarker.startIndex, NextMarker.endIndex))
    End Function
    
    Function NextSubpath(startIndex&, endIndex&, isClosed As Boolean) As Long
        Dim booln As BOOL
        SetStatus(GdipPathIterNextSubpath(pi, NextSubpath, startIndex, endIndex, booln))
        isClosed = booln
    End Function
    
    Function NextSubPath(path As GraphicsPath, isClosed As Boolean) As Long
        Dim booln As BOOL
        SetStatus(GdipPathIterNextSubpathPath(pi, NextSubPath, GetNative(path), booln))
        isClosed = booln
    End Function
    
    Function NextPathType(pathType As Byte, startIndex&, endIndex&) As Long
        SetStatus(GdipPathIterNextPathType(pi, NextPathType, pathType, startIndex, endIndex))
    End Function
    
    Function NextMarker(startIndex&, endIndex&) As Long
        SetStatus(GdipPathIterNextMarker(pi, NextMarker, startIndex, endIndex))
    End Function
    
    Function NextMarker(path As GraphicsPath) As Long
        SetStatus(GdipPathIterNextMarkerPath(pi, NextMarker, GetNative(path)))
    End Function
    
    Property Get Count() As Long
        SetStatus(GdipPathIterGetCount(pi, Count))
    End Property
    
    Property Get SubpathCount() As Long
        SetStatus(GdipPathIterGetSubpathCount(pi, SubpathCount))
    End Property
    
    Property Get HasCurve() As Boolean
        Dim booln As BOOL
        SetStatus(GdipPathIterHasCurve(pi, booln))
        Return booln
    End Property
    
    Function Rewind() As GpStatus
        SetStatus(GdipPathIterRewind(pi))
    End Function
    
    Function Enumerate() As TypedPoint()
        Dim count& = Me.Count
        Dim points() As GpPointF
        Dim types() As Byte
        Dim result() As TypedPoint
        ReDim points(1 To count)
        ReDim types(1 To count)
        SetStatus(GdipPathIterEnumerate(pi, count, points(1), types(1), count))
        If LastResult = Ok Then
            ReDim result(1 To count)
            For i As Integer = 1 To count
                result(i).pos = points(i)
                result(i).type = types(i)
            Next
            Return result
        End If
    End Function
    
    Function CopyData(ByVal startIndex&, ByVal endIndex&) As TypedPoint()
        Dim count& = 1 + endIndex - startIndex
        If count <= 0 Then
            SetStatus(InvalidParameter)
            Exit Function
        End If
        Dim points() As GpPointF
        Dim types() As Byte
        Dim result() As TypedPoint
        ReDim points(1 To count)
        ReDim types(1 To count)
        SetStatus(GdipPathIterCopyData(pi, count, points(1), types(1), startIndex, endIndex))
        If LastResult = Ok Then
            ReDim result(1 To count)
            For i As Integer = 1 To count
                result(i).pos = points(i)
                result(i).type = types(i)
            Next
            Return result
        End If
    End Function
    
    Function Enumerate(points As GpPointF, types As Byte, ByVal count&) As Long
        SetStatus(GdipPathIterEnumerate(pi, Enumerate, points, types, count))
    End Function
    
    Function CopyData(points As GpPointF, types As Byte, ByVal startIndex&, ByVal endIndex&) As Long
        SetStatus(GdipPathIterCopyData(pi, CopyData&, points, types, startIndex, endIndex))
    End Function
        
    Protected Property Get Native() As GpPathIterator
        Return pi
    End Property
    
    Protected Sub New(ByVal nativeIterator As GpPathIterator)
        LastStatus = Ok
        SetNativeIterator(nativeIterator)
    End Sub
    
    Protected Sub SetNativeIterator(ByVal nativeIterator As GpPathIterator)
        pi = nativeIterator
    End Sub
End Class

Module GdipPathIterator
    Function PathIterator(path As GraphicsPath) As PathIterator
        Return New PathIterator(path)
    End Function
    
    Type GpPathIterator
        Native As LongPtr
    End Type
    
    Function GpPathIterator() As GpPathIterator
        GpPathIterator.Native = 0
    End Function
    
    Type TypedPoint
        pos As GpPointF
        type As Byte
    End Type
    
    Type SubPathSection
        startIndex&
        endIndex&      
        count&
    End Type
    
    Type SubPathType
        pathType As Byte
        startIndex&
        endIndex&
        count&
    End Type
    
    Type IndexedSubPath
        startIndex&
        endIndex&
        isClosed As Boolean
        count&
    End Type
    
    Type PathSubPath
        path As GraphicsPath
        isClosed As Boolean
        count&
    End Type
    
    Public Declare PtrSafe Function GdipCreatePathIter Lib "gdiplus" (iterator As GpPathIterator, ByVal Path As GpPath) As GpStatus
    Public Declare PtrSafe Function GdipDeletePathIter Lib "gdiplus" (ByVal iterator As GpPathIterator) As GpStatus
    Public Declare PtrSafe Function GdipPathIterNextSubpath Lib "gdiplus" (ByVal iterator As GpPathIterator, resultCount As Long, startIndex As Long, endIndex As Long, isClosed As BOOL) As GpStatus
    Public Declare PtrSafe Function GdipPathIterNextSubpathPath Lib "gdiplus" (ByVal iterator As GpPathIterator, resultCount As Long, ByVal path As GpPath, isClosed As BOOL) As GpStatus
    Public Declare PtrSafe Function GdipPathIterNextPathType Lib "gdiplus" (ByVal iterator As GpPathIterator, resultCount As Long, pathType As Byte, startIndex As Long, endIndex As Long) As GpStatus
    Public Declare PtrSafe Function GdipPathIterNextMarker Lib "gdiplus" (ByVal iterator As GpPathIterator, resultCount As Long, startIndex As Long, endIndex As Long) As GpStatus
    Public Declare PtrSafe Function GdipPathIterNextMarkerPath Lib "gdiplus" (ByVal iterator As GpPathIterator, resultCount As Long, ByVal path As GpPath) As GpStatus
    Public Declare PtrSafe Function GdipPathIterGetCount Lib "gdiplus" (ByVal iterator As GpPathIterator, count As Long) As GpStatus
    Public Declare PtrSafe Function GdipPathIterGetSubpathCount Lib "gdiplus" (ByVal iterator As GpPathIterator, count As Long) As GpStatus
    Public Declare PtrSafe Function GdipPathIterIsValid Lib "gdiplus" (ByVal iterator As GpPathIterator, valid As BOOL) As GpStatus
    Public Declare PtrSafe Function GdipPathIterHasCurve Lib "gdiplus" (ByVal iterator As GpPathIterator, hasCurve As BOOL) As GpStatus
    Public Declare PtrSafe Function GdipPathIterRewind Lib "gdiplus" (ByVal iterator As GpPathIterator) As GpStatus
    Public Declare PtrSafe Function GdipPathIterEnumerate Lib "gdiplus" (ByVal iterator As GpPathIterator, resultCount As Long, points As GpPointF, types As Byte, ByVal count As Long) As GpStatus
    Public Declare PtrSafe Function GdipPathIterCopyData Lib "gdiplus" (ByVal iterator As GpPathIterator, resultCount As Long, points As GpPointF, types As Byte, ByVal startIndex As Long, ByVal endIndex As Long) As GpStatus
End Module
