' A part of the GDI+ Package for TwinBasic  https://github.com/kubao/GdiPlus
' Licensed under the MIT license. (c) 2026 Sunderland Ober Consulting.
' "Windows" is a trademark of the Microsoft Corporation.
' Modeled after code and descriptions (c) Microsoft, taken from SDK headers and official documentation.
' Certain flat API function and type declarations taken from WinDevLib by fafalone, with thanks.

[COMCreatable(False)]
Class PathIterator
    Inherits GdiPlusBase
    Dim pi As GpPathIterator
        
    Sub New(path As GraphicsPath)
        LastStatus = pi.CreateInto(GetNative(path))
    End Sub
    
    Sub Class_Terminate()
        pi.Delete
    End Sub
      
    Function NextIndexedSubpath() As IndexedSubPath
        Dim booln As GDIP_BOOL
        SetStatus(pi.NextSubpath( _
            NextIndexedSubpath.count, NextIndexedSubpath.startIndex, NextIndexedSubpath.endIndex, booln))
        NextIndexedSubpath.isClosed = booln
    End Function
    
    Function NextPathSubpath() As PathSubPath
        Dim native As GpPath
        Dim booln As GDIP_BOOL
        SetStatus(pi.NextSubpathPath(NextPathSubpath.count, native, booln))
        If LastResult = Ok Then
            NextPathSubpath.path = New GraphicsPath(native)
            NextPathSubpath.isClosed = booln
        End If
    End Function
    
    Function NextPathType() As SubPathType
        SetStatus(pi.NextPathType( _
            NextPathType.count, NextPathType.pathType, NextPathType.startIndex, NextPathType.endIndex))
    End Function
    
    Function NextMarker() As SubPathSection
        SetStatus(pi.NextMarker(NextMarker.count, NextMarker.startIndex, NextMarker.endIndex))
    End Function
    
    Function NextSubpath(startIndex&, endIndex&, isClosed As Boolean) As Long
        Dim booln As GDIP_BOOL
        SetStatus(pi.NextSubpath(NextSubpath, startIndex, endIndex, booln))
        isClosed = booln
    End Function
    
    Function NextSubPath(path As GraphicsPath, isClosed As Boolean) As Long
        Dim booln As GDIP_BOOL
        SetStatus(pi.NextSubpathPath(NextSubPath, GetNative(path), booln))
        isClosed = booln
    End Function
    
    Function NextPathType(pathType As Byte, startIndex&, endIndex&) As Long
        SetStatus(pi.NextPathType(NextPathType, pathType, startIndex, endIndex))
    End Function
    
    Function NextMarker(startIndex&, endIndex&) As Long
        SetStatus(pi.NextMarker(NextMarker, startIndex, endIndex))
    End Function
    
    Function NextMarker(path As GraphicsPath) As Long
        SetStatus(pi.NextMarkerPath(NextMarker, GetNative(path)))
    End Function
    
    Property Get Count() As Long
        SetStatus(pi.GetCount(Count))
    End Property
    
    Property Get SubpathCount() As Long
        SetStatus(pi.GetSubpathCount(SubpathCount))
    End Property
    
    Property Get HasCurve() As Boolean
        Dim booln As GDIP_BOOL
        SetStatus(pi.HasCurve(booln))
        Return booln
    End Property
    
    Function Rewind() As GpStatus
        SetStatus(pi.Rewind())
    End Function
    
    Function Enumerate() As TypedPoint()
        Dim count& = Me.Count
        Dim points() As GpPointF
        Dim types() As Byte
        Dim result() As TypedPoint
        ReDim points(1 To count)
        ReDim types(1 To count)
        SetStatus(pi.Enumerate(count, points(1), types(1), count))
        If LastResult = Ok Then
            ReDim result(1 To count)
            For i As Integer = 1 To count
                result(i).pos = points(i)
                result(i).type = types(i)
            Next
            Return result
        End If
    End Function
    
    Function CopyData(ByVal startIndex&, ByVal endIndex&) As TypedPoint()
        Dim count& = 1 + endIndex - startIndex
        If count <= 0 Then
            SetStatus(InvalidParameter)
            Exit Function
        End If
        Dim points() As GpPointF
        Dim types() As Byte
        Dim result() As TypedPoint
        ReDim points(1 To count)
        ReDim types(1 To count)
        SetStatus(pi.CopyData(count, points(1), types(1), startIndex, endIndex))
        If LastResult = Ok Then
            ReDim result(1 To count)
            For i As Integer = 1 To count
                result(i).pos = points(i)
                result(i).type = types(i)
            Next
            Return result
        End If
    End Function
    
    Function Enumerate(points As GpPointF, types As Byte, ByVal count&) As Long
        SetStatus(pi.Enumerate(Enumerate, points, types, count))
    End Function
    
    Function CopyData(points As GpPointF, types As Byte, ByVal startIndex&, ByVal endIndex&) As Long
        SetStatus(pi.CopyData(CopyData, points, types, startIndex, endIndex))
    End Function
        
    Protected Property Get Native() As GpPathIterator
        Return pi
    End Property
    
    Protected Sub New(ByVal nativeIterator As GpPathIterator)
        LastStatus = Ok
        SetNativeIterator(nativeIterator)
    End Sub
    
    Protected Sub SetNativeIterator(ByVal nativeIterator As GpPathIterator)
        pi = nativeIterator
    End Sub
End Class

Module GdipPathIterator
    Function PathIterator(path As GraphicsPath) As PathIterator
        Return New PathIterator(path)
    End Function
    
    Type GpPathIterator
        Native As LongPtr
        
        Function Create(ByVal path As GpPath) As GpStatus
            Return Me.CreateInto(path)
        End Function
            
        [UseGetLastError(False)]
        Declare PtrSafe Function CreateInto Lib "gdiplus" Alias "GdipCreatePathIter" (Me As GpPathIterator, ByVal Path As GpPath) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function Delete Lib "gdiplus" Alias "GdipDeletePathIter" (ByVal Me As GpPathIterator) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function NextSubpath Lib "gdiplus" Alias "GdipPathIterNextSubpath" (ByVal Me As GpPathIterator, resultCount As Long, startIndex As Long, endIndex As Long, isClosed As GDIP_BOOL) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function NextSubpathPath Lib "gdiplus" Alias "GdipPathIterNextSubpathPath" (ByVal Me As GpPathIterator, resultCount As Long, ByVal path As GpPath, isClosed As GDIP_BOOL) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function NextPathType Lib "gdiplus" Alias "GdipPathIterNextPathType" (ByVal Me As GpPathIterator, resultCount As Long, pathType As Byte, startIndex As Long, endIndex As Long) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function NextMarker Lib "gdiplus" Alias "GdipPathIterNextMarker" (ByVal Me As GpPathIterator, resultCount As Long, startIndex As Long, endIndex As Long) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function NextMarkerPath Lib "gdiplus" Alias "GdipPathIterNextMarkerPath" (ByVal Me As GpPathIterator, resultCount As Long, ByVal path As GpPath) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function GetCount Lib "gdiplus" Alias "GdipPathIterGetCount" (ByVal Me As GpPathIterator, count As Long) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function GetSubpathCount Lib "gdiplus" Alias "GdipPathIterGetSubpathCount" (ByVal Me As GpPathIterator, count As Long) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function IsValid Lib "gdiplus" Alias "GdipPathIterIsValid" (ByVal Me As GpPathIterator, valid As GDIP_BOOL) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function HasCurve Lib "gdiplus" Alias "GdipPathIterHasCurve" (ByVal Me As GpPathIterator, hasCurve As GDIP_BOOL) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function Rewind Lib "gdiplus" Alias "GdipPathIterRewind" (ByVal Me As GpPathIterator) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function Enumerate Lib "gdiplus" Alias "GdipPathIterEnumerate" (ByVal Me As GpPathIterator, resultCount As Long, points As GpPointF, types As Byte, ByVal count As Long) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function CopyData Lib "gdiplus" Alias "GdipPathIterCopyData" (ByVal Me As GpPathIterator, resultCount As Long, points As GpPointF, types As Byte, ByVal startIndex As Long, ByVal endIndex As Long) As GpStatus
    End Type
    
    Function GpPathIterator() As GpPathIterator
        GpPathIterator.Native = 0
    End Function
    
    Type TypedPoint
        pos As GpPointF
        type As Byte
    End Type
    
    Type SubPathSection
        startIndex&
        endIndex&      
        count&
    End Type
    
    Type SubPathType
        pathType As Byte
        startIndex&
        endIndex&
        count&
    End Type
    
    Type IndexedSubPath
        startIndex&
        endIndex&
        isClosed As Boolean
        count&
    End Type
    
    Type PathSubPath
        path As GraphicsPath
        isClosed As Boolean
        count&
    End Type
End Module
