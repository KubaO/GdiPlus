' A part of the GDI+ Package for TwinBasic  https://github.com/kubao/GdiPlus
' Licensed under the MIT license. (c) 2026 Sunderland Ober Consulting.
' "Windows" is a trademark of the Microsoft Corporation.
' Modeled after code and descriptions (c) Microsoft, taken from SDK headers and official documentation.
' Certain flat API function and type declarations taken from WinDevLib by fafalone, with thanks.

[COMCreatable(False)]
[Description("Texture Brush Fill Object")]
Class TextureBrush
    Inherits Brush
        
    ' Used by Pen
    Public Sub New(ByVal nativeTexture As GpTexture, ByVal status As GpStatus = Ok)
        LastStatus = status
        SetNativeTexture(nativeTexture)
    End Sub
    
    Sub New(image As Image, ByVal wrapMode As GpWrapMode = WrapModeTile)
        Dim texture As GpTexture
        LastStatus = GdipCreateTexture(GetNative(image), wrapMode, texture)
        SetNativeTexture(texture)
    End Sub
    
    ' When creating a texture brush from a metafile image, the dstRect
    ' is used to specify the size that the metafile image should be
    ' rendered at in the device units of the destination graphics.
    ' It is NOT used to crop the metafile image, so only the width 
    ' and height values matter for metafiles.
    
    Sub New(image As Image, ByVal wrapmode As GpWrapMode, dstRect As GpRectF)
        Dim texture As GpTexture
        LastStatus = GdipCreateTexture2(GetNative(image), wrapmode, _
                dstRect.X, dstRect.Y, dstRect.Width, dstRect.Height, texture)
        SetNativeTexture(texture)
    End Sub
    
    Sub New(image As Image, dstRect As GpRectF, Optional imageAttributes As ImageAttributes)
        Dim texture As GpTexture
        LastStatus = GdipCreateTextureIA(GetNative(image), GetNative(imageAttributes), _
                dstRect.X, dstRect.Y, dstRect.Width, dstRect.Height, texture)
        SetNativeTexture(texture)
    End Sub
    
    Sub New(image As Image, dstRect As GpRect, Optional imageAttributes As ImageAttributes)
        Dim texture As GpTexture
        LastStatus = GdipCreateTextureIAI(GetNative(image), GetNative(imageAttributes), _
                dstRect.X, dstRect.Y, dstRect.Width, dstRect.Height, texture)
        SetNativeTexture(texture)
    End Sub
    
    Sub New(image As Image, ByVal wrapmode As GpWrapMode, dstRect As GpRect)
        Dim texture As GpTexture
        LastStatus = GdipCreateTexture2I(GetNative(image), wrapmode, _
                dstRect.X, dstRect.Y, dstRect.Width, dstRect.Height, texture)
        SetNativeTexture(texture)
    End Sub
    
    Sub New(image As Image, ByVal wrapmode As GpWrapMode, ByVal dstX!, ByVal dstY!, ByVal dstWidth!, ByVal dstHeight!)
        Dim texture As GpTexture
        LastStatus = GdipCreateTexture2(GetNative(image), wrapmode, _
                dstX, dstY, dstWidth, dstHeight, texture)
        SetNativeTexture(texture)
    End Sub
    
    Sub New(image As Image, ByVal wrapmode As GpWrapMode, ByVal dstX&, ByVal dstY&, ByVal dstWidth&, ByVal dstHeight&)
        Dim texture As GpTexture
        LastStatus = GdipCreateTexture2I(GetNative(image), wrapmode, _
                dstX, dstY, dstWidth, dstHeight, texture)
        SetNativeTexture(texture)
    End Sub
    
    Property Let Transform(matrix As Matrix)
        SetStatus(GdipSetTextureTransform(br, GetNative(matrix)))
    End Property
    
    Property Get Transform() As Matrix
        Dim nativeMatrix As GpMatrix
        SetStatus(GdipGetTextureTransform(br, nativeMatrix))
        If LastResult = Ok Then Set Transform = New Matrix(nativeMatrix)
    End Property
    
    Function GetTransform(outMatrix As Matrix) As GpStatus
        Return SetStatus(GdipGetTextureTransform(br, UnprotectedAccess(outMatrix).mx))
    End Function

    Function ResetTransform() As GpStatus
        Return SetStatus(GdipResetTextureTransform(br))
    End Function
    
    Function MultiplyTransform(matrix As Matrix, ByVal order As GpMatrixOrder = MatrixOrderPrepend) As GpStatus
        Return SetStatus(GdipMultiplyTextureTransform(br, GetNative(matrix), order))
    End Function
    
    Function TranslateTransform(ByVal delta As GpPointF, ByVal order As GpMatrixOrder = MatrixOrderPrepend) As GpStatus
        Return SetStatus(GdipTranslateTextureTransform(br, delta.X, delta.Y, order))
    End Function
    
    Function TranslateTransform(ByVal dx!, ByVal dy!, ByVal order As GpMatrixOrder = MatrixOrderPrepend) As GpStatus
        Return SetStatus(GdipTranslateTextureTransform(br, dx, dy, order))
    End Function
    
    Function ScaleTransform(ByVal sx!, ByVal sy!, ByVal order As GpMatrixOrder = MatrixOrderPrepend) As GpStatus
        Return SetStatus(GdipScaleTextureTransform(br, sx, sy, order))
    End Function
    
    Function RotateTransform(ByVal angle!, ByVal order As GpMatrixOrder = MatrixOrderPrepend) As GpStatus
        Return SetStatus(GdipRotateTextureTransform(br, angle, order))
    End Function

    Property Let WrapMode(ByVal wrapMode As GpWrapMode)
        SetStatus(GdipSetTextureWrapMode(br, wrapMode))
    End Property
    
    Property Get WrapMode() As GpWrapMode
        SetStatus(GdipGetTextureWrapMode(br, WrapMode))
    End Property
        
    Function GetImage() As Image
        Dim img As GpImage
        SetStatus(GdipGetTextureImage(br, img))
        If LastStatus = Ok Then Set GetImage = New Image(img, LastResult)
    End Function
    
    Protected Sub New()
    End Sub
    
    Protected Property Get Native() As GpTexture
        Native.Native = br.Native
    End Property
    
    Protected Sub SetNativeTexture(ByVal nativeTexture As GpTexture)
        br.Native = nativeTexture.Native
    End Sub
End Class

Module GdipTexture
    Function TextureBrush(image As Image, ByVal wrapMode As GpWrapMode = WrapModeTile) As TextureBrush
        Return New TextureBrush(image, wrapMode)
    End Function
    
    Function TextureBrush(image As Image, ByVal wrapmode As GpWrapMode, dstRect As GpRectF) As TextureBrush
        Return New TextureBrush(image, wrapmode, dstRect)
    End Function
    
    Function TextureBrush(image As Image, ByVal wrapmode As GpWrapMode, dstRect As GpRect) As TextureBrush
        Return New TextureBrush(image, wrapmode, dstRect)
    End Function
    
    Function TextureBrush(image As Image, dstRect As GpRectF, Optional imageAttributes As ImageAttributes) As TextureBrush
        Return New TextureBrush(image, dstRect, imageAttributes)
    End Function
    
    Function TextureBrush(image As Image, dstRect As GpRect, Optional imageAttributes As ImageAttributes) As TextureBrush
        Return New TextureBrush(image, dstRect, imageAttributes)
    End Function
    
    Function TextureBrush(image As Image, ByVal wrapmode As GpWrapMode, ByVal dstX!, ByVal dstY!, ByVal dstWidth!, ByVal dstHeight!) As TextureBrush
        Return New TextureBrush(image, wrapmode, dstX, dstY, dstWidth, dstHeight)
    End Function
    
    Function TextureBrush(image As Image, ByVal wrapmode As GpWrapMode, ByVal dstX&, ByVal dstY&, ByVal dstWidth&, ByVal dstHeight&) As TextureBrush
        Return New TextureBrush(image, wrapmode, dstX, dstY, dstWidth, dstHeight)
    End Function
    
    Type GpTexture
        'Inherits GpBrush
        Native As LongPtr
    End Type
    
    Function GpTexture() As GpTexture
        GpTexture.Native = 0
    End Function
    
    Function GpTexture(nativeBrush As GpBrush) As GpTexture
        GpTexture.Native = nativeBrush.Native
    End Function
    
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipCreateTexture Lib "gdiplus" (ByVal Image As GpImage, ByVal wrapMode As GpWrapMode, texture As GpTexture) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipCreateTexture2 Lib "gdiplus" (ByVal Image As GpImage, ByVal wrapMode As GpWrapMode, ByVal x!, ByVal y!, ByVal width!, ByVal height!, texture As GpTexture) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipCreateTexture2I Lib "gdiplus" (ByVal Image As GpImage, ByVal wrapMode As GpWrapMode, ByVal x&, ByVal y&, ByVal width&, ByVal height&, texture As GpTexture) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipCreateTextureIA Lib "gdiplus" (ByVal Image As GpImage, ByVal imageAttributes As GpImageAttributes, ByVal x!, ByVal y!, ByVal width!, ByVal height!, texture As GpTexture) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipCreateTextureIAI Lib "gdiplus" (ByVal Image As GpImage, ByVal imageAttributes As GpImageAttributes, ByVal x&, ByVal y&, ByVal width&, ByVal height&, texture As GpTexture) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipSetTextureTransform Lib "gdiplus" (ByVal Brush As GpBrush, ByVal matrix As GpMatrix) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipGetTextureTransform Lib "gdiplus" (ByVal Brush As GpBrush, ByVal matrix As GpMatrix) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipResetTextureTransform Lib "gdiplus" (ByVal Brush As GpBrush) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipMultiplyTextureTransform Lib "gdiplus" (ByVal Brush As GpBrush, ByVal matrix As GpMatrix, ByVal order As GpMatrixOrder) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipTranslateTextureTransform Lib "gdiplus" (ByVal Brush As GpBrush, ByVal Dx!, ByVal Dy!, ByVal order As GpMatrixOrder) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipScaleTextureTransform Lib "gdiplus" (ByVal Brush As GpBrush, ByVal sx!, ByVal sy!, ByVal order As GpMatrixOrder) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipRotateTextureTransform Lib "gdiplus" (ByVal Brush As GpBrush, ByVal angle!, ByVal order As GpMatrixOrder) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipSetTextureWrapMode Lib "gdiplus" (ByVal Brush As GpBrush, ByVal wrapMode As GpWrapMode) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipGetTextureWrapMode Lib "gdiplus" (ByVal Brush As GpBrush, wrapMode As GpWrapMode) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipGetTextureImage Lib "gdiplus" (ByVal Brush As GpBrush, Image As GpImage) As GpStatus
End Module
