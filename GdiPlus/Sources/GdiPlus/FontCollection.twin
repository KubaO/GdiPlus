' A part of the GDI+ Package for TwinBasic  https://github.com/kubao/GdiPlus
' Licensed under the MIT license. (c) 2026 Sunderland Ober Consulting.
' "Windows" is a trademark of the Microsoft Corporation.
' Modeled after code and descriptions (c) Microsoft, taken from SDK headers and official documentation.
' Certain flat API function and type declarations taken from WinDevLib by fafalone, with thanks.

[COMCreatable(False)]
Class FontCollection
    Inherits GdiPlusBase
    Protected fc As GpFontCollection
   
    Sub New()
        ' empty by design
    End Sub
    
    Overridable Sub Class_Terminate()
        ' empty by design
    End Sub
    
    Property Get FamilyCount() As Long
        SetStatus(fc.GetFamilyCount(FamilyCount&))
    End Property
    
    Function Families() As FontFamily()
        Dim result() As FontFamily
        Dim numSought& = FamilyCount
        Dim numFound& = 0
        Dim nativeFamilies() As GpFontFamily
        ReDim nativeFamilies(1 To numSought)
        SetStatus(fc.GetFamilyList(numSought, nativeFamilies(1), numFound))
        If LastResult = Ok Then
            ReDim result(1 To numFound)
            For i As Integer = 1 To numFound
                Set result(i) = New FontFamily(nativeFamilies(i))
            Next
            Return result
        End If
    End Function
    
    Protected Property Get Native() As GpFontCollection
        Return fc
    End Property
    
    Protected Sub New(ByVal nativeCollection As GpFontCollection)
        SetNativeFontCollection(nativeCollection)
    End Sub
    
    Protected Sub SetNativeFontCollection(ByVal nativeCollection As GpFontCollection)
        fc = nativeCollection
    End Sub
End Class

Module GdipFontCollection
    Type GpFontCollection
        Native As LongPtr
        
        [UseGetLastError(False)]
        Public Declare PtrSafe Function GetFamilyCount Lib "gdiplus" Alias "GdipGetFontCollectionFamilyCount" (ByVal Me As GpFontCollection, numFound As Long) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function GetFamilyList Lib "gdiplus" Alias "GdipGetFontCollectionFamilyList" (ByVal Me As GpFontCollection, ByVal numSought As Long, gpfamilies As GpFontFamily, numFound As Long) As GpStatus
    End Type
    
    Function GpFontCollection() As GpFontCollection
        GpFontCollection.Native = 0
    End Function
End Module
