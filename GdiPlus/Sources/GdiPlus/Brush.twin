' A part of the GDI+ Package for TwinBasic  https://github.com/kubao/GdiPlus
' Licensed under the MIT license. (c) 2026 Sunderland Ober Consulting.
' "Windows" is a trademark of the Microsoft Corporation.
' Modeled after code and descriptions (c) Microsoft, taken from SDK headers and official documentation.
' Certain flat API function and type declarations taken from WinDevLib by fafalone, with thanks.

[COMCreatable(False)]
[Description("Abstract base class for various brush types")]
Class Brush
    Inherits GdiPlusBase
    Protected br As GpBrush
        
    Sub Class_Terminate()
        br.Delete
    End Sub
    
    Overridable Function Clone() As Brush
        Dim brush As GpBrush
        SetStatus(br.Clone(brush))
        If LastResult = Ok Then Set Clone = New Brush(brush, LastResult)
    End Function
    
    Property Get Type() As GpBrushType
        Type = -1
        SetStatus(br.GetType(Type))
    End Property
    
    Protected Sub New()
        SetStatus(NotImplemented)
    End Sub
    
    ' Used by Pen
    Public Sub New(ByVal nativeBrush As GpBrush, ByVal status As GpStatus)
        SetStatus(status)
        SetNativeBrush(nativeBrush)
    End Sub
    
    Protected Sub New(ByVal nativeBrush As GpBrush)
        SetNativeBrush(nativeBrush)
    End Sub
    
    Protected Property Get Native() As GpBrush
        Return br
    End Property
    
    Protected Sub SetNativeBrush(ByVal nativeBrush As GpBrush)
        br = nativeBrush
    End Sub
End Class

Module GdipBrush
    Type GpBrush
        Native As LongPtr
        
        Function Release() As GpBrush
            Release.Native = Me.Native
            Me.Native = 0
        End Function
        
        [UseGetLastError(False)]
        Declare PtrSafe Function Delete Lib "gdiplus" Alias "GdipDeleteBrush" (ByVal Me As GpBrush) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function Clone Lib "gdiplus" Alias "GdipCloneBrush" (ByVal Me As GpBrush, cloneBrush As GpBrush) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function GetType Lib "gdiplus" Alias "GdipGetBrushType" (ByVal Me As GpBrush, type As GpBrushType) As GpStatus
    End Type
    
    Function GpBrush() As GpBrush
        GpBrush.Native = 0
    End Function
End Module
