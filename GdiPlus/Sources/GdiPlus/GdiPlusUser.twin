' A part of the GDI+ Package for TwinBasic  https://github.com/kubao/GdiPlus
' Licensed under the MIT license. (c) 2026 Sunderland Ober Consulting.
' "Windows" is a trademark of the Microsoft Corporation.
' Modeled after code and descriptions (c) Microsoft, taken from SDK headers and official documentation.
' Certain flat API function and type declarations taken from WinDevLib by fafalone, with thanks.

[COMCreatable(False)]
Class GdiPlusUser
    Inherits GdiPlusBase

    Sub New()
        gdiPlusRefCount += 1
        If gdiPlusRefCount = 1 Or gdiPlusToken.value = -1 Then
            Dim gsi As GdiplusStartupInput
            gsi.GdiplusVersion = 1
            SetStatus(gdiPlusToken.Startup(gsi, 0))
        End If
    End Sub
    
    Sub Class_Terminate()
        gdiPlusRefCount -= 1
        If gdiPlusRefCount <= 0 And gdiPlusToken.value <> -1 Then
            Set GenericSansSerifFF = Nothing
            Set GenericSerifFF = Nothing
            Set GenericMonospaceFF = Nothing
            SetStatus(gdiPlusToken.Shutdown())
            gdiPlusToken.value = -1
            gdiPlusRefCount = 0
        End If
    End Sub
End Class

Module GdipGdiPlusUser
    Function GdiPlusUser() As GdiPlusUser
        Return New GdiPlusUser
    End Function
    
    Public Type GdiplusStartupInput
        GdiplusVersion As Long
        DebugEventCallback As LongPtr
        SuppressBackgroundThread As GDIP_BOOL
        SuppressExternalCodecs As GDIP_BOOL
    End Type
    
    Type GpToken
        value As LongPtr
        
        Sub Type_Initialize()
            Me.value = -1
        End Sub
        
        Sub Release()
            If Me.value <> -1 Then Me.Shutdown
        End Sub
        
        [UseGetLastError(False)]
        Declare PtrSafe Function Startup Lib "gdiplus" Alias "GdiplusStartup" (Me As GpToken, lInput As GdiplusStartupInput, Optional ByVal lOutPut As LongPtr = 0) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function Shutdown Lib "gdiplus" Alias "GdiplusShutdown" (ByVal Me As GpToken) As GpStatus
    End Type
End Module
