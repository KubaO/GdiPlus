' A part of the GDI+ Package for TwinBasic  https://github.com/kubao/GdiPlus
' Licensed under the MIT license. (c) 2026 Sunderland Ober Consulting.
' "Windows" is a trademark of the Microsoft Corporation.
' Modeled after code and descriptions (c) Microsoft, taken from SDK headers and official documentation.
' Certain flat API function and type declarations taken from WinDevLib by fafalone, with thanks.

Module GdipMetaHeader
    ' Placeable WMFs

    ' Placeable Metafiles were created as a non-standard way of specifying how 
    ' a metafile is mapped and scaled on an output device.
    ' Placeable metafiles are quite wide-spread, but not directly supported by
    ' the Windows API. To playback a placeable metafile using the Windows API,
    ' you will first need to strip the placeable metafile header from the file.
    ' This is typically performed by copying the metafile to a temporary file
    ' starting at file offset 22 (0x16). The contents of the temporary file may
    ' then be used as input to the Windows GetMetaFile(), PlayMetaFile(),
    ' CopyMetaFile(), etc. GDI functions.

    ' Each placeable metafile begins with a 22-byte header,
    '  followed by a standard metafile:
    
     [PackingAlignment(2)]
    Public Type PWMFRect16
        Left As Integer
        Top As Integer
        Right As Integer
        Bottom As Integer
    End Type
    
     [PackingAlignment(2)]
     Public Type WmfPlaceableFileHeader
         Key As Long                ' GDIP_WMF_PLACEABLEKEY
         Hmf As Integer             ' Metafile HANDLE number (always 0)
         BoundingBox As PWMFRect16  ' Coordinates in metafile units
         Inch As Integer            ' Number of metafile units per inch
         Reserved As Long           ' Reserved (always 0)
         Checksum As Integer        ' Checksum value for previous 10 WORDs
     End Type

     ' Key contains a special identification value that indicates the presence
     ' of a placeable metafile header and is always 0x9AC6CDD7.

     ' Handle is used to stored the handle of the metafile in memory. When written
     ' to disk, this field is not used and will always contains the value 0.

     ' Left, Top, Right, and Bottom contain the coordinates of the upper-left
     ' and lower-right corners of the image on the output device. These are
     ' measured in twips.

     ' A twip (meaning "twentieth of a point") is the logical unit of measurement
     ' used in Windows Metafiles. A twip is equal to 1/1440 of an inch. Thus 720
     ' twips equal 1/2 inch, while 32,768 twips is 22.75 inches.

     ' Inch contains the number of twips per inch used to represent the image.
     ' Normally, there are 1440 twips per inch; however, this number may be
     ' changed to scale the image. A value of 720 indicates that the image is
     ' double its normal size, or scaled to a factor of 2:1. A value of 360
     ' indicates a scale of 4:1, while a value of 2880 indicates that the image
     ' is scaled down in size by a factor of two. A value of 1440 indicates
     ' a 1:1 scale ratio.

     ' Reserved is not used and is always set to 0.

     ' Checksum contains a checksum value for the previous 10 WORDs in the header.
     ' This value can be used in an attempt to detect if the metafile has become
     ' corrupted. The checksum is calculated by XORing each WORD value to an
     ' initial value of 0.

     ' If the metafile was recorded with a reference Hdc that was a display.
     
     Public Const GDIP_EMFPLUSFLAGS_DISPLAY = &h00000001
     
     Type MetafileHeader
        Type As GpMetafileType
        Size As UINT               ' Size of the metafile (in bytes)
        Version As UINT            ' EMF+, EMF, or WMF version
        EmfPlusFlags As UINT       ' EMF+ flags associated with the metafile
        DpiX!
        DpiY!
        X&                          ' Bounds in device units
        Y&
        Width&
        Height&
        HeaderSpace As GDIP_ENHMETAHEADER3
        EmfPlusHeaderSize&  ' size of the EMF+ header in file
        LogicalDpiX&        ' Logical Dpi of reference Hdc
        LogicalDpiY&        ' usually valid only for EMF+
        
        Property Get Bounds() As GpRect
            Bounds.X = Me.X
            Bounds.Y = Me.Y
            Bounds.Width = Me.Width
            Bounds.Height = Me.Height
        End Property
            
        [Description("Is it any type of WMF (standard or Placeable Metafile)?")]
        Property Get IsWmf() As Boolean
            Return (Me.Type = MetafileTypeWmf) OrElse (Me.Type = MetafileTypeWmfPlaceable)
        End Property
        
        [Description("Is this an Placeable Metafile?")]
        Property Get IsWmfPlaceable() As Boolean
            Return Me.Type = MetafileTypeWmfPlaceable
        End Property
        
        [Description("Is this an EMF (not an EMF+)?")]
        Property Get IsEmf() As Boolean
            Return Me.Type = MetafileTypeEmf
        End Property
        
        [Description("Is this an EMF or EMF+ file?")]
        Property Get IsEmfOrEmfPlus() As Boolean
            Return Me.Type >= MetafileTypeEmf
        End Property
        
        [Description("Is this an EMF+ file?")]
        Property Get IsEmfPlus() As Boolean
            Return Me.Type >= MetafileTypeEmfPlusOnly
        End Property
        
        [Description("Is this an EMF+ dual (has dual, down-level records) file?")]
        Property Get IsEmfPlusDual() As Boolean
            Return Me.Type = MetafileTypeEmfPlusDual
        End Property
        
        [Description("Is this an EMF+ only (no dual records) file?")]
        Property Get IsEmfPlusOnly() As Boolean
            Return Me.Type = MetafileTypeEmfPlusOnly
        End Property
      
        [Description("If it's an EMF+ file, was it recorded against a display Hdc?")]
        Property Get IsDisplay() As Boolean
            Return Me.IsEmfPlus AndAlso (Me.EmfPlusFlags & GDIP_EMFPLUSFLAGS_DISPLAY) <> 0
        End Property
        
        [Description("Get the WMF header of the metafile (if it is a WMF)")]
        Property Get WmfHeader() As GDIP_METAHEADER
            If Me.IsWmf Then
                vbaCopyBytesRef(LenB(Of GDIP_METAHEADER), WmfHeader, Me.HeaderSpace)
            End If
        End Property
      
        [Description("Get the EMF header of the metafile (if it is an EMF)")]
        Property Get EmfHeader() As GDIP_ENHMETAHEADER3
            If Me.IsEmfOrEmfPlus Then EmfHeader = Me.HeaderSpace
        End Property
     End Type
End Module
