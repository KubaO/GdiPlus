' A part of the GDI+ Package for TwinBasic  https://github.com/kubao/GdiPlus
' Licensed under the MIT license. (c) 2026 Sunderland Ober Consulting.
' "Windows" is a trademark of the Microsoft Corporation.
' Modeled after code and descriptions (c) Microsoft, taken from SDK headers and official documentation.
' Certain flat API function and type declarations taken from WinDevLib by fafalone, with thanks.

[COMCreatable(False)]
Class Region
    Inherits GdiPlusBase
    Dim rg As GpRegion
    
    Sub New()
        LastStatus = rg.CreateInto(rg)
    End Sub
    
    Sub New(rect As GpRectF)
        LastStatus = rg.CreateRectInto(rect, rg)
    End Sub

    Sub New(rect As GpRect)
        LastStatus = rg.CreateRectIntoI(rect, rg)
    End Sub

    Sub New(path As GraphicsPath)
        LastStatus = rg.CreatePathInto(GetNative(path), rg)
    End Sub
    
    Sub New(regionData As Byte, ByVal size&)
        LastStatus = rg.CreateRgnDataInto(regionData, size, rg)
    End Sub
    
    Sub New(ByVal hRgn As LongPtr)
        LastStatus = rg.CreateHrgnInto(hRgn, rg)
    End Sub

    Sub Class_Terminate()
        rg.Delete
    End Sub
    
    Function Clone() As Region
        Dim nativeClone As GpRegion
        SetStatus(rg.Clone(nativeClone))
        If LastResult = Ok Then Set Clone = New Region(nativeClone)
    End Function
    
    Function MakeInfinite() As GpStatus
        Return SetStatus(rg.SetInfinite())
    End Function

    Function MakeEmpty() As GpStatus
        Return SetStatus(rg.SetEmpty())
    End Function
    
    Function GetDataSize() As UINT
        SetStatus(rg.GetDataSize(GetDataSize))
    End Function
    
    ' buffer     - where to put the data
    ' bufferSize - how big the buffer is (should be at least as big as GetDataSize())
    ' sizeFilled - if not NULL, this is an OUT param that says how many bytes
    '              of data were written to the buffer.    
    Function GetData(buffer As Byte, ByVal bufferSize As UINT, Optional sizeFilled&) As GpStatus
        Return SetStatus(rg.GetData(buffer, bufferSize, sizeFilled))
    End Function
    
    Function Intersect(rect As GpRect) As GpStatus
        Return SetStatus(rg.CombineRectI(rect, CombineModeIntersect))
    End Function
    
    Function Intersect(rect As GpRectF) As GpStatus
        Return SetStatus(rg.CombineRect(rect, CombineModeIntersect))
    End Function
    
    Function Intersect(path As GraphicsPath) As GpStatus
        Return SetStatus(rg.CombinePath(GetNative(path), CombineModeIntersect))
    End Function
    
    Function Intersect(region As Region) As GpStatus
        Return SetStatus(rg.CombineRegion(GetNative(region), CombineModeIntersect))
    End Function
    
    Function Union(rect As GpRect) As GpStatus
        Return SetStatus(rg.CombineRectI(rect, CombineModeUnion))
    End Function
    
    Function Union(rect As GpRectF) As GpStatus
        Return SetStatus(rg.CombineRect(rect, CombineModeUnion))
    End Function
    
    Function Union(path As GraphicsPath) As GpStatus
        Return SetStatus(rg.CombinePath(GetNative(path), CombineModeUnion))
    End Function
    
    Function Union(region As Region) As GpStatus
        Return SetStatus(rg.CombineRegion(GetNative(region), CombineModeUnion))
    End Function
    
    Function Xor(rect As GpRect) As GpStatus
        Return SetStatus(rg.CombineRectI(rect, CombineModeXor))
    End Function
    
    Function Xor(rect As GpRectF) As GpStatus
        Return SetStatus(rg.CombineRect(rect, CombineModeXor))
    End Function
    
    Function Xor(path As GraphicsPath) As GpStatus
        Return SetStatus(rg.CombinePath(GetNative(path), CombineModeXor))
    End Function
    
    Function Xor(region As Region) As GpStatus
        Return SetStatus(rg.CombineRegion(GetNative(region), CombineModeXor))
    End Function
    
    Function Exclude(rect As GpRect) As GpStatus
        Return SetStatus(rg.CombineRectI(rect, CombineModeExclude))
    End Function
    
    Function Exclude(rect As GpRectF) As GpStatus
        Return SetStatus(rg.CombineRect(rect, CombineModeExclude))
    End Function
    
    Function Exclude(path As GraphicsPath) As GpStatus
        Return SetStatus(rg.CombinePath(GetNative(path), CombineModeExclude))
    End Function
    
    Function Exclude(region As Region) As GpStatus
        Return SetStatus(rg.CombineRegion(GetNative(region), CombineModeExclude))
    End Function
    
    Function Complement(rect As GpRect) As GpStatus
        Return SetStatus(rg.CombineRectI(rect, CombineModeComplement))
    End Function
    
    Function Complement(rect As GpRectF) As GpStatus
        Return SetStatus(rg.CombineRect(rect, CombineModeComplement))
    End Function
    
    Function Complement(path As GraphicsPath) As GpStatus
        Return SetStatus(rg.CombinePath(GetNative(path), CombineModeComplement))
    End Function
    
    Function Complement(region As Region) As GpStatus
        Return SetStatus(rg.CombineRegion(GetNative(region), CombineModeComplement))
    End Function
    
    Function Translate(ByVal dx!, ByVal dy!) As GpStatus
        Return SetStatus(rg.Translate(dx, dy))
    End Function

    Function TranslateI(ByVal dx&, ByVal dy&) As GpStatus
        Return SetStatus(rg.TranslateI(dx, dy))
    End Function

    Function Transform(matrix As Matrix) As GpStatus
        Return SetStatus(rg.Transform(GetNative(matrix)))
    End Function
        
    Property Get Bounds(g As Graphics) As GpRectF
        SetStatus(rg.GetBounds(GetNative(g), Bounds))
    End Property
    
    Property Get BoundsI(g As Graphics) As GpRect
        SetStatus(rg.GetBoundsI(GetNative(g), BoundsI))
    End Property
        
    Function GetHRGN(g As Graphics) As LongPtr
        SetStatus(rg.GetHRgn(GetNative(g), GetHRGN))
    End Function
    
    Function IsEmpty(g As Graphics) As Boolean
        Dim booln As GDIP_BOOL
        SetStatus(rg.IsEmpty(GetNative(g), booln))
        Return booln
    End Function
    
    Function IsInfinite(g As Graphics) As Boolean
        Dim booln As GDIP_BOOL
        SetStatus(rg.IsInfinite(GetNative(g), booln))
        Return booln
    End Function

    Function IsVisibleI(ByVal x&, ByVal y&, g As Graphics) As Boolean
        Dim booln As GDIP_BOOL
        SetStatus(rg.IsVisiblePointI(x, y, GetNative(g), booln))
        Return booln
    End Function

    Function IsVisible(point As GpPoint, g As Graphics) As Boolean
        Return IsVisible(point.X, point.Y, g)
    End Function

    Function IsVisible(ByVal x!, ByVal y!, g As Graphics) As Boolean
        Dim booln As GDIP_BOOL
        SetStatus(rg.IsVisiblePoint(x, y, GetNative(g), booln))
        Return booln
    End Function

    Function IsVisible(point As GpPointF, g As Graphics) As Boolean
        Return IsVisible(point.X, point.Y, g)
    End Function
        
    Function IsVisibleI(ByVal x&, ByVal y&, ByVal width&, ByVal height&, g As Graphics) As Boolean
        Dim booln As GDIP_BOOL
        SetStatus(rg.IsVisibleRect(x, y, width, height, GetNative(g), booln))
        Return booln
    End Function
    
    Function IsVisible(rect As GpRect, g As Graphics) As Boolean
        Return IsVisible(rect.X, rect.Y, rect.Width, rect.Height, g)
    End Function
        
    Function IsVisible(ByVal x!, ByVal y!, ByVal width!, ByVal height!, g As Graphics) As Boolean
        Dim booln As GDIP_BOOL
        SetStatus(rg.IsVisibleRectI(x, y, width, height, GetNative(g), booln))
        Return booln
    End Function
    
    Function IsVisible(rect As GpRectF, g As Graphics) As Boolean
        Return IsVisible(rect.X, rect.Y, rect.Width, rect.Height, g)
    End Function
    
    Function Equals(region As Region, g As Graphics) As Boolean
        Dim booln As GDIP_BOOL
        SetStatus(rg.IsEqualTo(GetNative(region), GetNative(g), booln))
        Return booln
    End Function
    
    Function GetRegionScansCount(matrix As Matrix) As UINT
        SetStatus(rg.GetScansCount(GetRegionScansCount, GetNative(matrix)))
    End Function
    
    ' If rects is NULL, return the count of rects in the region.
    ' Otherwise, assume rects is big enough to hold all the region rects
    ' and fill them in and return the number of rects filled in.
    ' The rects are returned in the units specified by the matrix
    ' (which is typically a world-to-device transform).
    ' Note that the number of rects returned can vary, depending on the
    ' matrix that is used.
    Function GetRegionScans(matrix As Matrix, rects As GpRectF, count&) As GpStatus
        Return SetStatus(rg.GetScans(rects, count, GetNative(matrix)))
    End Function
    
    Function GetRegionScans(matrix As Matrix, rects As GpRect, count&) As GpStatus
        Return SetStatus(rg.GetScansI(rects, count, GetNative(matrix)))
    End Function
    
    Public Sub New(ByVal nativeRegion As GpRegion)
        SetNativeRegion(nativeRegion)
    End Sub
    
    Protected Property Get Native() As GpRegion
        Return rg
    End Property
        
    Protected Sub SetNativeRegion(ByVal nativeRegion As GpRegion)
        rg = nativeRegion
    End Sub
End Class

Module GdipRegion
    Function Region() As Region
        Return New Region
    End Function
    
    Function Region(rect As GpRectF) As Region
        Return New Region(rect)
    End Function

    Function Region(rect As GpRect) As Region
        Return New Region(rect)
    End Function

    Function Region(path As GraphicsPath) As Region
        Return New Region(path)
    End Function
    
    Function Region(regionData As Byte, ByVal size&) As Region
        Return New Region(regionData, size)
    End Function
    
    Function Region(ByVal hRgn As LongPtr) As Region
        Return New Region(hRgn)
    End Function
    
    Type GpRegion
        Native As LongPtr
        
        [UseGetLastError(False)]
        Public Declare PtrSafe Function CreateInto Lib "gdiplus" Alias "GdipCreateRegion" (Region As GpRegion) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function CreateRectInto Lib "gdiplus" Alias "GdipCreateRegionRect" (Rect As GpRectF, Region As GpRegion) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function CreateRectIntoI Lib "gdiplus" Alias "GdipCreateRegionRectI" (Rect As GpRect, Region As GpRegion) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function CreateRgnDataInto Lib "gdiplus" Alias "GdipCreateRegionRgnData" (regionData As Any, ByVal size As Long, Region As GpRegion) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function CreateHrgnInto Lib "gdiplus" Alias "GdipCreateRegionHrgn" (ByVal hRgn As LongPtr, Region As GpRegion) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function CreatePathInto Lib "gdiplus" Alias "GdipCreateRegionPath" (ByVal path As GpPath, Region As GpRegion) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function Clone Lib "gdiplus" Alias "GdipCloneRegion" (ByVal Me As GpRegion, cloneRegion As GpRegion) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function Delete Lib "gdiplus" Alias "GdipDeleteRegion" (ByVal Me As GpRegion) As GpStatus
    
        [UseGetLastError(False)]
        Public Declare PtrSafe Function SetEmpty Lib "gdiplus" Alias "GdipSetEmpty" (ByVal Me As GpRegion) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function Translate Lib "gdiplus" Alias "GdipTranslateRegion" (ByVal Me As GpRegion, ByVal dx As Single, ByVal dy As Single) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function TranslateI Lib "gdiplus" Alias "GdipTranslateRegionI" (ByVal Me As GpRegion, ByVal dx As Long, ByVal dy As Long) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function SetInfinite Lib "gdiplus" Alias "GdipSetInfinite" (ByVal Me As GpRegion) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function GetScansCount Lib "gdiplus" Alias "GdipGetRegionScansCount" (ByVal Me As GpRegion, count As Long, ByVal matrix As GpMatrix) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function GetScans Lib "gdiplus" Alias "GdipGetRegionScans" (ByVal Me As GpRegion, rects As GpRectF, count As Long, ByVal matrix As GpMatrix) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function GetScansI Lib "gdiplus" Alias "GdipGetRegionScansI" (ByVal Me As GpRegion, rects As GpRect, count As Long, ByVal matrix As GpMatrix) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function GetDataSize Lib "gdiplus" Alias "GdipGetRegionDataSize" (ByVal Me As GpRegion, bufferSize As Long) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function GetData Lib "gdiplus" Alias "GdipGetRegionData" (ByVal Me As GpRegion, buffer As Byte, ByVal bufferSize As Long, sizeFilled As Long) As GpStatus
    
        [UseGetLastError(False)]
        Public Declare PtrSafe Function CombineRect Lib "gdiplus" Alias "GdipCombineRegionRect" (ByVal Me As GpRegion, Rect As GpRectF, ByVal combineMode As GpCombineMode) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function CombineRectI Lib "gdiplus" Alias "GdipCombineRegionRectI" (ByVal Me As GpRegion, Rect As GpRect, ByVal combineMode As GpCombineMode) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function CombineRegion Lib "gdiplus" Alias "GdipCombineRegionRegion" (ByVal Me As GpRegion, ByVal region2 As GpRegion, ByVal combineMode As GpCombineMode) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function GetHRgn Lib "gdiplus" Alias "GdipGetRegionHRgn" (ByVal Me As GpRegion, ByVal Graphics As GpGraphics, hRgn As LongPtr) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function IsEmpty Lib "gdiplus" Alias "GdipIsEmptyRegion" (ByVal Me As GpRegion, ByVal Graphics As GpGraphics, result As GDIP_BOOL) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function IsInfinite Lib "gdiplus" Alias "GdipIsInfiniteRegion" (ByVal Me As GpRegion, ByVal Graphics As GpGraphics, result As GDIP_BOOL) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function IsEqualTo Lib "gdiplus" Alias "GdipIsEqualRegion" (ByVal Me As GpRegion, ByVal region2 As GpRegion, ByVal Graphics As GpGraphics, result As GDIP_BOOL) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function CombinePath Lib "gdiplus" Alias "GdipCombineRegionPath" (ByVal Me As GpRegion, ByVal Path As GpPath, ByVal combineMode As GpCombineMode) As GpStatus
        
        [UseGetLastError(False)]
        Public Declare PtrSafe Function Transform Lib "gdiplus" Alias "GdipTransformRegion" (ByVal Me As GpRegion, ByVal matrix As GpMatrix) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function GetBounds Lib "gdiplus" Alias "GdipGetRegionBounds" (ByVal Me As GpRegion, ByVal Graphics As GpGraphics, Rect As GpRectF) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function GetBoundsI Lib "gdiplus" Alias "GdipGetRegionBoundsI" (ByVal Me As GpRegion, ByVal Graphics As GpGraphics, Rect As GpRect) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function IsVisiblePoint Lib "gdiplus" Alias "GdipIsVisibleRegionPoint" (ByVal Me As GpRegion, ByVal X As Single, ByVal Y As Single, ByVal Graphics As GpGraphics, result As GDIP_BOOL) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function IsVisiblePointI Lib "gdiplus" Alias "GdipIsVisibleRegionPointI" (ByVal Me As GpRegion, ByVal X As Long, ByVal Y As Long, ByVal Graphics As GpGraphics, result As GDIP_BOOL) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function IsVisibleRect Lib "gdiplus" Alias "GdipIsVisibleRegionRect" (ByVal Me As GpRegion, ByVal X As Single, ByVal Y As Single, ByVal width As Single, ByVal height As Single, ByVal Graphics As GpGraphics, result As GDIP_BOOL) As GpStatus
        [UseGetLastError(False)]
        Public Declare PtrSafe Function IsVisibleRectI Lib "gdiplus" Alias "GdipIsVisibleRegionRectI" (ByVal Me As GpRegion, ByVal X As Long, ByVal Y As Long, ByVal width As Long, ByVal height As Long, ByVal Graphics As GpGraphics, result As GDIP_BOOL) As GpStatus
    End Type
    
    Function GpRegion() As GpRegion
        GpRegion.Native = 0
    End Function
    
    Function FromHRGN(ByVal hRgn As LongPtr) As Region
        Return New Region(hRgn)
    End Function
End Module
