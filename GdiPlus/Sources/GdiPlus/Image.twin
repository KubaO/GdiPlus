' A part of the GDI+ Package for TwinBasic  https://github.com/kubao/GdiPlus
' Licensed under the MIT license. (c) 2026 Sunderland Ober Consulting.
' "Windows" is a trademark of the Microsoft Corporation.
' Modeled after code and descriptions (c) Microsoft, taken from SDK headers and official documentation.
' Certain flat API function and type declarations taken from WinDevLib by fafalone, with thanks.

[COMCreatable(False)]
[Description("Abstract base class for Image and Metafile")]
Class Image
    Inherits GdiPlusBase
    Protected im As GpImage
    Protected loadStatus As GpStatus
   
    Sub New(ByVal filename$, useEmbeddedColorManagement As Boolean = False)
        If useEmbeddedColorManagement Then
            SetStatus(im.LoadFromFileICMInto(filename, im))
        Else
            SetStatus(im.LoadFromFileInto(filename, im))
        End If
    End Sub

    Sub New(stream As GDIP_IStream, useEmbeddedColorManagement As Boolean = False)
        If useEmbeddedColorManagement Then
            SetStatus(im.LoadFromStreamICMInto(stream, im))
        Else
            SetStatus(im.LoadFromStreamInto(stream, im))
        End If
    End Sub
        
    Function Clone() As Image
        Dim cloneImage As GpImage
        SetStatus(im.Clone(cloneImage))
        If LastResult = Ok Then Return New Image(cloneImage, LastResult)
    End Function
        
    Sub Class_Terminate()
        im.Dispose()
    End Sub
    
    '---------------------------------------------------------------'
    
    [Description("There should be a dummy encoderParams(0) that's not filled in. It's used internally.")]
    Function Save(ByVal filename$, clsidEncoder As GDIP_UUID, encoderParams() As GpEncoderParameter) As GpStatus
        Return SetStatus(im.SaveToFile(filename, clsidEncoder, encoderParams))
    End Function
    
    [Description("There should be a dummy encoderParams(0) that's not filled in. It's used internally.")]
    Function Save(ByVal stream As GDIP_IStream, clsidEncoder As GDIP_UUID, encoderParams() As GpEncoderParameter) As GpStatus
        Return SetStatus(im.SaveToStream(stream, clsidEncoder, encoderParams))
    End Function

    [Description("There should be a dummy encoderParams(0) that's not filled in. It's used internally.")]
    Function SaveAdd(encoderParams() As GpEncoderParameter) As GpStatus
        Return SetStatus(im.SaveAdd(encoderParams))
    End Function

    [Description("There should be a dummy encoderParams(0) that's not filled in. It's used internally.")]
    Function SaveAddImage(newImage As Image, encoderParams() As GpEncoderParameter) As GpStatus
        Return SetStatus(im.SaveAddImage(GetNative(newImage), encoderParams))
    End Function
    
    '---------------------------------------------------------------'
    
    Property Get Type() As GpImageType
        SetStatus(im.GetType(Type))
    End Property
    
    Property Get Dimension() As GpSizeF
        SetStatus(im.GetDimension(Dimension.Width, Dimension.Height))
    End Property
    
    Property Get Bounds(srcUnit As GpUnit) As GpRectF
        SetStatus(im.GetBounds(Bounds, srcUnit))
    End Property
    
    Property Get Width() As UINT
        SetStatus(im.GetWidth(Width))
    End Property
    
    Property Get Height() As UINT
        SetStatus(im.GetHeight(Height))
    End Property
    
    Property Get Size() As GpSize
        SetStatus(im.GetSize(Size))
    End Property
    
    Property Get HorizontalResolution() As Single
        SetStatus(im.GetHorizontalResolution(HorizontalResolution))
    End Property
    
    Property Get VerticalResolution() As Single
        SetStatus(im.GetVerticalResolution(VerticalResolution))
    End Property
    
    Property Get Flags() As UINT
        SetStatus(im.GetFlags(Flags))
    End Property
    
    Property Get RawFormat() As GDIP_UUID ' or GUID
        SetStatus(im.GetRawFormat(RawFormat))
    End Property
    
    Property Get PixelFormat() As PixelFormat
        SetStatus(im.GetPixelFormat(PixelFormat))
    End Property
    
    Property Get PaletteSize() As Long
        SetStatus(im.GetPaletteSize(PaletteSize&))
    End Property
    
    Property Get Palette() As ColorPalette()
        ReDim Palette(1 To PaletteSize)
        If LastResult = Ok Then SetStatus(im.GetPalette(Palette(1), PaletteSize))
    End Property
    
    Property Let Palette(palette() As ColorPalette)
        SetStatus(im.GetPalette(palette(LBound(palette)), ArrayLen(palette)))
    End Property
    
    Function GetPalette(palette As ColorPalette, ByVal count&) As GpStatus
        Return SetStatus(im.GetPalette(palette, count))
    End Function
    
    '---------------------------------------------------------------'    

    Function GetThumbnail(ByVal thumbSize As GpSize, Optional callback As GetThumbnailImageAbort, callbackData As LongPtr = 0) As Image
        Dim thumbImage As GpImage
        SetStatus(im.GetThumbnail(thumbSize, thumbImage, callback, callbackData))
        If LastResult = Ok Then Set GetThumbnail = New Image(thumbImage, LastResult)
    End Function
    
    Function GetThumbnail(ByVal thumbWidth&, ByVal thumbHeight&, Optional callback As GetThumbnailImageAbort, callbackData As LongPtr = 0) As Image
        Dim thumbImage As GpImage
        SetStatus(im.GetThumbnail2(thumbWidth, thumbHeight, thumbImage, callback, callbackData))
        If LastResult = Ok Then Set GetThumbnail = New Image(thumbImage, LastResult)
    End Function

    '---------------------------------------------------------------'
    
    Property Get FrameDimensionsCount() As Long
        SetStatus(im.GetFrameDimensionsCount(FrameDimensionsCount))
    End Property
    
    Function GetFrameDimensionsList() As GDIP_UUID()
        SetStatus(im.GetFrameDimensionsList(GetFrameDimensionsList))
    End Function
        
    Property Get FrameCount(ByVal dimensionID As GDIP_UUID) As UINT
        SetStatus(im.GetFrameCount(dimensionID, FrameCount))
    End Property

    Function SelectActiveFrame(ByVal dimensionID As GDIP_UUID, ByVal frameIndex As UINT) As GpStatus
        Return SetStatus(im.SelectActiveFrame(dimensionID, frameIndex))
    End Function

    Function RotateFlip(ByVal rotateFlipType As RotateFlipType) As GpStatus
        Return SetStatus(im.RotateFlip(rotateFlipType))
    End Function

    '---------------------------------------------------------------'
    
    Property Get PropertyCount() As UINT
        SetStatus(im.GetPropertyCount(PropertyCount))
    End Property
    
    Property Get PropertyIdList() As PROPID()
        SetStatus(im.GetPropertyIdList(PropertyIdList))
    End Property
    
    [Description("Returns a single property item at array index 1.")]
    Property Get PropertyItem(ByVal propid As PROPID) As GpPropertyItem()
        SetStatus(im.GetPropertyItem(propid, PropertyItem))
    End Property

    Property Let PropertyItem(item As GpPropertyItem)
        SetStatus(im.SetPropertyItem(item))
    End Property

    Function RemovePropertyItem(ByVal propId As PROPID) As GpStatus
        Return SetStatus(im.RemovePropertyItem(propId))
    End Function
    
    Function GetAllPropertyItems() As GpPropertyItem()
        SetStatus(im.GetAllPropertyItems(GetAllPropertyItems))
    End Function
        
    Function GetEncoderParameterList(clsidEncoder As GDIP_UUID) As GpEncoderParameter()
        SetStatus(im.GetEncoderParameterList(clsidEncoder, GetEncoderParameterList))
    End Function

    '#if (GDIPVER >= 0x0110)
    [Description("Returns the first item wrapped in a length-1 array.")]
    Function FindFirstItem() As GpImageItemData()
        SetStatus(im.FindFirstItem(FindFirstItem))
    End Function
    
    [Description("Returns the next item wrapped in a length-1 array.")]
    Function FindNextItem() As GpImageItemData()
        SetStatus(im.FindNextItem(FindNextItem))
    End Function
    
    [Description("Populates the data of an item wrapped in a length-1 array.")]
    Function GetItemData(item() As GpImageItemData) As GpStatus
        Return SetStatus(im.GetItemData(item))
    End Function
    
    [Description("⛔This API is not implemented in GdiPlus 1.0 nor 1.1.")]
    Function SetAbort(pIAbort As GdiPlusAbort) As GpStatus
        Return SetStatus(im.SetAbort(pIAbort))
    End Function
    '#endif //(GDIPVER >= 0x0110)  
    
    Protected Sub New()
    End Sub
    
    Sub New(ByVal nativeImage As GpImage, ByVal status As GpStatus)
        SetStatus(status)
        im = nativeImage
    End Sub
    
    Protected Property Get Native() As GpImage
        Return im
    End Property
    
    Protected Sub SetNativeImage(ByVal nativeImage As GpImage)
        im.Dispose()
        im.Native = 0
        im = nativeImage
    End Sub
End Class

Alias PROPID As Long

Module GdipImage
    Function Image(ByVal filename$, useEmbeddedColorManagement As Boolean = False) As Image
        Return New Image(filename, useEmbeddedColorManagement)
    End Function

    Function Image(stream As GDIP_IStream, useEmbeddedColorManagement As Boolean = False) As Image
        Return New Image(stream, useEmbeddedColorManagement)
    End Function
    
    Function FromFile(ByVal filename$, useEmbeddedColorManagement As Boolean = False) As Image
        Return New Image(filename, useEmbeddedColorManagement)
    End Function
    
    Function FromFile(ByVal stream As GDIP_IStream, useEmbeddedColorManagement As Boolean = False) As Image
        Return New Image(stream, useEmbeddedColorManagement)
    End Function
    
    Type GpImage
        Native As LongPtr
        
        [UseGetLastError(False)]
        DeclareWide PtrSafe Function LoadFromFileInto Lib "gdiplus" Alias "GdipLoadImageFromFile" (ByVal FileName$, ByRef Image As GpImage) As GpStatus
        [UseGetLastError(False)]
        DeclareWide PtrSafe Function LoadFromFileICMInto Lib "gdiplus" Alias "GdipLoadImageFromFileICM" (ByVal FileName$, ByRef Image As GpImage) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function LoadFromStreamInto Lib "gdiplus" Alias "GdipLoadImageFromStream" (ByVal stream As GDIP_IStream, ByRef Image As GpImage) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function LoadFromStreamICMInto Lib "gdiplus" Alias "GdipLoadImageFromStreamICM" (ByVal stream As GDIP_IStream, ByRef Image As GpImage) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function Clone Lib "gdiplus" Alias "GdipCloneImage" (ByVal Me As GpImage, cloneImage As GpImage) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function Dispose Lib "gdiplus" Alias "GdipDisposeImage" (ByVal Me As GpImage) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function Delete Lib "gdiplus" Alias "GdipDisposeImage" (ByVal Me As GpImage) As GpStatus

        [Description("There should be a dummy encoderParams(0) that's not filled in. It's used internally.")]
        Function SaveToFile(ByVal FileName$, ByVal clsidEncoder As GDIP_UUID, encoderParams() As GpEncoderParameter) As GpStatus
            Return SaveToFileImpl(Me.Native, FileName, clsidEncoder, encoderParams)
        End Function
        
        [Description("There should be a dummy encoderParams(0) that's not filled in. It's used internally.")]
        Function SaveToStream(ByVal stream As GDIP_IStream, ByVal clsidEncoder As GDIP_UUID, encoderParams() As GpEncoderParameter) As GpStatus
            Return SaveToStreamImpl(Me.Native, stream, clsidEncoder, encoderParams)
        End Function
        
        [Description("There should be a dummy encoderParams(0) that's not filled in. It's used internally.")]
        Function SaveAdd(encoderParams() As GpEncoderParameter) As GpStatus
            Return SaveAddImpl(Me.Native, encoderParams)
        End Function

        [Description("There should be a dummy encoderParams(0) that's not filled in. It's used internally.")]
        Function SaveAddImage(ByVal newImage As GpImage, encoderParams() As GpEncoderParameter) As GpStatus
            Return SaveAddImageImpl(Me.Native, newImage.Native, encoderParams)
        End Function
        
        [UseGetLastError(False)]
        Declare PtrSafe Function GetType Lib "gdiplus" Alias "GdipGetImageType" (ByVal Me As GpImage, type As GpImageType) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function GetDimension Lib "gdiplus" Alias "GdipGetImageDimension" (ByVal Me As GpImage, ByRef Width!, ByRef Height!) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function GetBounds Lib "gdiplus" Alias "GdipGetImageBounds" (ByVal Me As GpImage, srcRect As GpRectF, srcUnit As GpUnit) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function GetHeight Lib "gdiplus" Alias "GdipGetImageHeight" (ByVal Me As GpImage, Height As Long) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function GetWidth Lib "gdiplus" Alias "GdipGetImageWidth" (ByVal Me As GpImage, Width As Long) As GpStatus
        
        Function GetSize(size As GpSize) As GpStatus
            Return GetSizeImpl(Me, size)
        End Function
    
        [UseGetLastError(False)]
        Declare PtrSafe Function GetHorizontalResolution Lib "gdiplus" Alias "GdipGetImageHorizontalResolution" (ByVal Me As GpImage, resolution!) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function GetVerticalResolution Lib "gdiplus" Alias "GdipGetImageVerticalResolution" (ByVal Me As GpImage, resolution!) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function GetFlags Lib "gdiplus" Alias "GdipGetImageFlags" (ByVal Me As GpImage, flags&) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function GetRawFormat Lib "gdiplus" Alias "GdipGetImageRawFormat" (ByVal Me As GpImage, Format As GDIP_UUID) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function GetPixelFormat Lib "gdiplus" Alias "GdipGetImagePixelFormat" (ByVal Me As GpImage, format As PixelFormat) As GpStatus
        
        [UseGetLastError(False)]
        Declare PtrSafe Function GetPaletteSize Lib "gdiplus" Alias "GdipGetImagePaletteSize" (ByVal Me As GpImage, size As Long) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function GetPalette Lib "gdiplus" Alias "GdipGetImagePalette" (ByVal Me As GpImage, palette As ColorPalette, ByVal size&) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function SetPalette Lib "gdiplus" Alias "GdipSetImagePalette" (ByVal Me As GpImage, palette As ColorPalette) As GpStatus
        
        Function GetThumbnail(ByVal thumbSize As GpSize, thumbImage As GpImage, ByVal callback As LongPtr, callbackData As LongPtr) As GpStatus
            Return Me.GetThumbnail2(thumbSize.Width, thumbSize.Height, thumbImage, callback, callbackData)
        End Function
        [UseGetLastError(False)]
        Declare PtrSafe Function GetThumbnail2 Lib "gdiplus" Alias "GdipGetImageThumbnail" (ByVal Me As GpImage, ByVal thumbWidth&, ByVal thumbHeight&, thumbImage As GpImage, ByVal callback As LongPtr, callbackData As Any) As GpStatus
        
        Function GetFrameDimensionsList(dims As GDIP_UUID()) As GpStatus
            Return GetFrameDimensionsListImpl(Me.Native, dims)
        End Function
        [UseGetLastError(False)]
        Declare PtrSafe Function GetFrameDimensionsCount Lib "gdiplus" Alias "GdipImageGetFrameDimensionsCount" (ByVal Me As GpImage, count&) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function GetFrameCount Lib "gdiplus" Alias "GdipImageGetFrameCount" (ByVal Me As GpImage, dimensionID As GDIP_UUID, count&) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function SelectActiveFrame Lib "gdiplus" Alias "GdipImageSelectActiveFrame" (ByVal Me As GpImage, ByRef dimensionID As GDIP_UUID, ByVal frameIndex&) As GpStatus

        [UseGetLastError(False)]
        Declare PtrSafe Function RotateFlip Lib "gdiplus" Alias "GdipImageRotateFlip" (ByVal Me As GpImage, ByVal rfType As RotateFlipType) As GpStatus
    
        Function GetPropertyIdList(propIDs() As PROPID) As GpStatus
            Return GetPropertyIdListImpl(Me.Native, propIDs)
        End Function
        [UseGetLastError(False)]
        Declare PtrSafe Function GetPropertyCount Lib "gdiplus" Alias "GdipGetPropertyCount" (ByVal Me As GpImage, numOfProperty&) As GpStatus
        
        ' The simplest way to return a ref-counted item is to wrap it in an array.
        ' That forgoes the need for a class that represents the item.
        Function GetPropertyItem(ByVal propID As PROPID, item() As GpPropertyItem) As GpStatus
            Return GetPropertyItemImpl(Me.Native, propID, item)
        End Function
        [UseGetLastError(False)]
        Declare PtrSafe Function SetPropertyItem Lib "gdiplus" Alias "GdipSetPropertyItem" (ByVal Me As GpImage, item As GpPropertyItem) As GpStatus
        [UseGetLastError(False)]
        Declare PtrSafe Function GetPropertyItemSize Lib "gdiplus" Alias "GdipGetPropertyItemSize" (ByVal Me As GpImage, ByVal propId&, ByRef Size&) As GpStatus
        
        Function GetAllPropertyItems(items() As GpPropertyItem) As GpStatus
            Return GetAllPropertyItemsImpl(Me.Native, items)
        End Function
        [UseGetLastError(False)]
        Declare PtrSafe Function GetPropertySize Lib "gdiplus" Alias "GdipGetPropertySize" (ByVal Me As GpImage, totalBufferSize&, numProperties&) As GpStatus
        
        [UseGetLastError(False)]
        Declare PtrSafe Function RemovePropertyItem Lib "gdiplus" Alias "GdipRemovePropertyItem" (ByVal Me As GpImage, ByVal propId As PROPID) As GpStatus

        [Description("Returns an array with parameters. The array starts at index 1 even though LBound(params)=0")]
        Function GetEncoderParameterList(ByVal clsidencoder As GDIP_UUID, params() As GpEncoderParameter) As GpStatus
            Return GetEncoderParameterListImpl(Me.Native, clsidencoder, params)
        End Function
        [UseGetLastError(False)]
        Declare PtrSafe Function GetEncoderParameterListSize Lib "gdiplus" Alias "GdipGetEncoderParameterListSize" (ByVal Me As GpImage, clsidEncoder As GDIP_UUID, size&) As GpStatus
    
        Function FindFirstItem(item() As GpImageItemData) As GpStatus
            Return FindFirstItemImpl(Me.Native, item)
        End Function
        
        Function FindNextItem(item() As GpImageItemData) As GpStatus
            Return FindNextItemImpl(Me.Native, item)
        End Function
        
        Function GetItemData(item() As GpImageItemData) As GpStatus
            Return GetItemDataImpl(Me.Native, item)
        End Function
                
        [UseGetLastError(False)]
        Declare PtrSafe Function GetGraphicsContext Lib "gdiplus" Alias "GdipGetImageGraphicsContext" (ByVal Me As GpImage, ByVal Graphics As GpGraphics) As GpStatus
        [UseGetLastError(False)]
        [Description("⛔This API is not implemented in GdiPlus 1.0 nor 1.1." & vbCrLf _
            & "⛔NOTE: This API is a placeholder until tB supports no-vtable interfaces")]
        Declare PtrSafe Function SetAbort Lib "gdiplus" Alias "GdipImageSetAbort" (ByVal Me As GpImage, pIAbort As Any /*GdiplusAbort*/) As GpStatus
    End Type
    
    Function GpImage() As GpImage
        GpImage.Native = 0
    End Function
    
    '---------------------------------------------------------------'
    '                                                               '
    ' Implementation Details                                        '
    '                                                               '
    '---------------------------------------------------------------'
    
    [Description("There should be a dummy encoderParams(0) that's not filled in. It's used internally.")]
    Function SaveToFileImpl(ByVal Me As LongPtr, ByVal FileName$, ByVal clsidEncoder As GDIP_UUID, encoderParams() As GpEncoderParameter) As GpStatus
        If LBound(encoderParams) <> 0 Then Return InvalidParameter
        Dim stencil As GpEncoderParametersHeader
        Dim offset& = VarPtr(stencil.Parameter(1)) - VarPtr(stencil.Count)
        Dim countAddress As LongPtr = VarPtr(encoderParams(LBound(encoderParams))) - offset
        PutMem4(countAddress, UBound(encoderParams))
        Return GdipSaveImageToFile(Me, FileName, clsidEncoder, ByVal countAddress)
    End Function
    [UseGetLastError(False)]
    DeclareWide PtrSafe Function GdipSaveImageToFile Lib "gdiplus" (ByVal Me As LongPtr, ByVal FileName$, clsidEncoder As GDIP_UUID, encoderParams As Any) As GpStatus
        
    [Description("There should be a dummy encoderParams(0) that's not filled in. It's used internally.")]
    Function SaveToStreamImpl(ByVal Me As LongPtr, ByVal stream As GDIP_IStream, ByVal clsidEncoder As GDIP_UUID, encoderParams() As GpEncoderParameter) As GpStatus
        If LBound(encoderParams) <> 0 Then Return InvalidParameter
        Dim stencil As GpEncoderParametersHeader
        Dim offset& = VarPtr(stencil.Parameter(1)) - VarPtr(stencil.Count)
        Dim countAddress As LongPtr = VarPtr(encoderParams(LBound(encoderParams))) - offset
        PutMem4(countAddress, UBound(encoderParams))
        Return GdipSaveImageToStream(Me, stream, clsidEncoder, ByVal countAddress)
    End Function
    [UseGetLastError(False)]
    Declare PtrSafe Function GdipSaveImageToStream Lib "gdiplus" (ByVal Me As LongPtr, ByVal stream As GDIP_IStream, clsidEncoder As GDIP_UUID, encoderParams As Any) As GpStatus
            
    [Description("There should be a dummy encoderParams(0) that's not filled in. It's used internally.")]
    Function SaveAddImpl(ByVal Me As LongPtr, encoderParams() As GpEncoderParameter) As GpStatus
        If LBound(encoderParams) <> 0 Then Return InvalidParameter
        Dim stencil As GpEncoderParametersHeader
        Dim offset& = VarPtr(stencil.Parameter(1)) - VarPtr(stencil.Count)
        Dim countAddress As LongPtr = VarPtr(encoderParams(LBound(encoderParams))) - offset
        PutMem4(countAddress, UBound(encoderParams))
        Return GdipSaveAdd(Me, ByVal countAddress)
    End Function
    [UseGetLastError(False)]
    Declare PtrSafe Function GdipSaveAdd Lib "gdiplus" (ByVal Me As LongPtr, encoderParams As Any) As GpStatus
        
    [Description("There should be a dummy encoderParams(0) that's not filled in. It's used internally.")]
    Function SaveAddImageImpl(ByVal Me As LongPtr, ByVal newImage As LongPtr, encoderParams() As GpEncoderParameter) As GpStatus
        If LBound(encoderParams) <> 0 Then Return InvalidParameter
        Dim stencil As GpEncoderParametersHeader
        Dim offset& = VarPtr(stencil.Parameter(1)) - VarPtr(stencil.Count)
        Dim countAddress As LongPtr = VarPtr(encoderParams(LBound(encoderParams))) - offset
        PutMem4(countAddress, UBound(encoderParams))
        Return GdipSaveAddImage(Me, newImage, ByVal countAddress)
    End Function
    [UseGetLastError(False)]
    Declare PtrSafe Function GdipSaveAddImage Lib "gdiplus" (ByVal Me As LongPtr, ByVal newImage As LongPtr, encoderParams As Any) As GpStatus
    
    Function GetSizeImpl(Of T)(Me As T, size As GpSize) As GpStatus
        Dim status As Any = Me.GetHeight(size.Height)
        If status = Ok Then status = Me.GetWidth(size.Width)
        Return status
    End Function
    
    Function GetFrameDimensionsListImpl(ByVal Me As LongPtr, dims As GDIP_UUID()) As GpStatus
        Dim count&
        Dim status As Any = GdipImageGetFrameDimensionsCount(Me, count)
        If status = Ok Then
            ReDim dims(1 To count&)
            status = GdipImageGetFrameDimensionsList(Me, dims(1), count)
            If status <> Ok Then Erase dims
        End If
        Return status
    End Function
    [UseGetLastError(False)]
    Declare PtrSafe Function GdipImageGetFrameDimensionsCount Lib "gdiplus" (ByVal Me As LongPtr, count As Long) As GpStatus
    [UseGetLastError(False)]
    Declare PtrSafe Function GdipImageGetFrameDimensionsList Lib "gdiplus" (ByVal Me As LongPtr, dimensionIDs As GDIP_UUID, ByVal count As Long) As GpStatus
        
    Function GetPropertyIdListImpl(ByVal Me As LongPtr, propIDs() As PROPID) As GpStatus
        Dim count&
        Dim status As Any = GdipGetPropertyCount(Me, count)
        If status = Ok Then
            ReDim propIDs(1 To count)
            status = GdipGetPropertyIdList(Me, count, propIDs(1))
            If status <> Ok Then Erase propIDs
        End If
        Return status
    End Function
    [UseGetLastError(False)]
    Declare PtrSafe Function GdipGetPropertyCount Lib "gdiplus" (ByVal Me As LongPtr, numOfProperty&) As GpStatus
    [UseGetLastError(False)]
    Declare PtrSafe Function GdipGetPropertyIdList Lib "gdiplus" (ByVal Me As LongPtr, ByVal numOfProperty&, list&) As GpStatus
    
    Function GetPropertyItemImpl(ByVal Me As LongPtr, ByVal propID As PROPID, item() As GpPropertyItem) As GpStatus
        Dim size&
        Dim status As Any = GdipGetPropertyItemSize(Me, propID, size)
        If status = Ok Then
            ReDimOversized(item, 1, 1, size)
            status = GdipGetPropertyItem(Me, propID, size, item(1))
            If status <> Ok Then Erase item
        End If
        Return status
    End Function
    [UseGetLastError(False)]
    Declare PtrSafe Function GdipGetPropertyItemSize Lib "gdiplus" (ByVal Me As LongPtr, ByVal propId&, Size&) As GpStatus
    [UseGetLastError(False)]
    Declare PtrSafe Function GdipGetPropertyItem Lib "gdiplus" (ByVal Me As LongPtr, ByVal propId&, ByVal propSize&, ByRef buffer As Any) As GpStatus

   
    Function GetAllPropertyItemsImpl(ByVal Me As LongPtr, items() As GpPropertyItem) As GpStatus
        Dim size&, count&
        Dim status As Any = GdipGetPropertySize(Me, size, count)
        If status = Ok Then
            ReDimOversized(items, 1, count, size)
            status = GdipGetAllPropertyItems(Me, size, count, items(1))
            If status <> Ok Then Erase items
        End If
        Return status
    End Function
    [UseGetLastError(False)]
    Declare PtrSafe Function GdipGetPropertySize Lib "gdiplus" (ByVal Me As LongPtr, totalBufferSize&, numProperties&) As GpStatus
    [UseGetLastError(False)]
    Declare PtrSafe Function GdipGetAllPropertyItems Lib "gdiplus" (ByVal Me As LongPtr, ByVal totalBufferSize&, ByVal numProperties&, allItems As Any) As GpStatus
    
    [Description("Returns an array with parameters. The array starts at index 1 even though LBound(params)=0")]
    Function GetEncoderParameterListImpl(ByVal Me As LongPtr, ByVal clsidencoder As GDIP_UUID, params() As GpEncoderParameter) As GpStatus
        Dim size&
        Dim status As Any = GdipGetEncoderParameterListSize(Me, clsidencoder, size)
        If status = Ok Then
            Dim stencil As GpEncoderParametersHeader
            Dim offset& = VarPtr(stencil.Parameter(1)) - VarPtr(stencil.Count)
            Dim count&
            ReDimOversized(params, 0, 1, size + LenB(Of GpEncoderParameter))
            status = GdipGetEncoderParameterList(Me, clsidencoder, size, VarPtr(params(1)) - offset)
            If status = Ok Then
                GetMem4(VarPtr(params(1)) - offset, count)
                SetOversizedCount(params, count + 1)
                ' The extra element is element 0 that stores the count, not an actual parameter
            Else
                Erase params
            End If
        End If
        Return status
    End Function
    [UseGetLastError(False)]
    Declare PtrSafe Function GdipGetEncoderParameterListSize Lib "gdiplus" (ByVal Me As LongPtr, clsidEncoder As GDIP_UUID, size&) As GpStatus
    [UseGetLastError(False)]
    Declare PtrSafe Function GdipGetEncoderParameterList Lib "gdiplus" (ByVal Me As LongPtr, clsidEncoder As GDIP_UUID, ByVal size&, buffer As Any) As GpStatus
    
    Function FindFirstItemImpl(ByVal Me As LongPtr, item() As GpImageItemData) As GpStatus
        Dim byteSize&= LenB(Of GpImageItemData) + 11
        ReDimOversized(item, 1, 1, byteSize)
        item(1).Size = byteSize
        item(1).DescSize = 11
        item(1).Desc = VarPtr(item(1)) + LenB(Of GpImageItemData)
        Return GdipFindFirstImageItem(Me, item(1))
    End Function
        
    Function FindNextItemImpl(ByVal Me As LongPtr, item() As GpImageItemData) As GpStatus
        Dim byteSize&= LenB(Of GpImageItemData) + 11
        ReDimOversized(item, 1, 1, byteSize)
        item(1).Size = byteSize
        item(1).DescSize = 11
        item(1).Desc = VarPtr(item(1)) + LenB(Of GpImageItemData)
        Return GdipFindNextImageItem(Me, item(1))
    End Function
        
    Function GetItemDataImpl(ByVal Me As LongPtr, item() As GpImageItemData) As GpStatus
        Dim byteSize&= LenB(Of GpImageItemData) + 11
        Dim status As GpStatus
        item(1).Data = 0
        status = GdipGetImageItemData(Me, item(1))   ' sets DataSize
        If status = Ok Then
            If item(1).DataSize < 1 Then Return WrongState
            byteSize += item(1).DataSize
            ReDimOversized(item, 1, 1, byteSize)
            item(1).Size = byteSize
            item(1).Desc = VarPtr(item(1)) + LenB(Of GpImageItemData)
            item(1).Data = VarPtr(item(1)) + LenB(Of GpImageItemData) + item(1).DescSize
            status = GdipGetImageItemData(Me, item(1))   ' populates data
        End If
        Return status
    End Function
    
    [UseGetLastError(False)]
    Declare PtrSafe Function GdipFindFirstImageItem Lib "gdiplus" (ByVal Me As LongPtr, item As GpImageItemData) As GpStatus
    [UseGetLastError(False)]
    Declare PtrSafe Function GdipFindNextImageItem Lib "gdiplus" (ByVal Me As LongPtr, item As GpImageItemData) As GpStatus
    [UseGetLastError(False)]
    Declare PtrSafe Function GdipGetImageItemData Lib "gdiplus" (ByVal Me As LongPtr, item As GpImageItemData) As GpStatus
End Module
