' A part of the GDI+ Package for TwinBasic  https://github.com/kubao/GdiPlus
' Licensed under the MIT license. (c) 2026 Sunderland Ober Consulting.
' "Windows" is a trademark of the Microsoft Corporation.
' Modeled after code and descriptions (c) Microsoft, taken from SDK headers and official documentation.
' Certain flat API function and type declarations taken from WinDevLib by fafalone, with thanks.

[COMCreatable(False)]
Class GdiPlusBase
    Private LastStatus As GpStatus
    
    Property Get LastResult() As GpStatus
        Return LastStatus
    End Property
    
    Function GetLastResult() As GpStatus
        GetLastResult = LastStatus
        LastStatus = Ok
    End Function
    
    Protected Function SetStatus(ByVal status As GpStatus) As GpStatus
        If status <> Ok Then
            LastStatus = status
        End If
        Return status
    End Function
    
    Static Function Alloc(size As LongPtr) As LongPtr
        Return GdipAlloc(size)
    End Function
    
    Static Sub Free(ptr As LongPtr)
        GdipFree(ptr)
    End Sub
    
    Property Get Status() As String
        Return AsText$(LastStatus)
    End Property
    
    Property Get StatusNL() As String
        Return AsText$(LastStatus) & vbCrLf
    End Property
    
    Protected Function CloneImpl(Of T, N)(ByVal native As N) As T
        Dim cloned As N
        SetStatus(native.Clone(cloned))
        If LastResult = Ok Then
            Return New T(cloned)
        End If
    End Function
    
    Protected Function CloneImpl2(Of T, N, Nin)(ByVal native As Nin) As T
        Dim cloned As N, from As N
        from.Native = native.Native
        SetStatus(from.Clone(cloned))
        If LastResult = Ok Then
            Return New T(cloned)
        End If
    End Function
End Class

Module GdipModule
    [UseGetLastError(False)]
    Declare PtrSafe Function GdipAlloc Lib "gdiplus.dll" (ByVal Size As LongPtr) As LongPtr
    [UseGetLastError(False)]
    Declare PtrSafe Function GdipFree Lib "gdiplus.dll" (ByVal Ptr As LongPtr) As GpStatus
End Module
