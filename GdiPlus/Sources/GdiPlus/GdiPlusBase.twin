' A part of the GDI+ Package for TwinBasic  https://github.com/kubao/GdiPlus
' Licensed under the MIT license. (c) 2026 Sunderland Ober Consulting.
' "Windows" is a trademark of the Microsoft Corporation.
' Modeled after code and descriptions (c) Microsoft, taken from SDK headers and official documentation.
' Certain flat API function and type declarations taken from WinDevLib by fafalone, with thanks.

[COMCreatable(False)]
Class GdiPlusBase
    Private _lastStatus As GpStatus
    
    Property Get LastResult() As GpStatus
        Return _lastStatus
    End Property
    
    Function ClearResult() As GpStatus
        Return GetLastResult()
    End Function
    
    Function GetLastResult() As GpStatus
        GetLastResult = _lastStatus
        _lastStatus = Ok
    End Function
    
    Protected Function SetStatus(ByVal status As GpStatus) As GpStatus
        If status <> Ok Then
            _lastStatus = status
            Select Case GdipErrorHandlingStrategy
                Case RaiseErrors
                    Err.Raise(GdipErrorCodeBase& + _lastStatus, "GdiPlus:" & TypeName(Me), status)
                Case HandleErrors
                    If GdipErrorHandler <> 0 Then GdipErrorHandler(Me)
                Case IgnoreErrors
                    '
            End Select
        End If
        Return status
    End Function
    
    Static Function Alloc(size As LongPtr) As LongPtr
        Return GdipAlloc(size)
    End Function
    
    Static Sub Free(ptr As LongPtr)
        GdipFree(ptr)
    End Sub
    
    Property Get Status() As String
        Return AsText$(_lastStatus)
    End Property
    
    Property Get StatusNL() As String
        Return AsText$(_lastStatus) & vbCrLf
    End Property
    
    Protected Function CloneImpl(Of T, N)(ByVal native As N) As T
        Dim cloned As N
        SetStatus(native.Clone(cloned))
        If LastResult = Ok Then
            Return New T(cloned)
        End If
    End Function
    
    Protected Function CloneImpl2(Of T, N, Nin)(ByVal native As Nin) As T
        Dim cloned As N, from As N
        from.Native = native.Native
        SetStatus(from.Clone(cloned))
        If LastResult = Ok Then
            Return New T(cloned)
        End If
    End Function
End Class

Module GdipModule
    Enum GpErrorHandlingStrategy
        IgnoreErrors
        RaiseErrors
        HandleErrors
    End Enum
    
    [Description("The base error code in the RaiseErrors strategy. GpStatus is added to this base code.")]
    Public GdipErrorCodeBase&= 3300
    [Description("Whether the errors from GdiPlus objects are ignored, Err.Raise-d, or processed by a handler.")]
    Public GdipErrorHandlingStrategy As GpErrorHandlingStrategy = IgnoreErrors
    [Description("The error handler to be invoked upon errors when the strategy is HandleErrors.")]
    Public GdipErrorHandler As GdipErrorHandler = 0
    
    Public Delegate Function GdipErrorHandler (ByVal obj As GdiPlusBase)
    
    [UseGetLastError(False)]
    Declare PtrSafe Function GdipAlloc Lib "gdiplus.dll" (ByVal Size As LongPtr) As LongPtr
    [UseGetLastError(False)]
    Declare PtrSafe Function GdipFree Lib "gdiplus.dll" (ByVal Ptr As LongPtr) As GpStatus
End Module
