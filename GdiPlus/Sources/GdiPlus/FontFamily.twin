' A part of the GDI+ Package for TwinBasic  https://github.com/kubao/GdiPlus
' Licensed under the MIT license. (c) 2026 Sunderland Ober Consulting.
' "Windows" is a trademark of the Microsoft Corporation.
' Modeled after code and descriptions (c) Microsoft, taken from SDK headers and official documentation.
' Certain flat API function and type declarations taken from WinDevLib by fafalone, with thanks.

[COMCreatable(False)]
Class FontFamily
    Inherits GdiPlusBase
    Dim ff As GpFontFamily
        
    Sub New()
        ' empty
    End Sub
    
    Sub New(ByVal name$, Optional fontCollection As FontCollection)
        LastStatus = GdipCreateFontFamilyFromName(name, GetNative(fontCollection), ff)
    End Sub
        
    Sub Class_Terminate()
        GdipDeleteFontFamily(ff)
    End Sub
    
    Property Get FamilyName(ByVal language As LANGID = 0) As String
        FamilyName = Space$(LF_FACESIZE)
        SetStatus(GdipGetFamilyName(ff, FamilyName, language))
    End Property
        
    Function Clone() As FontFamily
        Dim clonedFamily As GpFontFamily
        SetStatus(GdipCloneFontFamily(ff, clonedFamily))
        If LastStatus = Ok Then Set Clone = New FontFamily(clonedFamily, LastResult)
    End Function
    
    Property Get IsAvailable() As Boolean
        Return ff.Native <> vbNullPtr
    End Property
    
    Property Get IsBoldAvailable() As Boolean
        Return IsStyleAvailable(FontStyleBold)
    End Property

    Property Get IsItalicAvailable() As Boolean
        Return IsStyleAvailable(FontStyleItalic)
    End Property

    Property Get IsBoldItalicAvailable() As Boolean
        Return IsStyleAvailable(FontStyleBoldItalic)
    End Property
    
    Property Get IsUnderlinedAvailable() As Boolean
        Return IsStyleAvailable(FontStyleUnderline)
    End Property

    Property Get IsStrikeoutAvailable() As Boolean
        Return IsStyleAvailable(FontStyleStrikeout)
    End Property
    
    Function IsStyleAvailable(ByVal style As GpFontStyle) As Boolean
        Dim booln As BOOL
        SetStatus(GdipIsStyleAvailable(ff, style, booln))
        If LastStatus = Ok Then IsStyleAvailable = booln
    End Function
    
    Property Get EmHeight(ByVal style&) As Integer
        SetStatus(GdipGetEmHeight(ff, style, EmHeight))
    End Property
    
    Property Get CellAscent(ByVal style&) As Integer
        SetStatus(GdipGetCellAscent(ff, style, CellAscent))
    End Property
        
    Property Get CellDescent(ByVal style&) As Integer
        SetStatus(GdipGetCellDescent(ff, style, CellDescent))
    End Property

    Property Get LineSpacing(ByVal style&) As Integer
        SetStatus(GdipGetLineSpacing(ff, style, LineSpacing))
    End Property
    
    Protected Property Get Native() As GpFontFamily
        Return ff
    End Property
        
    Protected Sub SetNativeFontFamily(ByVal nativeFamily As GpFontFamily)
        ff = nativeFamily
    End Sub
    
    ' Used in Font
    Public Sub New(ByVal nativeFamily As GpFontFamily)
        LastStatus = Ok
        SetNativeFontFamily(nativeFamily)
    End Sub
    
    ' Used in FontFamilyModule
    Public Sub New(ByVal nativeFamily As GpFontFamily, ByVal status As GpStatus)
        LastStatus = status
        SetNativeFontFamily(nativeFamily)
    End Sub
    
    Private Const LF_FACESIZE = 32
    Private Const LF_FULLFACESIZE = 64
End Class

Alias WORD As Integer
Alias LANGID As Integer  ' WORD

Module GdipFontFamily
    Function FontFamily(ByVal name$, Optional fontCollection As FontCollection) As FontFamily
        Return New FontFamily(name, fontCollection)
    End Function
    
    Type GpFontFamily
        Native As LongPtr
    End Type
    
    Function GpFontFamily() As GpFontFamily
        GpFontFamily.Native = 0
    End Function
    
    Function GenericSansSerif() As FontFamily
        If GenericSansSerifFF IsNot Nothing Then Return GenericSansSerifFF
        Dim nativeFF As GpFontFamily
        Dim status As GpStatus = GdipGetGenericFontFamilySansSerif(nativeFF)
        Set GenericSansSerifFF = New FontFamily(nativeFF, status)
    End Function
    
    Function GenericSerif() As FontFamily
        If GenericSerifFF IsNot Nothing Then Return GenericSerifFF
        Dim nativeFF As GpFontFamily
        Dim status As GpStatus = GdipGetGenericFontFamilySerif(nativeFF)
        Set GenericSerifFF = New FontFamily(nativeFF, status)
    End Function
    
    Function GenericMonospace() As FontFamily
        If GenericMonospaceFF IsNot Nothing Then Return GenericMonospaceFF
        Dim nativeFF As GpFontFamily
        Dim status As GpStatus = GdipGetGenericFontFamilyMonospace(nativeFF)
        Set GenericMonospaceFF = New FontFamily(nativeFF, status)
    End Function
        
    Dim GenericSansSerifFF As FontFamily
    Dim GenericSerifFF As FontFamily
    Dim GenericMonospaceFF As FontFamily
    
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipGetGenericFontFamilySerif Lib "gdiplus" (Me As GpFontFamily) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipGetGenericFontFamilySansSerif Lib "gdiplus" (Me As GpFontFamily) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipGetGenericFontFamilyMonospace Lib "gdiplus" (Me As GpFontFamily) As GpStatus

    [UseGetLastError(False)]
    Public DeclareWide PtrSafe Function GdipCreateFontFamilyFromName Lib "gdiplus" (ByVal Name As String, ByVal fontCollection As GpFontCollection, fontFamily As GpFontFamily) As GpStatus
    [UseGetLastError(False)]
    Public DeclareWide PtrSafe Function GdipGetFamilyName Lib "gdiplus" (ByVal Me As GpFontFamily, ByVal Name As String, ByVal language As Integer) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipDeleteFontFamily Lib "gdiplus" (ByVal Me As GpFontFamily) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipCloneFontFamily Lib "gdiplus" (ByVal Me As GpFontFamily, clonedFontFamily As GpFontFamily) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipIsStyleAvailable Lib "gdiplus" (ByVal Me As GpFontFamily, ByVal style As Long, IsStyleAvailable As BOOL) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipGetEmHeight Lib "gdiplus" (ByVal Me As GpFontFamily, ByVal style As Long, EmHeight As Integer) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipGetCellAscent Lib "gdiplus" (ByVal Me As GpFontFamily, ByVal style As Long, CellAscent As Integer) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipGetCellDescent Lib "gdiplus" (ByVal Me As GpFontFamily, ByVal style As Long, CellDescent As Integer) As GpStatus
    [UseGetLastError(False)]
    Public Declare PtrSafe Function GdipGetLineSpacing Lib "gdiplus" (ByVal Me As GpFontFamily, ByVal style As Long, LineSpacing As Integer) As GpStatus
End Module
